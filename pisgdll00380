|******************************************************************************
|* tpisgdll0038  0  VRC B61U a7 live
|* 
|* tools1
|* 14-06-20 [14:52]
|******************************************************************************
|* Script Type: Library
|******************************************************************************
|*  ID	ISGECDV001094	Sujeet Kumar,		2014-10-09
|* Multiple Installment Export Invoicing is not processed,
|* Due to Status of the Lines will be On-Hold, instead of Confirmed
|* Functional: Mr. Vikas Yadav
|*
|******************************************************************************
#pragma used dll ottstpapihand 
#include <bic_dam>
	table	ttpppc215
	table	ttpppc295
	table	ttppin020
	table	tcisli235
	table	ttpisg039

function extern string tpisgdll0038.Transfer.Project.Data.to.Central.Invoicing
			(
				domain	tppdm.yeno	i.adva.yeno,		|Advance Payment Requests
				domain	tppdm.yeno	i.unit.yeno,		|Unit Rates
				domain	tppdm.yeno	i.inst.yeno,		|Installments
				domain	tppdm.yeno	i.cstl.yeno,		|Extensions
				domain	tppdm.yeno	i.cost.plus.yeno,	|Cost Plus
				domain	tppdm.yeno	i.hdbk.yeno,		|Hold Back
				domain	tccprj		i.cprj.f,		|Project From
				domain	tccprj		i.cprj.t,		|Project To
				domain	tccom.bpid	i.ofbp.f,		|Sold-to Business Partner
				domain	tccom.bpid	i.ofbp.t,		|Sold-to Business Partern
				domain	tpptc.cstl	i.cstl.f,		|Extension From
				domain	tpptc.cstl	i.cstl.t,		|Extension To
				domain	tppdm.date	i.ltdt.f,		|Transaction Time From
				domain	tppdm.date	i.ltdt.t,		|Transaction Time To
				domain	tppdm.yeno	i.draft.print,		|Print Draft Invoice
				domain	tppdm.yeno	i.euro.print,		|Print Euro
				domain	cisli.psco	i.f.print.seq,		|Printing Sequence
				domain	tppdm.s014	i.draft.device,		|Device for Draft Invoice
				domain	tpint.sli.stat	i.invoice.status,	|Invoice Status
				domain	tcdate		i.cut.off.date		|Cut Off Date
			)
{
	string error.msg(500)
	
	if i.adva.yeno <> empty then
		stpapi.put.field("tppin4200m000","adva.yeno",str$(etol(i.adva.yeno)))
	endif
	
	if i.unit.yeno <> empty then
		stpapi.put.field("tppin4200m000","unit.yeno",str$(etol(i.unit.yeno)))
	endif
	
	if i.inst.yeno <> empty then
		stpapi.put.field("tppin4200m000","inst.yeno",str$(etol(i.inst.yeno)))
	endif
	
	if i.cstl.yeno <> empty then
		stpapi.put.field("tppin4200m000","cstl.yeno",str$(etol(i.cstl.yeno)))
	endif
	
	if i.cost.plus.yeno <> empty then
		stpapi.put.field("tppin4200m000","cost.plus.yeno",str$(etol(i.cost.plus.yeno)))
	endif
	
	if i.hdbk.yeno <> empty then
		stpapi.put.field("tppin4200m000","hdbk.yeno",str$(etol(i.hdbk.yeno)))
	endif
	
	if isspace(i.cprj.f) then
		stpapi.put.field("tppin4200m000","cprj.f","")
	else
		stpapi.put.field("tppin4200m000","cprj.f",i.cprj.f)
	endif
	
	if not isspace(i.cprj.t) then
		stpapi.put.field("tppin4200m000","cprj.t",i.cprj.t)
	else
		stpapi.put.field("tppin4200m000","cprj.t","ZZZZZZZZZ")
	endif
	
	if isspace(i.ofbp.f) then
		stpapi.put.field("tppin4200m000","ofbp.f","")
	else
		stpapi.put.field("tppin4200m000","ofbp.f",i.ofbp.f)
	endif
	
	if not isspace(i.ofbp.t) then
		stpapi.put.field("tppin4200m000","ofbp.t",i.ofbp.t)
	else
		stpapi.put.field("tppin4200m000","ofbp.t","ZZZZZZZZZ")
	endif
	
	if i.cstl.yeno = tppdm.yeno.yes then
		if isspace(i.cstl.f) then
			stpapi.put.field("tppin4200m000","cstl.f","")
		else
			stpapi.put.field("tppin4200m000","cstl.f",i.cstl.f)
		endif
		
		if not isspace(i.cstl.t) then
			stpapi.put.field("tppin4200m000","cstl.t",i.cstl.t)
		else
			stpapi.put.field("tppin4200m000","cstl.t","ZZZZ")
		endif
	endif
	
	if i.cost.plus.yeno = tppdm.yeno.yes then
	
		if i.ltdt.f <> 0 then		
			stpapi.put.field("tppin4200m000","ltdt.f",str$(i.ltdt.f))
		endif
		
		if i.ltdt.t <> 0 then
			stpapi.put.field("tppin4200m000","ltdt.t",str$(i.ltdt.t))
		else
			stpapi.put.field("tppin4200m000","ltdt.t",str$(utc.num()))
		endif
	endif
	
	if i.draft.print <> empty then
		stpapi.put.field("tppin4200m000","draft.print",str$(etol(i.draft.print)))
	endif
	
	if i.euro.print <> empty then
		stpapi.put.field("tppin4200m000","euro.print",str$(etol(i.euro.print)))
	endif
	
	if not isspace(i.f.print.seq) then
		stpapi.put.field("tppin4200m000","f.print.seq",i.f.print.seq)
	endif
	
	if i.draft.print = tppdm.yeno.yes then
		if not isspace(i.draft.device) then
			stpapi.put.field("tppin4200m000","draft.device",i.draft.device)
		endif
	endif
	
	if  i.invoice.status <> empty then 
		stpapi.put.field("tppin4200m000","invoice.status",str$(etol(i.invoice.status)))
	endif
	
	if i.cut.off.date <> 0 then
		stpapi.put.field("tppin4200m000","cut.off.date",str$(i.cut.off.date))
	endif
	
	if i.draft.print = tppdm.yeno.yes or i.euro.print = tppdm.yeno.yes then
		stpapi.set.report("tppin4200m000","rtppin420001000",i.draft.device,error.msg)
	else
		stpapi.set.report("tppin4200m000","rtppin420001000","D",error.msg)
	endif
	
	stpapi.form.command("tppin4200m000",5,"exec.cont.process",error.msg)
	
	stpapi.end.session("tppin4200m000")
	
	return(error.msg)
}


function extern long tpisgdll0038.Material.Transaction.to.be.Invoiced
			(
				domain	tccprj		i.cprj,		|Project
				domain	tppdm.cspa	i.cspa,		|Element
				domain	tcitem		i.item,		|Item
				domain	tppdm.serf	i.sern		|Sequence Number	
			)
{
	long ret_val
	
	select 	tpppc215.blbl,
		tpppc215.invo,
		tpppc215.vasv.l,
		tpppc215.sasv.l,
		tpppc215.amos
	from	tpppc215 for update
	where	tpppc215._index1 = {:i.cprj,:i.cspa,:i.item,:i.sern}
	selectdo
		dal.change.object("tpppc215")
		dal.set.field("tpppc215.blbl",tcyesno.yes)
		dal.set.field("tpppc215.invo",tcyesno.yes)
		
		dal.set.field("tpppc215.vasv.l",tpppc215.amos)
		dal.set.field("tpppc215.sasv.l",tpppc215.amos)
		
		ret_val = dal.save.object("tpppc215")
		if ret_val <> 0 then
			return(DALHOOKERROR)
		endif
	endselect
	return(0)
}

function extern long tpisgdll0038.Material.Transaction.to.be.Invoiced.sundry.cost
			(
				domain	tccprj		i.cprj,		|Project
				domain	tppdm.cspa	i.cspa,		|Element
				domain	tppdm.cico	i.cico,		|Sundry Cost
				domain	tppdm.serf	i.sern		|Sequence Number	
			)
{
	long ret_val
	
	select 	tpppc295.blbl,
		tpppc295.invo,
		tpppc295.vasv.l,
		tpppc295.sasv.l,
		tpppc295.amos
	from	tpppc295 for update
	where	tpppc295._index1 = {:i.cprj,:i.cspa,:i.cico,:i.sern}
	selectdo
		dal.change.object("tpppc295")
		dal.set.field("tpppc295.blbl",tcyesno.yes)
		dal.set.field("tpppc295.invo",tcyesno.yes)
		
		dal.set.field("tpppc295.vasv.l",tpppc295.amos)
		dal.set.field("tpppc295.sasv.l",tpppc295.amos)
		
		ret_val = dal.save.object("tpppc295")
		if ret_val <> 0 then
			return(DALHOOKERROR)
		endif
	endselect
	return(0)
}

function extern string tpisgdll0038.BillingRequestsAFS
						(
							domain	cisli.brtp	i.brtp,		|Template
							domain	cisli.brad	i.brad,		|Billing Request Addition
							domain	tccom.bpid	i.itbf,		|Invoice To Business Partner
							domain	tctran		i.ityp,		|Sales Invoice Transaction
							domain	tcsenm		i.seri,		|Series Number
| 							domain	tcshpm		i.shpf,		|Shipment From
							domain	tcncmp		i.cpjf,		|Logistic Company
							domain	tccprj		i.prjf,		|Project
						ref 	domain	tcorno		o.brid,		|Billing Request Number
						ref	domain	tcncmp		o.scmp		|Sales Invoice Company
						)
{
	string error.message(500)
	string	error(500),tmp.scmp(3)				
	long ret_val
	
	stpapi.synchronize.dialog("cisli2100m000", "Add", error.message)
	
	if not isspace(i.brtp) then
		stpapi.put.field("cisli2100m000","cisli200.brtp",i.brtp)
	endif
	
	if not isspace(i.brad) then
		stpapi.put.field("cisli2100m000","cisli200.brad",i.brad)
	endif
	
	if not isspace(i.itbf) then
		stpapi.put.field("cisli2100m000","cisli200.itbf",i.itbf)
		stpapi.put.field("cisli2100m000","cisli200.itbt",i.itbf)
	endif
	
	if not isspace(i.ityp) then
		stpapi.put.field("cisli2100m000","cisli200.ityp",i.ityp)
	endif
	
	if i.seri <> 0 then
		stpapi.put.field("cisli2100m000","cisli200.seri",str$(i.seri))
	endif
	
	
	if i.cpjf <> 0 then
		stpapi.put.field("cisli2100m000","cisli200.cpjf",str$(i.cpjf))
	endif
	
	if not isspace(i.prjf) then
		stpapi.put.field("cisli2100m000","cisli200.prjf",i.prjf)
	endif
	
	
	ret_val = stpapi.insert("cisli2100m000",true,error.message)
	
	if ret_val = 1 then
		stpapi.get.field("cisli2100m000","f.billing.request",o.brid)
		stpapi.get.field("cisli2100m000","cisli200.scmp",tmp.scmp)
		o.scmp = lval(tmp.scmp)
	else
		stpapi.recover("cisli2100m000",error)				
	endif
	error.message = error.message & "" & error				
	stpapi.end.session("cisli2100m000")
	
	return(error.message)
		
}


function extern string tpisgdll0038.Compose_Print_PostInvoice
					(
						domain	tcyesno		i.f.select.br.set,		|Billing Request Set
						domain	tcorno		i.f.billing.req.f,		|Billing Request Number
						domain	tcyesno		i.f.composing,			|Composing
						domain	tcyesno		i.f.br.compose,			|Compose 
						domain	tcdate		i.f.invoice.date,		|Invoice Date
						domain	tcyesno		i.f.br.print,			|Printing
						domain	cisli.prno	i.f.print.option,
						domain	tcyesno		i.f.print.inv.dlv.m,		|Invoice Delivery Method
						domain	tcyesno		i.f.print.in.euro,		|Euro
						domain	cisli.psco	i.f.print.seq,			|Printing Sequence
						domain	tcyesno		i.f.br.post,			|Posting
						domain	tcdate		i.f.post.trans.dt		|Transaction Entry Date	
					)
{
	string error.message(500)
	
	if i.f.select.br.set <> empty then
		stpapi.put.field("cisli2200m000","f.select.br.set",str$(etol(i.f.select.br.set)))
	endif
	
	if  not isspace(i.f.billing.req.f) then
		stpapi.put.field("cisli2200m000","f.billing.req.f",i.f.billing.req.f)
		stpapi.put.field("cisli2200m000","f.billing.req.t",i.f.billing.req.f)
	endif
	
	if i.f.composing <> empty then
		stpapi.put.field("cisli2200m000","f.composing",str$(etol(i.f.composing)))
	endif
	
	if i.f.br.compose <> empty then
		stpapi.put.field("cisli2200m000","f.br.compose",str$(etol(i.f.br.compose)))
	endif
	
	if i.f.invoice.date <> 0 then
		stpapi.put.field("cisli2200m000","f.invoice.date",str$(i.f.invoice.date))
	endif
	
	if i.f.br.print <> empty then
		stpapi.put.field("cisli2200m000","f.br.print",str$(etol(i.f.br.print)))
	endif
	
	if i.f.print.option <> empty then
		stpapi.put.field("cisli2200m000","f.print.option",str$(etol(i.f.print.option)))
	endif
	
	if i.f.print.inv.dlv.m <> empty then
		stpapi.put.field("cisli2200m000","f.print.inv.dlv.m",str$(etol(i.f.print.inv.dlv.m)))
	endif
	
	if i.f.print.in.euro <> empty then
		stpapi.put.field("cisli2200m000","f.print.in.euro",str$(etol(i.f.print.in.euro)))
	endif
	
	
	if not isspace(i.f.print.seq) then
		stpapi.put.field("cisli2200m000","f.print.seq",i.f.print.seq)
	endif
	
	if i.f.br.post <> empty then
		stpapi.put.field("cisli2200m000","f.br.post",str$(etol(i.f.br.post)))
	endif
	
	if i.f.post.trans.dt <> 0 then
		stpapi.put.field("cisli2200m000","f.post.trans.dt",str$(i.f.post.trans.dt))
	endif
	
	stpapi.set.report("cisli2200m000","rcisli120021000","D",error.message)
	stpapi.continue.process("cisli2200m000",error.message)
	stpapi.form.command("cisli2200m000",5,"exec.cont.process",error.message)
	
	stpapi.end.session("cisli2200m000")
	return(error.message)
}

function extern long tpisgdll0038.Change.Status.of.Project.Inst.data
					(
						domain	tcorno		i.edrn,	|Billing Advice
						domain	tcncmp		i.comp,	|Logistic Company
						domain	tccprj		i.cprj, |Project
						domain	tccom.bpid	i.bpid	|Sold to Business Partner
					)
{
	domain	tcorno	ins.edrn
	long	ret
	

	select 	tpisg039.nins,
		tpisg039.edrn,
		tpisg039.sidt
	from	tpisg039
	where	tpisg039._index1 = {:i.cprj,:i.bpid}
	and	tpisg039.idoc = 0 
	and	tpisg039.sidt <> 0
	selectdo
		select	cisli235.*
		from	cisli235 for update
		where	cisli235._index1 = {:i.comp, :i.cprj, :i.bpid, tcinvt.installment}
		and	cisli235.nins = :tpisg039.nins
		and	cisli235.codt = :tpisg039.sidt
		and	cisli235.stat in (cisli.stat.on.hold, cisli.stat.confirmed)
		selectdo
			dal.change.object("cisli235")
			if	i.edrn = tpisg039.edrn	then
				if	cisli235.stat = cisli.stat.on.hold	then
					dal.set.field("cisli235.stat", cisli.stat.confirmed)
				endif
			else
				if	cisli235.stat = cisli.stat.confirmed	then
					dal.set.field("cisli235.stat", cisli.stat.on.hold)
				endif
			endif
			ret = dal.save.object("cisli235")
			if	ret = 0	then
			else
				return(DALHOOKERROR)
			endif
		endselect
	endselect
	
	return(0)
}						

							|#ISGECDV001094.sn
function extern long tpisgdll0038.Change.Status.of.Project.Inst.data.for.export
					(
						domain	tcorno		i.cinv,	|Commerical Invoice No 
						domain	tcncmp		i.comp,	|Logistic Company
						domain	tccprj		i.cprj, |Project
						domain	tccom.bpid	i.bpid	|Sold to Business Partner
					)
{
	domain	tcorno	ins.edrn
	long	ret
	domain	tcorno	o.cinv

	select 	tpisg039.nins,
		tpisg039.edrn,
		tpisg039.sidt,
		tdisg831.cinv:o.cinv
	from	tpisg039,tdisg831
	where	tpisg039._index1 = {:i.cprj,:i.bpid}
	and	tpisg039.idoc = 0 
	and	tpisg039.sidt <> 0
	and	tpisg039.edrn refers to tdisg831
	selectdo
		select	cisli235.*
		from	cisli235 for update
		where	cisli235._index1 = {:i.comp, :i.cprj, :i.bpid, tcinvt.installment}
		and	cisli235.nins = :tpisg039.nins
		and	cisli235.codt = :tpisg039.sidt
		and	cisli235.stat in (cisli.stat.on.hold, cisli.stat.confirmed)
		selectdo
			dal.change.object("cisli235")
			if	i.cinv = o.cinv	then
				if	cisli235.stat = cisli.stat.on.hold	then
					dal.set.field("cisli235.stat", cisli.stat.confirmed)
				endif
			else
				if	cisli235.stat = cisli.stat.confirmed	then
					dal.set.field("cisli235.stat", cisli.stat.on.hold)
				endif
			endif
			ret = dal.save.object("cisli235")
			if	ret = 0	then
			else
				return(DALHOOKERROR)
			endif
		endselect
	endselect
	
	return(0)
}						

									|#ISGECDV001094.en

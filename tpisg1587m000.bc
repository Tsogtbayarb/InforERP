|******************************************************************************
|* tpisg1587m000  0  VRC B61U a7 isg 
|* Monthwise Projectwise Actual Billing
|* Ravi Kumar                    
|* 2020-01-03
|******************************************************************************
|* Main table tpisg187 Contractwise Billing Lines, Form Type 1
|******************************************************************************
                                                                                
|****************************** declaration section ***************************
declaration:

  table   ttpisg187 | Contractwise Billing Lines
  table   ttppdm740
  table   ttcemm030
  table   ttcmcs052

	extern	domain	tcamnt		vari, budg.tot, actu.tot, vari.tot, budg
	extern	domain	tcdsca		cprj.dsca, eunt.dsca
	extern	domain	tcmcs.str215m	reason
	
	extern	domain	tcamnt		budg.bill.amnt,bill.out.amnt,vari.amnt,proj.val,act.bill.prev,bal.bill
	extern	domain	tcmcs.str215m	reason.of.var
	extern	domain	tcdsca		division.desc,proj.desc
		
			long		brp.id
|****************************** program section ********************************
before.program:
	 fattr.total.line = true
	 
on.display.total.line:
	cal.total()
	display.total.fields( "budg", budg.tot, 
				"tpisg187.actu", actu.tot,
				"vari", vari.tot)
				
|****************************** group section **********************************
field.budg:
before.display:
	budg = budget.amount()
	
field.vari:
before.display:
	budg = budget.amount()
| 	vari = budg - tpisg187.actu
	vari = tpisg187.actu - budg
	
field.cprj.dsca:
before.display:
	select	tcmcs052.dsca:cprj.dsca
	from	tcmcs052
	where	tcmcs052._index1 = {:tpisg187.cprj}
	as set with 1 rows
	selectdo
	selectempty
		cprj.dsca = ""
	endselect
	
field.eunt.dsca:
before.display:
	select	tcemm030.dsca:eunt.dsca
	from	tcemm030
	where	tcemm030._index1 = {:tpisg187.eunt}
	as set with 1 rows
	selectdo
	selectempty
		eunt.dsca = ""
	endselect
	
field.reason:
before.display:
	select	tpisg086.rmks:reason
	from	tpisg086
	where	tpisg086._index2 = {:tpisg187.year, :tpisg187.mnth, :tpisg187.eunt, :tpisg187.cprj}
	as set with 1 rows
	selectdo
	selectempty
		reason = ""
	endselect

choice.refresh:
on.choice:
	tpisgdll0186_update_create_actual_tpisg187(tpisg187.year,tpisg187.mnth,tpisg187.eunt)
	message("Data updated successfully...")
	
functions:
function cal.total()
{
	select	sum(tpisg086.budg):budg.tot
	from	tpisg086
	where	tpisg086._index2 = {:tpisg187.year, :tpisg187.mnth, :tpisg187.eunt}
	selectdo
	selectempty
		budg.tot = 0
	endselect
	
	select	sum(tpisg187.actu):actu.tot
	from	tpisg187
	where	tpisg187._index1 = {:tpisg187.year,:tpisg187.mnth,:tpisg187.eunt}
	selectdo
	selectempty
		actu.tot = 0
	endselect
	
| 	vari.tot = budg.tot - actu.tot
	vari.tot = actu.tot - budg.tot
}

function domain	tcamnt	budget.amount()
{
	domain	tcamnt	budg.amnt 
	
	select	tpisg086.budg:budg.amnt
	from	tpisg086
	where	tpisg086._index2 = {:tpisg187.year, :tpisg187.mnth, :tpisg187.eunt, :tpisg187.cprj}
	as set with 1 rows
	selectdo
	selectempty
		budg.amnt = 0
	endselect
	
	return(budg.amnt)
}

function extern print.report()
{
	brp.id = brp.open("rtpisg158701001","D",1)
	
	division.desc = get.division.desc(tpisg187.eunt)
	
	select	tpisg187.*
	from	tpisg187
	where	tpisg187._index1 = {:tpisg187.year,:tpisg187.mnth,:tpisg187.eunt}
	selectdo
		get.budgeted.billing.and.var.reason(	tpisg187.year,
							tpisg187.mnth,
							tpisg187.eunt,
							tpisg187.cprj,
							budg.bill.amnt,
							bill.out.amnt,
							reason.of.var	)
		vari.amnt = tpisg187.actu - budg.bill.amnt
		proj.val = get.project.value(tpisg187.cprj)
		proj.val = proj.val/100000
		act.bill.prev = get.act.bill.till.prev.mnth(	tpisg187.year,
								tpisg187.mnth,
								tpisg187.eunt,
								tpisg187.cprj	)
		bal.bill = proj.val - act.bill.prev - tpisg187.actu
		proj.desc = get.project.desc(tpisg187.cprj)
	
		brp.ready(brp.id)
	endselect
	
	brp.close(brp.id)
}

function get.budgeted.billing.and.var.reason(		domain	tfgld.year	i.year,
							domain	tpisg.month	i.mnth,
							domain	tcemm.grid	i.eunt,
							domain	tccprj		i.cprj,
						ref	domain	tcamnt		o.budg,
						ref	domain	tcamnt		o.outl,
						ref	domain	tcmcs.str215m	o.rmks	)
{
	select	tpisg086.budg:o.budg,
		tpisg086.outl:o.outl,
		tpisg086.rmks:o.rmks
	from	tpisg086
	where	tpisg086._index2 = {:i.year,:i.mnth,:i.eunt,:i.cprj}
	as set with 1 rows
	selectdo
	selectempty
		o.budg = 0.0
		o.outl = 0.0
		o.rmks = ""
	endselect
}

function domain tcamnt get.project.value(domain	tccprj i.cprj)
{
	select	tppdm740.copr,
		tppdm740.rate
	from	tppdm740
	where	tppdm740._index1 = {:i.cprj}
	as set with 1 rows
	selectdo
	selectempty
		tppdm740.copr = 0.0
		tppdm740.rate(1) = 0.0
	endselect
	
	return(tppdm740.copr * tppdm740.rate(1))
}

function domain	tcamnt get.act.bill.till.prev.mnth(	domain	tfgld.year	i.year,
							domain	tpisg.month	i.mnth,
							domain	tcemm.grid	i.eunt,
							domain	tccprj		i.cprj	) 
{
	domain	tcamnt	actu.amnt.1
	domain	tcamnt	actu.amnt.2
	
	actu.amnt.1 = 0.0
	actu.amnt.2 = 0.0
	
	select	sum(tpisg187.actu):actu.amnt.1
	from	tpisg187
	where	tpisg187._index1 = {:i.year}
	and	tpisg187.mnth < :i.mnth	
	and	tpisg187.eunt = :i.eunt	
	and	tpisg187.cprj = :i.cprj	
	selectdo
	selectempty
		actu.amnt.1 = 0.0
	endselect
	
	select	sum(tpisg187.actu):actu.amnt.2
	from	tpisg187
	where	tpisg187._index1 < {:i.year - 1}
	and	tpisg187.eunt = :i.eunt	
	and	tpisg187.cprj = :i.cprj	
	selectdo
	selectempty
		actu.amnt.2 = 0.0
	endselect
	
	return(actu.amnt.1 + actu.amnt.2)
}

function domain tcdsca get.division.desc(domain	tcemm.grid i.eunt)
{
	select	tcemm030.dsca
	from	tcemm030
	where	tcemm030._index1 = {:i.eunt}
	as set with 1 rows
	selectdo
	selectempty
		tcemm030.dsca = ""
	endselect
	
	return(tcemm030.dsca)
}

function domain tcdsca get.project.desc(domain tccprj i.cprj)
{
	select	tcmcs052.dsca
	from	tcmcs052
	where	tcmcs052._index1 = {:i.cprj}
	as set with 1 rows
	selectdo
	selectempty
		tcmcs052.dsca = ""
	endselect
	
	return(tcmcs052.dsca)
}

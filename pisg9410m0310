|******************************************************************************
|* tiisg9410m031  0  VRC B61U a7 live
|* Print Target Hours v/s Pending Hours Project
|* Installation user             
|* 2014-12-17
|******************************************************************************
|* Main table tiisg910 Hours Transaction, Form Type 4
|******************************************************************************
                                                                                
|****************************** declaration section ***************************
declaration:

	table	ttiisg910 | Hours Transaction
	table	ttiisg911
	table	ttiisg924
	table	ttcmcs052

	|*************** Form Variables ********************
	extern	domain	tccprj		cprj.f	fixed
	extern	domain	tccprj		cprj.t	fixed
	extern	domain	tcmcs.str10	grcd.f	fixed
	extern	domain	tcmcs.str10	grcd.t	fixed
	extern	domain	tfgld.date	tdat.f
	extern	domain	tfgld.date	tdat.t
	
	|*************** Report Variables ********************
	extern	domain	tccprj		rep.cprj
	extern	domain	tcdsca		rep.pjdc
	extern	domain	tcmcs.str10	rep.grcd
	extern	domain	tcdsca		rep.gdsc	
	extern	domain	tfacp.hour	rep.thrs	
	extern	domain	tfacp.hour	rep.chrs	
	extern	domain	tfacp.hour	rep.bhrs	

|****************************** program section ********************************


|****************************** group section **********************************
group.1:
init.group:
	get.screen.defaults()

|****************************** choice section ********************************
choice.print.data:
on.choice:
	if	rprt_open() then
		read.main.table()
		rprt_close()
	else
		choice.again()
	endif

|****************************** field section *********************************
field.cprj.f:
when.field.changes:
	cprj.t = cprj.f

field.grcd.f:
when.field.changes:
	grcd.t = grcd.f

field.tdat.f:
when.field.changes:
	tdat.t = tdat.f

|****************************** function section ******************************

functions:

function read.main.table()
{
	select	tiisg924.*, tcmcs052.dsca, tiisg911.desc
	from	tiisg924, tcmcs052, tiisg911
	where	tiisg924._index1 inrange {:cprj.f, :grcd.f}	and	{:cprj.t, :grcd.t}
	and	tiisg924.cprj refers to tcmcs052 Unref Clear
	and	tiisg924.grcd refers to tiisg911 Unref Clear
	selectdo
		Initialize_Variables()
		rep.cprj = tiisg924.cprj
		rep.pjdc = tcmcs052.dsca
		rep.grcd = tiisg924.grcd
		rep.gdsc = tiisg911.desc
		rep.thrs = tiisg924.thrs
		select	sum(tiisg910.hhrs):rep.chrs
		from	tiisg910
		where	tiisg910._index10 = {:tiisg924.grcd, :tiisg924.cprj}
		and	tiisg910._index11 inrange {:tdat.f}	and	{:tdat.t}
		selectdo
		endselect
		rep.bhrs = rep.thrs - rep.chrs
		rprt_send()
	endselect	
}

function Initialize_Variables()
{
	rep.cprj = ""
	rep.pjdc = ""
	rep.grcd = ""
	rep.gdsc = ""
	rep.thrs = 0
	rep.chrs = 0
	rep.bhrs = 0
}

|******************************************************************************
|* tdisg0402m000  0  VRC B61U a7 live
|* PO wise Documents attached to Child Item
|* Installation user             
|* 2014-12-22
|******************************************************************************
|* Main table tdisg002 Bill of Material, Form Type 4
|******************************************************************************
                                                                                
|****************************** declaration section ***************************
declaration:

	table	ttdisg002 | Bill of Material

	|******************* Form Variables *****************
	extern	domain	tcorno		orno.f	fixed
	extern	domain	tcorno		orno.t	fixed
	
	|******************* Report Variables ***************
	extern	domain	tcorno		rep.orno
	extern	domain	dmdoc.docid	rep.ldcn
	extern	domain	dmdoc.revv	rep.lrev
	extern	domain	dmdoc.docid	rep.mdcn
	extern	domain	dmdoc.revv	rep.mrev
	extern	domain	tcitem		rep.item
	extern	domain	tcdsca		rep.idsc
	extern	domain	tcqiv1		rep.qnty
	extern	domain	tcqiv1		rep.wght

|****************************** program section ********************************


|****************************** group section **********************************
group.1:
init.group:
	get.screen.defaults()

|****************************** choice section ********************************
choice.print.data:
on.choice:
	if	rprt_open() then
		read.main.table()
		rprt_close()
	else
		choice.again()
	endif


|****************************** field section *********************************
field.orno.f:
when.field.changes:
	orno.t = orno.f

|****************************** function section ******************************

functions:

function read.main.table()
{
	select	tdisg002.*
	from	tdisg002
	where	tdisg002._index1 inrange {:orno.f}	and	{:orno.t}
	order by tdisg002._index1
	selectdo
		Initialize_Variables()
		rep.orno = tdisg002.orno
		rep.mdcn = tdisg002.docn
		rep.mrev = tdisg002.revi
		rep.item = tdisg002.item
		rep.idsc = tdisg002.desc
		rep.qnty = tdisg002.qnty
		rep.wght = tdisg002.wght
		Get_Latest_Document(tdisg002.docn, tdisg002.revi, rep.ldcn, rep.lrev)
		rprt_send()
	endselect
}

function Get_Latest_Document(
			domain	dmdoc.docid	i.docn,
			domain	dmdoc.revv	i.revi,
		ref	domain	dmdoc.docid	o.ldcn,
		ref	domain	dmdoc.revv	o.lrev
			)
{
	select	dmisg001.docn:o.ldcn,
		dmisg001.revn:o.lrev
	from	dmisg001
	where	dmisg001._index1 = {:i.docn}
	and	dmisg001.revn >= :i.revi
	and	dmisg001.stat <> dmstat.expire
	order by dmisg001.revn desc
	as set with 1 rows
	selectdo
	endselect
}

function Initialize_Variables()
{
	rep.orno = ""
	rep.ldcn = ""
	rep.lrev = ""
	rep.mdcn = ""
	rep.mrev = ""
	rep.item = ""
	rep.idsc = ""
	rep.qnty = 0
	rep.wght = 0
}

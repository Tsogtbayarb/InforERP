|******************************************************************************
|* tdisg4401m200  0  VRC B61U a7 isg 
|* Print PO Delivery Status [Detailed]
|* Installation user             
|* 2014-09-02
|******************************************************************************
|* Main table tdpur401 Purchase Order Lines, Form Type 4
|******************************************************************************
|* ISGECDV001022, IT0205, Arjit Gupta, Dt. 16/09/2014 VRC B61U a7 live 
|* If record exist in whinh936 then no need to pick from tdpur401 for WH Receipt 
|*
|* ISGECDV001027, IT0205, Arjit Gupta, Dt. 17/09/2014 VRC B61U a7 live
|* Add two columns Supplier Code and Supplier Name
|*
|* ISGECDV001031, IT0205, Arjit Gupta, Dt. 19-09-2014, VRC B61U a7 live
|* Calculate Excise on tdpur401.tase.l and Service on tdpur401.tass.l
|*
|* ISGECDV001032, IT0205, Arjit Gupta, Dt. 20-09-2014, VRC B61U a7 live
|* Calculate Excise on tdpur401.tase.l and Service on tdpur401.tass.l on the basis 
|* of receipt quantity.
|*
|* ISGEC001069, IT0205, Arjit Gupta, Dt. 06-10-2014, VRC B61U a7 live
|* Add a Column of Delivery Date, buyer name and correct calculation of tax if
|* Purchase receipt and Warehouse receipt have same Number
|*
|* ISGEC001087, IT0205, Arjit Gupta, Dt. 10-10-2014, VRC B61U a7 isg
|* Add a column PO Status
|*
|* ISGEC001125, IT0205, Arjit Gupta, Dt. 27-10-2014, VRC B61U a7 isg
|* Add a column Balance Receipt Value
|* 
|* ISGECDV001176, IT0205, Arjit Gupta, Dt. 24-11-2014, VRC B61U a7 live
|* Add a column GR Date Transporter Name
|*
|* ISGECDV001192, IT0205, Arjit Gupta, Dt. 01-12-2014, VRC B61U a7 live
|* Add a column Balance Basic Amount.
|* 
|* ISGEC001175, IT0205, Arjit Gupta, Dt. 04-12-2014, VRC B61U a7 live
|* Add a column Balance Basic Amount Correction
|*
|* ISGEC01072, IT0375, Sandhyarani, Dt. 03-03-2015, VRC B61U a7 live
|* Modification on Order date
|
|* ISGEC015087, IT0302, Ankit Sharma, Dt. 23-02-2016, VRC B61U a7 live
|* Added currency column in field
|*
|* ISGECGST002, IT0386, Saurabh Dubey, 27 May 2017, VRC B61 a7 isg
|* GST Related Modifications for Report, Functional : Mrs. Veena(Isgec)
|* Isgec Onsite Deployment
|*
|* GH431CR774, RAvi Kumar, 02-11-2020
|* Print Warehouse codes in Po Delivery Reports
|* 
|* GH495CR000, RAvi Kumar, 17-02-2021
|* In Project EC0034, PO P83006079 and P83006081 showing incorrect value in Linewise Po delivery status report
|* 
|* GH557CR888, RAvi Kumar, 21-05-2021
|* CR888-To modify the range of filter named Delivery Date to current date+3 years
|*
|* GH588CR910, RAvi Kumar, 05-07-2021
|* Status of Work flow to be added in Delivery status reports
|****************************** declaration section ***************************
declaration:

	table	ttdpur401 | Purchase Order Lines
	table	ttctax941
	table	ttcmcs036
	table	ttclct200
	table	ttdpur400
	table	ttcibd001
	table	ttcmcs052
	table	ttdpur406
	table	ttcmcs032
	table	ttfacp935
	table	twhisg312
	table	twhinh310
	table	twhinh312
	table	twhinh936
	table	ttfisg001
	table	ttfisg002
	table	ttfacp100
	table	ttccom100				|#ISGECDV001027.n
	table	ttccom001			|#ISGEC001069.n
	table	ttfisg003			|#ISGECDV001176.n
	table	ttdmsl400			|#ISGEC01072.n
	table	ttdmsl503	|#ISGECGST002.n

	|*************** Form Variables *******************
	extern	domain	tcorno		orno.f    fixed
	extern	domain	tcorno		orno.t    fixed
	extern	domain	tcorno		rcno.f
	extern	domain	tcorno		rcno.t
	extern	domain	tccom.bpid	bpid.f
	extern	domain	tccom.bpid	bpid.t
	extern	domain	tcdate		odat.f
	extern	domain	tcdate		odat.t
	extern	domain	tcdate		ddta.f
	extern	domain	tcdate		ddta.t
	extern	domain	tcdate		rcdt.f
	extern	domain	tcdate		rcdt.t
	extern	domain	tccprj		cprj.f
	extern	domain	tccprj		cprj.t
	extern	domain	tppdm.cspa	cspa.f
	extern	domain	tppdm.cspa	cspa.t
	extern	domain	tcemno		emno.f
	extern	domain	tcemno		emno.t
	|*************** File Handling Variables *******************
	string		str(300), err.file(100), file.name(100), line3(2000), line4(2000)
	long		ret2, stat.fp
	|************** Script Variables ******************
		domain	tcdate		v.odat.f
		domain	tcdate		v.odat.t
		domain	tcdate		v.ddta.f
		domain	tcdate		v.ddta.t
		domain	tcdate		v.rcdt.f
		domain	tcdate		v.rcdt.t
		domain	tcbool		receipt.found
		domain	tcbool		fwhinh936.found		|#ISGECDV001022.n
		domain	tcamnt		tmp.oval		|#ISGEC001125.n
		domain	tcamnt		tmp.obsc		|#ISGEC001175.n
	|************** File Utilization Variables *************************
	extern	domain	tcorno		rep.orno	
	extern	domain	tcpono		rep.pono	
	extern	domain	tcdate		rep.odat	
	extern	domain	tcitem		rep.item	
	extern	domain	tcdsca		rep.itdsc	
	extern	domain	tccprj		rep.cprj	
	extern	domain	tcdsca		rep.pjdsc	
	extern	domain	tppdm.cspa	rep.cspa	
	extern	domain	tcdsca		rep.eldsc	
	extern	domain	tcqiv1		rep.oqnt	
	extern	domain	tcpric		rep.oprc	
	extern	domain	tcamnt		rep.obsc	
	extern	domain	tcamnt		rep.oexc	
	extern	domain	tcamnt		rep.ocst	
	extern	domain	tcamnt		rep.ostx	
	extern	domain	tcamnt		rep.lcst	
	extern	domain	tcamnt		rep.ogrs
	extern	domain	tcorno		rep.rcno	
	extern	domain	tcpono		rep.rcln	
	extern	domain	tcdate		rep.rcdt
	extern	domain	tfacp.isup	rep.isup
	extern	domain	tcmcs.str215m	rep.grno
	extern	domain	tcmcs.str215m	rep.grdt			|#ISGECDV001176.n	
	extern	domain	tcmcs.str256m	rep.trnm			|#ISGECDV001176.n
	extern	domain	tcqiv1		rep.rqnt	
	extern	domain	tcpric		rep.rprc	
	extern	domain	tcamnt		rep.rbsc	
	extern	domain	tcamnt		rep.rexc	
	extern	domain	tcamnt		rep.rcst	
	extern	domain	tcamnt		rep.rstx	
	extern	domain	tcamnt		rep.roch	
	extern	domain	tcamnt		rep.rgrs	
	extern	domain	tcmcs.str20	rep.rstt	
	extern	domain	tcorno		rep.blad	
	extern	domain	tcamnt		rep.bval
	extern	domain	tccom.bpid	rep.otbp		|#ISGECDV001027.n
	extern	domain	tcnama		rep.nama		|#ISGECDV001027.n
	extern	domain	tcemno		rep.buyr		|#ISGEC001069.n
	extern	domain	tcnama		rep.bynm		|#ISGEC001069.n
	extern	domain	tcdate		rep.ddta		|#ISGEC001069.n
	extern	domain	tcmcs.str15	rep.hdst		|#ISGEC001087.n
	extern	domain	tcamnt		rep.rbal		|#ISGEC001125.n
	extern	domain	tcamnt		rep.bbal		|#ISGECDV001192.n
	extern	domain	tcccur		rep.ccur		|#ISGEC015087.n
	extern	domain	tchsnc.l	rep.hcod	|#ISGECGST002.sn	
	extern	domain	tcamnt		rep.sgst		
	extern	domain	tcamnt		rep.cgst		
	extern	domain	tcamnt		rep.igst		
	extern	domain	tcamnt		rep.rsgst			
	extern	domain	tcamnt		rep.rcgst			
	extern	domain	tcamnt		rep.rigst	|#ISGECGST002.en	
	extern	domain	tccwar		rep.cwar	|GH431CR774.a		

|*************** Include **************
#include<bic_desktop>
|****************************** program section ********************************
before.program:
	utc.add(utc.num(), 3, 0, 0 ,0 ,0 ,0 , ddta.t)					|GH557CR888.a

|****************************** group section **********************************

group.1:
init.group:
   get.screen.defaults()

|****************************** choice section ********************************
choice.cont.process:
on.choice:
	generate.file()

|****************************** field section *********************************
field.orno.f:
when.field.changes:
	orno.t = orno.f
before.zoom:
	str = "tdpur400.orno in ("
	str = str & "select tdpur401.orno from tdpur401 where tdpur401._index5 inrange '" &cprj.f&"' and '"&cprj.t&"')"
	query.extend.where.in.zoom(str)
	
field.orno.t:
before.zoom:
	str = "tdpur400.orno in ("
	str = str & "select tdpur401.orno from tdpur401 where tdpur401._index5 inrange '" &cprj.f&"' and '"&cprj.t&"')"
	query.extend.where.in.zoom(str)

field.odat.f:
when.field.changes:
	odat.t = odat.f
	
field.rcdt.f:
when.field.changes:
	rcdt.t = rcdt.f	

field.emno.f:
when.field.changes:
	emno.t = emno.f

field.cprj.f:
when.field.changes:
	cprj.t = cprj.f

field.cspa.f:
when.field.changes:
	cspa.t = cspa.f

field.ddta.f:
when.field.changes:
	ddta.t = ddta.f

field.rcno.f:
when.field.changes:
	rcno.t = rcno.f

field.bpid.f:
when.field.changes:
	bpid.t = bpid.f

|****************************** function section ******************************
functions:

function generate.file()
{
	open.file()
	generate.header()
	read.main.table()
	close.file()
}

function read.main.table()
{
	rcdt.f = 0
	rcdt.t = utc.num()
	set.fmin(rcno.f)
	set.fmax(rcno.t)
	get.date.utc(odat.f, 00, 00, 00, v.odat.f)
	get.date.utc(odat.t, 23, 59, 59, v.odat.t)
	get.date.utc(ddta.f, 00, 00, 00, v.ddta.f)
	get.date.utc(ddta.t, 23, 59, 59, v.ddta.t)
	get.date.utc(rcdt.f, 00, 00, 00, v.rcdt.f)
	get.date.utc(rcdt.t, 23, 59, 59, v.rcdt.t)
	
	select	tdpur401.*, tdpur400.orno, tdpur400.odat, tdpur400.otbp,	|#ISGECDV001027.n
		tdpur400.hdst, tdpur400.ccon, tcibd001.dsca, tcmcs052.dsca,					|#ISGECDV001027.n
		tccom100.nama, tccom001.nama,								|#ISGEC001069.n
		tdpur400.ccur								|#ISGEC015087.n
	from	tdpur401, tdpur400, tcibd001, tcmcs052, tccom100, tccom001		|#ISGEC001069.n
	where	tdpur401._index1 inrange {:orno.f}	and	{:orno.t}
	and	tdpur401._index5 inrange {:cprj.f}	and	{:cprj.t}
	and	tdpur401.cspa inrange :cspa.f		and	:cspa.t
	and	tdpur401.ddta inrange :v.ddta.f		and	:v.ddta.t
	and	tdpur401.orno refers to tdpur400
	and	tdpur400.odat inrange :v.odat.f		and	:v.odat.t	
	and	tdpur400._index2 inrange {:bpid.f}	and	{:bpid.t}	
	and	tdpur400._index4 inrange {:emno.f}	and	{:emno.t}
	and	tdpur401.oltp in (tdgen.oltp.total, tdgen.oltp.orderline)
	and	tdpur401.item refers to tcibd001
	and	tdpur401.cprj refers to tcmcs052
	and	tdpur400.otbp refers to tccom100
	and	tdpur400.ccon refers to tccom001 Unref Clear				|#ISGEC001069.n
	selectdo
		Initialize_Variable()
		get.po.date()
		get.po.workstatus()				|GH588CR910.a
		rep.orno = tdpur401.orno	
		rep.pono = tdpur401.pono	
		rep.cwar = tdpur401.cwar					|GH431CR774.a	
		rep.item = tdpur401.item	
		rep.itdsc = tcibd001.dsca	
		rep.cprj = tdpur401.cprj	
		rep.pjdsc = tcmcs052.dsca
		rep.cspa = tdpur401.cspa
| 		rep.odat = tdpur400.odat		|#ISGEC01072.o
		rep.otbp = tdpur400.otbp		|#ISGECDV001027.n
		rep.nama = tccom100.nama		|#ISGECDV001027.n
		rep.buyr = tdpur400.ccon		|#ISGEC001069.n	
		rep.bynm = tccom001.nama		|#ISGEC001069.n	
		rep.ddta = tdpur401.ddta		|#ISGEC001069.n	
		rep.ccur = tdpur400.ccur		|#ISGEC015087.n	
		receipt.found = false
		rep.hdst = enum.descr$("tdpur.hdst", tdpur400.hdst)		|#ISGEC001087.n
		
		select	e_tpptc100.desc:rep.eldsc
		from	tpptc100 e_tpptc100
		where	e_tpptc100._index1 = {:tdpur401.cprj, :tdpur401.cspa}
		selectdo
		selectempty
			rep.eldsc = ""
		endselect
					|#ISGECGST002.sn
		select	tdmsl503.code:rep.hcod
		from	tdmsl503
		where	tdmsl503._index1 = {:tdpur401.orno, :tdpur401.pono}
		selectdo
		selectempty
			rep.hcod = ""
		endselect
					|#ISGECGST002.en
			
| 		rep.oqnt = tdpur401.qoor + get.additional.qty.from.401(tdpur401.orno, tdpur401.pono)	|GH495CR000.old
		rep.oqnt = get.additional.qty.from.401(tdpur401.orno, tdpur401.pono)			|GH495CR000.new
		if	tdpur401.qoor <> 0	then
			rep.oprc = (tdpur401.oamt / tdpur401.qoor)
		else
			rep.oprc = 0
		endif	
		rep.obsc = rep.oqnt * rep.oprc
		tdpur401.oamt = rep.obsc	|#ISGECGST002.n, for multiple sequences
		tdpur401.tasg.l = rep.oqnt * tdpur401.asvg.l	|#ISGECGST002.n, for multiple sequences
		populate.tax.amounts()
		Get_Landed_Cost(tdpur401.orno, tdpur401.pono)
		rep.ogrs = rep.obsc + rep.oexc + rep.ocst + rep.ostx + rep.lcst + 
				rep.cgst + rep.sgst + rep.igst |ISGECGST002.n
		tmp.oval = 0				|#ISGEC001125.n
		tmp.oval = rep.ogrs			|#ISGEC001125.n
		tmp.obsc = 0				|#ISGEC001175.n
		tmp.obsc = rep.obsc			|#ISGEC001175.n
		if	not Get_Warehouse_Receipt(tdpur401.orno, tdpur401.pono, rcno.f, rcno.t,
						v.rcdt.f, v.rcdt.t, tdpur401.qoor, tdpur401.pric, tdpur401.ccty,
| 						tdpur401.cvat, tdpur401.tasv.l)	then		|#ISGECDV001031.o
						tdpur401.cvat, tdpur401.tasv.l, tdpur401.tase.l, tdpur401.tass.l,		|#ISGECDV001031.n
						tmp.obsc,				|#ISGEC001175.n 
						tmp.oval)	then		|#ISGEC001125.n
		endif				
		if	not Get_Purchase_Receipt(tdpur401.orno, tdpur401.pono, rcno.f, rcno.t,
					v.rcdt.f, v.rcdt.t, tdpur401.qoor, tdpur401.pric, tdpur401.ccty,
| 					tdpur401.cvat, tdpur401.tasv.l)	then		|#ISGECDV001031.o
					tdpur401.cvat, tdpur401.tasv.l, tdpur401.tase.l, tdpur401.tass.l,		|#ISGECDV001031.n
					tmp.obsc,			|#ISGEC001175.n
					tmp.oval)	then|#ISGEC001125.n
		endif
		
		if	not receipt.found then
			rep.rbal = rep.ogrs		|#ISGEC001125.n
			rep.bbal = rep.obsc			|#ISGECDV001192.n
			print.detail()
		endif	
	endselect	
}

function domain tcqiv1	 get.additional.qty.from.401(
				domain	tcorno	in.orno,
				domain	tcpono	in.pono
					)
{
| 	domain	tcqiv1 ord.qty, app.qty, diff.qty, bac.qty				|GH495CR000.com.s
| 	ord.qty = 0
| 	app.qty = 0
| 	bac.qty = 0
| 	diff.qty = 0
| 	select	q_tdpur401.qoor:ord.qty,
| 		q_tdpur401.qiap:app.qty,
| 		q_tdpur401.qibo:bac.qty
| 	from	tdpur401 q_tdpur401
| 	where	q_tdpur401._index1 = {:in.orno, :in.pono}
| 	and	q_tdpur401.oltp = tdgen.oltp.total
| 	selectdo
| 		diff.qty = diff.qty + bac.qty - (ord.qty - app.qty)
| 	endselect
| 	return(diff.qty)								|GH495CR000.com.e

	domain tcqsl1 ord.qty,app.qty,diff.qty,bac.qty					|GH495CR000.a.s
	domain tcqsl1 final.rcpt.qnty, non.final.rcpt.qnty	
	domain tcqsl1	final.rcpt,non.final.rcpt			
	ord.qty = 0
	app.qty = 0
	bac.qty = 0
	diff.qty = 0
	non.final.rcpt.qnty = 0	
	final.rcpt.qnty = 0	
	
	select	sum(tdpur401.qidl):final.rcpt.qnty
	from 	tdpur401 
	where 	tdpur401._index1 = {:in.orno, :in.pono}
	and	tdpur401.oltp <> 1		|* Not Total Line Type
	and	tdpur401.clyn <> tcyesno.yes 	|* Not Consider Cancelled PO Line
	selectdo
	selectempty
		final.rcpt.qnty = 0.00
	endselect
		
	select	sum(tdpur401.qidl):non.final.rcpt.qnty,
		sum(tdpur401.qoor):ord.qty
	from 	tdpur401 
	where 	tdpur401._index1 = {:in.orno, :in.pono}
	and	tdpur401.fire = tcyesno.no
	and	tdpur401.oltp <> 1		|* Not Total Line Type
	and	tdpur401.clyn <> tcyesno.yes 	|* Not Consider Cancelled PO Line
	selectdo
	selectempty
		ord.qty = 0.00
		non.final.rcpt.qnty = 0.00
	endselect
	
	diff.qty = final.rcpt.qnty + (ord.qty - non.final.rcpt.qnty)
							
	diff.qty = round(diff.qty,6,2)									
	return(diff.qty)								|GH495CR000.a.e
}

function populate.tax.amounts()
{
	domain	tcamnt		line.tax.amount.array(1, 1)	based
	domain	tcamnt		line.base.amount.array(1, 1)	based
	domain	tccvat		line.tax.code.array(1, 1)	based
	domain	tctax.indt.l	o.line.indirect.tax.array(1, 1)	based
	domain	tctax.indt.l	i.duty.type.array(1, 1)		based
	domain	tcyesno		expensed.tax(1, 1)		based
	domain	tcmcs.s250m	o.comb.output.set(1)		based
	domain	tcpvat		o.tax.rates(1,1)		based
	domain	tcamnt		i.duties.array(1,1)		based
	domain tctax.seqn 	o.maximum.sequence.number
	domain tcamnt 		o.claimable.tax.amount
	domain tcamnt 		o.non.claimable.tax.amount
	domain tcamnt 		o.total.tax.amount
	domain tcamnt 		o.sales.tax
	domain tcmcs.s250m 	o.error.msg mb
	
	
	tctax.dll9141.determine.aggregate.tax.amounts.per.line( 
						tdpur401.ccty,				| domain tcccty i.tax.country, 
						tdpur401.cvat,				| domain tccvat i.aggregate.tax.code, 
						tdpur401.odat,				| domain tcdate i.tax.date, 
						tdpur401.oamt,				| domain tcamnt i.order.line.price, 
						tdpur401.cuva,				| domain tcamnt i.customs.value, 
						tdpur401.mrpi.l,			| domain tcamnt i.market.retail.price, 
						0.00,					| domain tcamnt i.retail.sales.price, 
						0.00,					| domain tcamnt i.tariff.price, 
						tdpur400.ccur,				| domain tcccur i.price.currency, 
						tdpur400.ratd,				| domain tcdate i.currency.rate.date, 
						tdpur400.ratt,				| domain tcrtyp i.currency.exchange.rate.type, 
						tdpur401.tase.l,			| domain tcpric i.asv.excise, 
						tdpur401.tasv.l,			| domain tcpric i.asv.vat, 
						tdpur401.tass.l,			| domain tcpric i.asv.service.tax, 
						tdpur401.tasg.l,			| domain tcpric i.asv.gst,	|#ISGECGST002.n 	
						tdpur401.ddta,				| domain tcdate i.ship.or.receive.date, 
						tcyesno.no,				| domain tcyesno i.as.is.sales, 
						tcyesno.no,				| domain tcyesno i.yesno.flag, 
						i.duty.type.array,			| ref domain tctax.indt.l i.duty.type.array(,), 
						i.duties.array,				| ref domain tcamnt i.duties.array(,), 
						o.tax.rates,				| ref domain tcpvat o.tax.rates(,), 
						o.maximum.sequence.number,		| ref domain tctax.seqn o.maximum.sequence.number, 
						line.base.amount.array,			| ref domain tcamnt o.line.base.amount.array(,), 
						line.tax.amount.array,			| ref domain tcamnt o.line.tax.amount.array(,), 
						line.tax.code.array,			| ref domain tccvat o.line.tax.code.array(,) fixed, 
						o.line.indirect.tax.array,		| ref domain tctax.indt.l o.line.indirect.tax.array(,), 
						expensed.tax,				| ref domain tcyesno o.expensed.tax(,), 
						o.claimable.tax.amount,			| ref domain tcamnt o.claimable.tax.amount, 
						o.non.claimable.tax.amount,		| ref domain tcamnt o.non.claimable.tax.amount, 
						o.total.tax.amount,			| ref domain tcamnt o.total.tax.amount, 
						o.sales.tax,				| ref domain tcamnt o.sales.tax, 
						o.comb.output.set,			| ref domain tcmcs.s250m o.comb.output.set() fixed mb, 
						o.error.msg)				| ref domain tcmcs.s250m o.error.msg mb )
	select	tctax941.*,tcmcs036.*
	from	tctax941,tcmcs036
	where	tctax941._index1 = {:tdpur401.ccty, :tdpur401.cvat}
	and	tctax941.type = 20		|tctax.type.l.tax
	and	tctax941.cmbb refers to tcmcs036
	selectdo
		on case tcmcs036.indt.l
			case	tctax.indt.l.bed:
			case	tctax.indt.l.service:			
			case	tctax.indt.l.aed:	
			case	tctax.indt.l.sed:	
			case	tctax.indt.l.e.cess.excise:	
			case	tctax.indt.l.e.cess.service:			
			case	tctax.indt.l.hse.cess.excise:
			case	tctax.indt.l.hse.cess.servic:		
				rep.oexc = rep.oexc + line.tax.amount.array(tctax941.layr,tctax941.seqn)
				break
			case	tctax.indt.l.lst:	
			case	tctax.indt.l.cst:	
			case	tctax.indt.l.vat:	
			case	tctax.indt.l.n.a:
				rep.ocst = rep.ocst + line.tax.amount.array(tctax941.layr,tctax941.seqn)
				break	
			case tctax.indt.l.sgst:		|#ISGECGST002.sn
			case tctax.indt.l.utgst:		
				rep.sgst = rep.sgst + line.tax.amount.array(tctax941.layr,tctax941.seqn)
				break
			case tctax.indt.l.cgst:
				rep.cgst = rep.cgst + line.tax.amount.array(tctax941.layr,tctax941.seqn)
				break
			case tctax.indt.l.igst:
				rep.igst = rep.igst + line.tax.amount.array(tctax941.layr,tctax941.seqn)
				break			
						|#ISGECGST002.en	
			default:
				rep.ostx = rep.ostx + line.tax.amount.array(tctax941.layr,tctax941.seqn)
		endcase
	endselect
}

function Get_Landed_Cost(domain tcorno		im.orno,
			domain	tcpono		im.pono)
{
	domain	tcpono	tmp.sqnb
	string	tmp.borf(5)
	
	select	a_tdpur401.sqnb:tmp.sqnb 
	from	tdpur401 a_tdpur401
	where	a_tdpur401._index1 = {:im.orno, :im.pono}
	and	a_tdpur401.oltp in (2,3,4)
	selectdo
		tmp.borf = str$(im.pono)&"/"&str$(tmp.sqnb)
		select	tclct200.* from tclct200
		where	tclct200._index1 = {2}
		and	tclct200.bobj = :im.orno
		and	tclct200.borf = :tmp.borf
		order by tclct200._index1
		selectdo
			rep.lcst = rep.lcst + tclct200.lcam
		endselect
	endselect	
}

function boolean Get_Warehouse_Receipt(
			domain	tcorno		in.orno,
			domain	tcpono		in.pono,
			domain	tcorno		in.rcno.f,
			domain	tcorno		in.rcno.t,
			domain	tcdate		in.rcdt.f,
			domain	tcdate		in.rcdt.t,
			domain	tcqiv1		in.qoor,
			domain	tcpric		in.pric,
			domain	tcccty		in.ccty,
			domain	tccvat		in.cvat,
			domain	tcamnt		in.tasv.l,		
			domain	tcamnt		in.tase.l,			|#ISGECDV001031.n
			domain	tcamnt		in.tass.l,			|#ISGECDV001031.n
		ref	domain	tcamnt		in.obsc,			|#ISGEC001175.n	
		ref	domain	tcamnt		in.ogrs				|#ISGEC001125.n
					)
{
	domain	tcpvat	r.eces.per
	domain	tcpvat	r.cess.per
	domain	tcpvat	r.cst.per
	domain	tcpvat	r.ser.per
	domain	tcpvat	r.sces.per
	domain	tcpvat	r.sgst.per	|#ISGECGST002.sn
	domain	tcpvat	r.cgst.per
	domain	tcpvat	r.igst.per	|#ISGECGST002.en
	domain	tcamnt	v.eces
	domain	tcamnt	v.cess
	domain	tcamnt	v.serv
	domain	tcamnt	v.sces
	domain	tcamnt	tot.rcpt		|#ISGEC001125.n
	domain	tcamnt	tot.ogrs		|#ISGEC001125.n
	domain	tcamnt	tot.rbsc		|#ISGEC001175.n
	domain	tcamnt	tot.obsc		|#ISGEC001175.n
	
	tot.rcpt = 0				|#ISGEC001125.n
	tot.ogrs = in.ogrs				|#ISGEC001125.n
	tot.rbsc = 0				|#ISGEC001175.n
	tot.obsc = in.obsc				|#ISGEC001175.n
	select	whinh312.*, whinh310.stat, whinh310.dino
	from	whinh312, whinh310
	where	whinh312._index1 inrange {:in.rcno.f}	and	{:in.rcno.t}
	and	whinh312._index4 = {whinh.oorg.purchase, :in.orno, :in.pono}
	and	whinh312.ardt inrange :in.rcdt.f	and	:in.rcdt.t
	and	whinh312.rcno refers to whinh310
	selectdo
		rep.rcno = whinh312.rcno
		rep.rcln = whinh312.rcln
		rep.rcdt = whinh312.ardt
		rep.rqnt = whinh312.qrcr
		rep.rbsc = in.pric * whinh312.qrcr
		rep.rprc = in.pric
		select	whisg312.inrq
		from	whisg312
		where	whisg312._index1 = {:whinh312.rcno, :whinh312.rcln}
		selectdo
			rep.blad = whisg312.inrq
			get.billing.value(whisg312.inrq, whinh312.rcno, whinh312.rcln, rep.bval)
		selectempty
			rep.blad = ""
			rep.bval = 0
		endselect
			
		r.eces.per = 0
		r.cess.per = 0
		r.cst.per = 0
		r.ser.per = 0
		r.sces.per = 0
		r.sgst.per = 0.00	|#ISGECGST002.sn
		r.cgst.per = 0.00
		r.igst.per = 0.00	|#ISGECGST002.en
		v.eces = 0
		v.cess = 0
		v.serv = 0
		v.sces = 0
		fwhinh936.found = false		|#ISGECDV001022.n
| 		Get_GR_Supp_Invn(whinh310.dino, rep.isup, rep.grno)		|#ISGECDV001176.o
		Get_GR_Supp_Invn(whinh310.dino, rep.isup, rep.grno, rep.grdt, rep.trnm)		|#ISGECDV001176.n
		Calculate_Other_Charge(whinh312.rcno, whinh312.rcln, rep.roch)
		
		on case whinh310.stat
			case whinh.rhst.confirmed:
				rep.rstt = "Confirmed"
| 				get.tax.from.tfacp935(whinh312.rcno, whinh312.rcln, rep.rexc, rep.rcst, rep.rstx)	|#ISGEC001069.o
														|#ISGEC001069.sn
				get.tax.from.tfacp935(whinh312.rcno, whinh312.rcln, whinh312.orno, whinh312.pono,
								whinh312.seqn, rep.rexc, rep.rcst, rep.rstx,
								rep.rsgst, rep.rcgst, rep.rigst)	|#ISGECGST002.n
														|#ISGEC001069.en
				break
			case whinh.rhst.open:
				fwhinh936.found = false		|#ISGECDV001022.n
				rep.rstt = "Open"
				get.tax.from.whinh936(whinh312.rcno, whinh312.rcln, rep.rexc, rep.rcst, rep.rstx,
							rep.rsgst, rep.rcgst, rep.rigst)	|#ISGECGST002.n
| 				if	rep.rcst = 0	and	rep.rexc = 0	and	rep.rstx = 0	then		|#ISGECDV001022.o
				if	fwhinh936.found = false		then		|#ISGECDV001022.n
					get.tax.rate(in.ccty, in.cvat, whinh312.ardt, r.eces.per, r.cess.per, r.cst.per, r.ser.per, r.sces.per,
					r.sgst.per, r.cgst.per, r.igst.per)	|#ISGECGST002.n
					|Excise
| 					v.eces =  ((rep.rbsc * r.eces.per) / 100 )		|#ISGECDV001031.o
												|#ISGECDV001032.sn
					if	in.qoor <> 0	then
						in.tase.l = (in.tase.l/in.qoor)* whinh312.qrcr
						in.tass.l = (in.tass.l/in.qoor)* whinh312.qrcr
						tdpur401.tasg.l = (tdpur401.tasg.l/in.qoor)* tdpur406.qiap
					else
						in.tase.l = 0
						in.tass.l = 0
						tdpur401.tasg.l = 0.00
					endif
												|#ISGECDV001032.en
					v.eces =  ((in.tase.l * r.eces.per) / 100 )		|#ISGECDV001031.n	
					v.cess = ((v.eces * r.cess.per) / 100) 
					rep.rexc = rep.rexc + (v.eces + v.cess)
					|Service
| 					v.serv = ((rep.rbsc * r.ser.per) / 100)			|#ISGECDV001031.o
					v.serv = ((in.tass.l * r.ser.per) / 100)		|#ISGECDV001031.n
					v.sces = ((v.serv * r.sces.per) / 100)
					rep.rstx = rep.rstx + (v.serv + v.sces)
					|CST / VAT				
					if	in.qoor <> 0	then
						in.tasv.l = (in.tasv.l/in.qoor)* whinh312.qrcr
					else
						in.tasv.l = 0
					endif	
					rep.rcst = ((in.tasv.l + rep.rexc) * r.cst.per) / 100
					rep.rsgst = (rep.rsgst * r.sgst.per) / 100	|#ISGECGST002.sn
					rep.rcgst = (rep.rcgst * r.cgst.per) / 100
					rep.rigst = (rep.rigst * r.igst.per) / 100	|#ISGECGST002.en
				endif	
				break	
			default:
				rep.rstt = "Open"
				get.tax.rate(in.ccty, in.cvat, whinh312.ardt, r.eces.per, r.cess.per, r.cst.per, r.ser.per, r.sces.per,
						r.sgst.per, r.cgst.per, r.igst.per)	|#ISGECGST002.n
				|Excise
| 				v.eces =  ((rep.rbsc * r.eces.per) / 100 )		|#ISGECDV001031.o
												|#ISGECDV001032.sn
				if	in.qoor <> 0	then
					in.tase.l = (in.tase.l/in.qoor)* whinh312.qrcr
					in.tass.l = (in.tass.l/in.qoor)* whinh312.qrcr
					tdpur401.tasg.l = (tdpur401.tasg.l/in.qoor)* tdpur406.qiap
				else
					in.tase.l = 0
					in.tass.l = 0
					tdpur401.tasg.l = 0.00
				endif
												|#ISGECDV001032.en
				v.eces =  ((in.tase.l * r.eces.per) / 100 )		|#ISGECDV001031.n
				v.cess = ((v.eces * r.cess.per) / 100) 
				rep.rexc = rep.rexc + (v.eces + v.cess)
				|Service
| 				v.serv = ((rep.rbsc * r.ser.per) / 100)			|#ISGECDV001031.o
				v.serv = ((in.tass.l * r.ser.per) / 100)		|#ISGECDV001031.n
				v.sces = ((v.serv * r.sces.per) / 100)
				rep.rstx = rep.rstx + (v.serv + v.sces)
				|CST / VAT				
				if	in.qoor <> 0	then
					in.tasv.l = (in.tasv.l/in.qoor)* whinh312.qrcr
				else
					in.tasv.l = 0
				endif	
				rep.rcst = ((in.tasv.l + rep.rexc) * r.cst.per) / 100
				rep.rsgst = (rep.rsgst * r.sgst.per) / 100	|#ISGECGST002.sn
				rep.rcgst = (rep.rcgst * r.cgst.per) / 100
				rep.rigst = (rep.rigst * r.igst.per) / 100	|#ISGECGST002.en
		ENDCASE	
		rep.rgrs = (rep.rbsc + rep.roch + rep.rexc + rep.rcst + rep.rstx + rep.rcgst + rep.rsgst + rep.rigst)
		
							|#ISGEC001125.sn
		if	rep.rgrs <> 0	then
			tot.rcpt = tot.rcpt + rep.rgrs
		endif
		rep.rbal = tot.ogrs -  tot.rcpt	
		in.ogrs = rep.rbal
							|#ISGEC001125.en
							|#ISGEC001175.sn
		if	rep.rbsc <> 0	then
			tot.rbsc = tot.rbsc + rep.rbsc
		endif
		rep.bbal = tot.obsc -  tot.rbsc	
		in.obsc = rep.bbal
							|#ISGEC001175.en
| 		rep.bbal = rep.obsc - rep.rbsc			|#ISGECDV001192.n	|#ISGEC001175.o	
		print.detail()
		receipt.found = true
		Initialize_Variable_Multi_Receipt()
	selectempty
		return(false)	
	endselect
	return(true)
}

function boolean Get_Purchase_Receipt(
			domain	tcorno		in.orno,
			domain	tcpono		in.pono,
			domain	tcorno		in.rcno.f,
			domain	tcorno		in.rcno.t,
			domain	tcdate		in.rcdt.f,
			domain	tcdate		in.rcdt.t,
			domain	tcqiv1		in.qoor,
			domain	tcpric		in.pric,
			domain	tcccty		in.ccty,
			domain	tccvat		in.cvat,
			domain	tcamnt		in.tasv.l,		
			domain	tcamnt		in.tase.l,			|#ISGECDV001031.n
			domain	tcamnt		in.tass.l,			|#ISGECDV001031.n
		ref	domain	tcamnt		in.obsc, 			|#ISGEC001175.n		
		ref	domain	tcamnt		in.ogrs				|#ISGEC001125.n
					)
{
	domain	tcpvat	r.eces.per
	domain	tcpvat	r.cess.per
	domain	tcpvat	r.vat.per
	domain	tcpvat	r.ser.per
	domain	tcpvat	r.sces.per
	domain	tcpvat	r.sgst.per	|#ISGECGST002.sn
	domain	tcpvat	r.cgst.per
	domain	tcpvat	r.igst.per	|#ISGECGST002.en
	domain	tcamnt	v.eces
	domain	tcamnt	v.cess
	domain	tcamnt	v.serv
	domain	tcamnt	v.sces
	domain	tcamnt	tot.rcpt		|#ISGEC001125.n
	domain	tcamnt	tot.ogrs		|#ISGEC001125.n
	domain	tcamnt	tot.rbsc		|#ISGEC001175.n
	domain	tcamnt	tot.obsc		|#ISGEC001175.n
	
	tot.rcpt = 0			|#ISGEC001125.n
	tot.ogrs = in.ogrs			|#ISGEC001125.n
	tot.rbsc = 0				|#ISGEC001175.n
	tot.obsc = in.obsc			|#ISGEC001175.n
	select	tdpur406.*
	from	tdpur406
	where	tdpur406._index2 inrange {:in.rcno.f} and	{:in.rcno.t}
	and	tdpur406._index1 = {:in.orno, :in.pono}
	and	tdpur406.ddte inrange	:in.rcdt.f	and	:in.rcdt.t
	and	tdpur406.rcno not in (select	whinh312.rcno	from	whinh312
					where	whinh312._index4 = {whinh.oorg.purchase, :in.orno, :in.pono}
					and	whinh312._index1 inrange {:in.rcno.f} and	{:in.rcno.t}
					and	whinh312.ardt inrange	:in.rcdt.f	and	:in.rcdt.t)
	selectdo	
		rep.rcno = tdpur406.rcno	
		rep.rcln = tdpur406.rseq
		rep.rcdt = tdpur406.ddte
		rep.rqnt = tdpur406.qiap	
		select	whisg312.inrq
		from	whisg312
		where	whisg312._index1 = {:tdpur406.rcno, :tdpur406.rseq}
		selectdo
			rep.blad = whisg312.inrq
			get.billing.value(whisg312.inrq, tdpur406.rcno, tdpur406.rseq, rep.bval)
		selectempty
			rep.blad = ""
			rep.bval = 0
		endselect
		
		
| 		Get_GR_Supp_Invn(tdpur406.dino, rep.isup, rep.grno)		|#ISGECDV001176.o
		Get_GR_Supp_Invn(tdpur406.dino, rep.isup, rep.grno, rep.grdt, rep.trnm)		|#ISGECDV001176.n
		v.eces = 0
		v.cess = 0
		r.eces.per = 0
		r.cess.per = 0
		r.vat.per = 0
		r.ser.per = 0
		r.sces.per = 0
		r.sgst.per = 0.00	|#ISGECGST002.sn
		r.cgst.per = 0.00
		r.igst.per = 0.00	|#ISGECGST002.en
		v.serv = 0
		v.sces = 0
		
		rep.rbsc = (tdpur406.qiap * in.pric)
		rep.rprc = in.pric
		Calculate_Other_Charge(tdpur406.rcno, tdpur406.rseq, rep.roch)
		
		if	tdpur406.conf = tcyesno.yes	then
			rep.rstt = "Confirmed"
| 			get.tax.from.tfacp935(tdpur406.rcno, tdpur406.rseq, rep.rexc, rep.rcst, rep.rstx)	|#ISGEC001069.o
												|#ISGEC001069.sn
			get.tax.from.tfacp935(tdpur406.rcno, tdpur406.rseq, tdpur406.orno,
						tdpur406.pono, tdpur406.sqnb, rep.rexc, rep.rcst, rep.rstx,
						rep.rsgst, rep.rcgst, rep.rigst)		|#ISGECGST002.n
												|#ISGEC001069.en
		else
			rep.rstt = "Open"
			get.tax.rate(in.ccty, in.cvat, tdpur406.ddte, r.eces.per, r.cess.per, r.vat.per, r.ser.per, r.sces.per,
					r.sgst.per, r.cgst.per, r.igst.per)	|#ISGECGST002.n
				
| 			v.eces =  ( (rep.rbsc * r.eces.per) / 100 )		|#ISGECDV001031.o
								|#ISGECDV001032.sn
			if	in.qoor <> 0	then
				in.tase.l = (in.tase.l/in.qoor)* tdpur406.qiap
				in.tass.l = (in.tass.l/in.qoor)* tdpur406.qiap
				tdpur401.tasg.l = (tdpur401.tasg.l/in.qoor)* tdpur406.qiap
			else
				in.tase.l = 0
				in.tass.l = 0
				tdpur401.tasg.l = 0
			endif
										|#ISGECDV001032.en
			v.eces =  ( (in.tase.l * r.eces.per) / 100 )		|#ISGECDV001031.n
			v.cess = (v.eces * r.cess.per) / 100 
			rep.rexc = rep.rexc + (v.eces + v.cess)
			
| 			v.serv = ((rep.rbsc * r.ser.per) /100)			|#ISGECDV001031.o
			v.serv = ((in.tass.l * r.ser.per) /100)			|#ISGECDV001031.n
			v.sces = ((v.serv * r.sces.per) /100)
			rep.rstx = rep.rstx + (v.serv + v.sces)
					
			if	in.qoor <> 0	then
				in.tasv.l = (in.tasv.l/in.qoor) * tdpur406.qiap
			else
				in.tasv.l = 0
			endif	
			rep.rcst = ((in.tasv.l + rep.rexc) * r.vat.per) / 100
			
			rep.rsgst = (rep.rsgst * r.sgst.per) / 100	|#ISGECGST002.sn
			rep.rcgst = (rep.rcgst * r.cgst.per) / 100
			rep.rigst = (rep.rigst * r.igst.per) / 100	|#ISGECGST002.en
		endif	
		
		rep.rgrs = (rep.rbsc + rep.rexc + rep.rcst + rep.roch + rep.rstx + 
				rep.rsgst + rep.rcgst + rep.rigst)	|#ISGECGST002.n
			
							|#ISGEC001125.sn
		if	rep.rgrs <> 0	then
			tot.rcpt = tot.rcpt + rep.rgrs
		endif
		rep.rbal = tot.ogrs -  tot.rcpt
		in.ogrs = rep.rbal	
							|#ISGEC001125.en
							|#ISGEC001175.sn
		if	rep.rbsc <> 0	then
			tot.rbsc = tot.rbsc + rep.rbsc
		endif
		rep.bbal = tot.obsc -  tot.rbsc
		in.obsc = rep.bbal	
							|#ISGEC001175.en
| 		rep.bbal = rep.obsc - rep.rbsc			|#ISGECDV001192.n	|#ISGEC001175.n
		print.detail()
		receipt.found = true
		Initialize_Variable_Multi_Receipt()	
	selectempty
		return(false)
	endselect
	
	return(true)
}

function Calculate_Other_Charge(
			domain	tcorno	i.rcno,
			domain	tcpono	i.rcln,
		ref	domain	tcamnt	tmp.roch	
				)
{
	domain tcamnt	additional.amount
	
	tmp.roch = 0
	select 	tclct200.lcos, sum(tclct200.lcam):additional.amount
	from	tclct200
	where	tclct200._index1 = {21, 100, :i.rcno, :1}
	group by tclct200.lcos
	wherebind(1, str$(i.rcln))
	selectdo
		tmp.roch = tmp.roch + additional.amount
	endselect
}

function get.tax.rate(
			domain	tcccty	i.ccty, 
			domain	tccvat	i.cvat,
			domain	tcdate	i.ddte,
		ref	domain	tcpvat	o.eces.per,
		ref	domain	tcpvat	o.cess.per,
		ref	domain	tcpvat	o.vat.per,
		ref	domain	tcpvat	o.serv.per,
		ref	domain	tcpvat	o.sces.per,
		ref	domain	tcpvat	o.sgst.per,	|#ISGECGST002.sn
		ref	domain	tcpvat	o.cgst.per,
		ref	domain	tcpvat	o.igst.per	|#ISGECGST002.en
			)
{
	o.eces.per = 0 
	o.cess.per = 0
	o.vat.per = 0
	o.serv.per = 0
	o.sces.per = 0
	o.sgst.per = 0.00	|#ISGECGST002.sn
	o.cgst.per = 0.00
	o.igst.per = 0.00	|#ISGECGST002.en
	
	select 	tctax941.ccty, tctax941.cvat
	from 	tctax941
	where	tctax941._index1={:i.ccty, :i.cvat}
	and	tctax941.type = {20}
	selectdo
		select 	tcmcs036.ccty, tcmcs036.cvat, tcmcs036.indt.l
		from 	tcmcs036
		where	tcmcs036._index1 = {:tctax941.ccty, :tctax941.cvat}
		selectdo
			on case	tcmcs036.indt.l
				case tctax.indt.l.bed:
					o.eces.per = get.rate.from.tcmcs032(tcmcs036.ccty, tcmcs036.cvat, i.ddte)
					break
				case tctax.indt.l.service:
					o.serv.per = get.rate.from.tcmcs032(tcmcs036.ccty, tcmcs036.cvat, i.ddte)
					break	
				case tctax.indt.l.e.cess.excise:	
				case tctax.indt.l.hse.cess.excise:	
					o.cess.per = o.cess.per + get.rate.from.tcmcs032(tcmcs036.ccty, tcmcs036.cvat, i.ddte)
					break
				case tctax.indt.l.e.cess.service:	
				case tctax.indt.l.hse.cess.servic:
					o.sces.per = o.sces.per + get.rate.from.tcmcs032(tcmcs036.ccty, tcmcs036.cvat, i.ddte)
					break	
				case tctax.indt.l.vat:	
				case tctax.indt.l.cst:	
				case tctax.indt.l.lst:	
				case tctax.indt.l.n.a:	
					o.vat.per = o.vat.per + get.rate.from.tcmcs032(tcmcs036.ccty, tcmcs036.cvat, i.ddte)
					break
				case tctax.indt.l.sgst:		|#ISGECGST002.sn
				case tctax.indt.l.utgst:
					o.sgst.per = o.sgst.per + get.rate.from.tcmcs032(tcmcs036.ccty, tcmcs036.cvat, i.ddte)
					break
				case tctax.indt.l.cgst:	
					o.cgst.per = o.cgst.per + get.rate.from.tcmcs032(tcmcs036.ccty, tcmcs036.cvat, i.ddte)
					break
				case tctax.indt.l.igst:	
					o.igst.per = o.igst.per + get.rate.from.tcmcs032(tcmcs036.ccty, tcmcs036.cvat, i.ddte)
					break			|#ISGECGST002.en
			ENDCASE		
		endselect
	endselect	
}

function domain tcpvat get.rate.from.tcmcs032(
			domain	tcccty	in.ccty,
			domain	tccvat	in.cvat,
			domain	tcdate	in.ddte
			)
{
	domain	tcpvat	tmp.pvat
	
	tmp.pvat = 0
	select 	tcmcs032.ccty, tcmcs032.cvat, tcmcs032.edat, tcmcs032.pvat
	from 	tcmcs032
	where	tcmcs032._index1 = {:in.ccty, :in.cvat}
	and	tcmcs032.edat <= :in.ddte
	order by tcmcs032._index1 desc
	as set with 1 rows
	selectdo
		tmp.pvat = tmp.pvat + tcmcs032.pvat
	selectempty
		tmp.pvat = 0
	endselect
	
	return(tmp.pvat)
}

function get.tax.from.tfacp935(
			domain	tcorno	i.rcno,
			domain	tcpono	i.rcln,
			domain	tcorno	i.orno,			|#ISGEC001069.n
			domain	tcpono	i.pono,			|#ISGEC001069.n
			domain	tcpono	i.sqnb,			|#ISGEC001069.n
		ref	domain	tcamnt	o.rexc.amnt,
		ref	domain	tcamnt	o.rcst.amnt,
		ref	domain	tcamnt	o.ser.amnt,
		ref	domain	tcamnt	o.rsgst,		|#ISGECGST002.sn
		ref	domain	tcamnt	o.rcgst,		
		ref	domain	tcamnt	o.rigst			|#ISGECGST002.en
				)
{
	domain	tcamnt	amount
	
	o.rexc.amnt = 0
	o.rcst.amnt = 0
	o.ser.amnt = 0
	o.rcgst = 0	|#ISGECGST002.sn		
	o.rsgst = 0	
	o.rigst = 0	|#ISGECGST002.en
	amount = 0
	
	select	tfacp935.ccty, tfacp935.cvat, sum(tfacp935.vamt):amount
	from	tfacp935
	where	tfacp935.rcno = :i.rcno
	and	tfacp935.rseq = :i.rcln
	and	tfacp935.orno = :i.orno			|#ISGEC001069.n
	and	tfacp935.pono = :i.pono			|#ISGEC001069.n
	and	tfacp935.sqnb = :i.sqnb			|#ISGEC001069.n
	group by tfacp935.ccty, tfacp935.cvat
	selectdo
		select	tcmcs036.indt.l
		from	tcmcs036
		where	tcmcs036._index1 = {:tfacp935.ccty, :tfacp935.cvat}
		selectdo
			on case etol(tcmcs036.indt.l)
				case 4:				|Basic Excise Duty
				case 9:				|Basic Custom Duty	
				case 10:			|Countervailing Duty
				case 14:			|ECess CVD
				case 15:			|ECess on Custom
				case 17:			|Ecess on Excise
				case 19:			|SHE Cess on CVD
				case 20:			|SHE Cess on Excise
				case 22:			|SHE Cess on Custom
					o.rexc.amnt = o.rexc.amnt + amount
					break
				case 1:				|Service
				case 16:			|Ecess on Service
				case 21:			|SHE Cess on Service	
					o.ser.amnt = o.ser.amnt + amount
					break	
				case 2:				|Local Sales Tax	
				case 3:				|Central Sales Tax
				case 26:			|VAT
				case 30:			|Not Applicable
					o.rcst.amnt = o.rcst.amnt + amount
					break
				case 40:			|SGST	|#ISGECGST002.sn	
				case 43:			|UTGST		
					o.rsgst = o.rsgst + amount
					break
				case 41:			|CGST				
					o.rcgst = o.rcgst + amount
					break
				case 42:			|IGST
					o.rigst = o.rigst + amount
					break			
									|#ISGECGST002.en
			endcase
		endselect
	endselect
}

function get.billing.value(
			domain	tcorno	i.inrq,
			domain	tcorno	i.rcno,
			domain	tcpono	i.rcln,
		ref	domain	tcamnt	o.bval
			)
{
	o.bval = 0
	
	select	b_tdisg832.bivl:o.bval
	from	tdisg832 b_tdisg832
	where	b_tdisg832._index1 = {:i.inrq, :i.rcno, :i.rcln}
	selectdo
	selectempty
		o.bval = 0
	endselect
}

function get.tax.from.whinh936(
			domain	tcorno	in.rcno,
			domain	tcpono	in.rcln,	
		ref	domain	tcamnt	o.exc.amnt,
		ref	domain	tcamnt	o.cst.amnt,
		ref	domain	tcamnt	o.ser.amnt,
		ref	domain	tcamnt	o.rsgst,		|#ISGECGST002.sn
		ref	domain	tcamnt	o.rcgst,		
		ref	domain	tcamnt	o.rigst			|#ISGECGST002.en
			)
{
	domain	tcamnt	amount
	
	o.exc.amnt = 0
	o.cst.amnt = 0
	o.ser.amnt = 0
	o.rcgst = 0	|#ISGECGST002.sn		
	o.rsgst = 0	
	o.rigst = 0	|#ISGECGST002.en
	
	select	sum(whinh936.aamt):amount, whinh936.rcno, whinh936.taxc, whinh936.pvat
	from	whinh936
	where	whinh936._index1 = {:in.rcno, :in.rcln}
	group by whinh936.rcno,whinh936.taxc,whinh936.pvat
	selectdo
		fwhinh936.found = true		|#ISGECDV001022.n
		select	tcmcs036.indt.l
		from	tcmcs036
		where	tcmcs036._index1 = {"IN", :whinh936.taxc}
		selectdo
			on case etol(tcmcs036.indt.l)
				case 4:				|Basic Excise Duty
				case 9:				|Basic Custom Duty	
				case 10:			|Countervailing Duty
				case 14:			|ECess CVD
				case 15:			|ECess on Custom
				case 17:			|Ecess on Excise
				case 19:			|SHE Cess on CVD
				case 20:			|SHE Cess on Excise
				case 22:			|SHE Cess on Custom
					o.exc.amnt = o.exc.amnt + amount
					break
				case 1:				|Service
				case 16:			|Ecess on Service
				case 21:			|SHE Cess on Service	
					o.ser.amnt = o.ser.amnt + amount
					break	
				case 2:				|Local Sales Tax	
				case 3:				|Central Sales Tax
				case 26:			|VAT
				case 30:			|Not Applicable
					o.cst.amnt = o.cst.amnt + amount
					break
				case 40:			|SGST	|#ISGECGST002.sn	
				case 43:			|UTGST		
					o.rsgst = o.rsgst + amount
					break
				case 41:			|CGST				
					o.rcgst = o.rcgst + amount
					break
				case 42:			|IGST
					o.rigst = o.rigst + amount
					break			
									|#ISGECGST002.en	
			endcase
		endselect
	endselect
}

function Get_GR_Supp_Invn(
			domain	tcibd.sern	in.dino,
		ref	domain	tfacp.isup	ot.isup,
		ref	domain	tcmcs.str215m	ot.grno,
		ref	domain	tcmcs.str215m	ot.grdt,		|#ISGECDV001176.n
		ref	domain	tcmcs.str256m	ot.trnm			|#ISGECDV001176.n
			)
{
	long	cnt.rec
	ot.isup = ""
	ot.grno = ""
	ot.grdt = ""			|#ISGECDV001176.n
	ot.trnm = ""			|#ISGECDV001176.n
	
	select	tfisg001.irno, tfacp100.isup
	from	tfisg001, tfacp100
	where	tfisg001._index1 = {:1}
	and	tfisg001.irno refers to tfacp100 Unref Clear
	as set with 1 rows
	wherebind(1, lval(in.dino))
	selectdo
		ot.isup = trim$(tfacp100.isup)
		cnt.rec  = 0
		select	tfisg002.grno,
			tfisg002.grdt, tfisg003.otbp, tfisg003.bpnm, tfisg003.bpid		|#ISGECDV001176.n
		from	tfisg002, tfisg003				|#ISGECDV001176.n
		where	tfisg002._index1 = {:tfisg001.irno}
		and	tfisg002.cmbb refers to tfisg003 		|#ISGECDV001176.n
		selectdo
			cnt.rec = cnt.rec + 1
			if	cnt.rec = 1	then
				ot.grno = trim$(tfisg002.grno)
												|#ISGECDV001176.sn
| 				ot.grdt = sprintf$("%D(%02m/%02d/%04Y)", tfisg002.grdt)		|RAvi.05052020.com
				ot.grdt = sprintf$("%D(%02d/%02m/%04Y)", tfisg002.grdt)		|RAvi.05052020.a
				if	tfisg003.otbp = tcyesno.yes	then
					ot.trnm = trim$(tfisg003.bpnm)
				else
					ot.trnm = trim$(Get_BP_Name(tfisg003.bpid))
				endif	
												||#ISGECDV001176.en
			else
				ot.grno = trim$(ot.grno) & ", " & trim$(tfisg002.grno)
															|#ISGECDV001176.sn
| 				ot.grdt = trim$(ot.grdt) & ", " & trim$(sprintf$("%D(%02m/%02d/%04Y)", tfisg002.grdt))	|RAvi.05052020.com
				ot.grdt = trim$(ot.grdt) & ", " & trim$(sprintf$("%D(%02d/%02m/%04Y)", tfisg002.grdt))	|RAvi.05052020.a
				if	tfisg003.otbp = tcyesno.yes	then
					ot.trnm = trim$(ot.trnm) & ", " & trim$(tfisg003.bpnm)
				else
					ot.trnm = trim$(ot.trnm) & ", " & trim$(Get_BP_Name(tfisg003.bpid))
				endif
															|#ISGECDV001176.en
			endif	
		endselect
	endselect
}

						|#ISGECDV001176.sn
function domain tcnama Get_BP_Name(
				domain	tcnama	i.bpid
				)
{
	domain	tcnama	tbp.nama
	
	tbp.nama = ""
	select	bp_tccom100.nama:tbp.nama
	from	tccom100 bp_tccom100
	where	bp_tccom100._index1 = {:i.bpid}
	selectdo
	endselect
	
	return(tbp.nama)
}
						|#ISGECDV001176.en

function	generate.header()
{
	line4 =	concat$("	",
		sprintf$("PO Number"),					|1		
		sprintf$("Supplier"),					|2		|#ISGECDV001027.n
		sprintf$("Name"),					|3		|#ISGECDV001027.n
		sprintf$("Buyer"),					|4		|#ISGEC001069.n
		sprintf$("Buyer Name"),				|5		|#ISGEC001069.n
		sprintf$("Position"),					|6
		sprintf$("Warehouse"),					|6.1		|GH431CR774.a
		sprintf$("Order Date"),				|7
		sprintf$("Currency"),					|7.1		|#ISGEC015087.n
		sprintf$("Order Status"),				|8		|#ISGEC001087.n
		sprintf$("PO Workflow Status"),			|8.1		|GH588CR910.a
		sprintf$("Delivery Date"),				|9		|#ISGEC001069.n
		sprintf$("Item"),					|10
		sprintf$("Description"),		  		|11
		sprintf$("HSN/SAC Code"),		  		|11.A	|#ISGECGST002.n
		sprintf$("Project"),					|12
		sprintf$("Description"),			  	|13
		sprintf$("Element"),					|14
		sprintf$("Description"),			        |15
		sprintf$("Quantity"),					|16
		sprintf$("Price"),					|17
		sprintf$("Basic Value"),				|18
		sprintf$("Excise"),					|19	
		sprintf$("CST/VAT"),					|20
		sprintf$("S. Tax"),					|21
		sprintf$("IGST"),					|21.A	|#ISGECGST002.sn
		sprintf$("CGST"),					|21.B
		sprintf$("SGST/UTGST"),				|21.C	|#ISGECGST002.en
		sprintf$("Landed Cost"),				|22
		sprintf$("Gross Value"),				|23
		sprintf$("Receipt No."),				|24
		sprintf$("Receipt Position"),				|25
		sprintf$("Receipt Date"),				|26
		sprintf$("Supplier Invoice"),				|27
		sprintf$("GR Number"),					|28
		sprintf$("GR Date"),					|29			|#ISGECDV001176.n
		sprintf$("Transporter Name"),				|30			|#ISGECDV001176.n
		sprintf$("Receipt Quantity"),				|31	
		sprintf$("Receipt Price"),				|32	
		sprintf$("Receipt Basic Amount"),			|33
		sprintf$("Excise Duty"),				|34
		sprintf$("CST/VAT"),					|35
		sprintf$("Service Tax"),				|36
		sprintf$("Receipt IGST"),				|36.A	|#ISGECGST002.sn
		sprintf$("Receipt CGST"),				|36.B	
		sprintf$("Receipt SGST/UTGST"),			|36.C	|#ISGECGST002.en
		sprintf$("Other Charge"),				|37
		sprintf$("Gross Amount"),				|38
		sprintf$("Basic Balance Amount"),			|39			|#ISGECDV001192.n
		sprintf$("Receipt Balance Amount"),			|40			|#ISGEC001125.n
		sprintf$("Receipt Status"),				|41
		sprintf$("Billing Advice"),				|42
		sprintf$("Billing Value"))				|43											
		
	line4 = strip$(shiftl$(line4))		
	seq.puts(line4, stat.fp)	
}

function	print.detail()
{	
	line3 =	concat$("	",
		sprintf$("%s", rep.orno),				|1
		sprintf$("%s", rep.otbp),				|2				|#ISGECDV001027.n
		sprintf$("%s", rep.nama),				|3				|#ISGECDV001027.n
		sprintf$("%s", rep.buyr),				|4				|#ISGEC001069.n
		sprintf$("%s", rep.bynm),				|5				|#ISGEC001069.n
		sprintf$("%d", rep.pono),				|6
		sprintf$("%s", rep.cwar),				|6.1				|GH431CR774.a
		sprintf$("%u(%02d/%02m/%04Y)", rep.odat),		|7
		sprintf$("%s", rep.ccur),				|7.1				|# ISGEC015087.n
		sprintf$("%s", rep.hdst),				|8				|#ISGEC001087.n
		sprintf$("%s", enum.descr$("tdmsl.work", tdmsl400.work)),	|8.1				|GH588CR910.a
		sprintf$("%u(%02d/%02m/%04Y)", rep.ddta),		|9				|#ISGEC001069.n
		sprintf$("%s", rep.item),				|10
		sprintf$("%s", rep.itdsc),				|11
		sprintf$("%s", rep.hcod),				|11.A	|#ISGECGST002.n
		sprintf$("%s", rep.cprj),				|12	
		sprintf$("%s", rep.pjdsc),				|13
		sprintf$("%s", rep.cspa),				|14
		sprintf$("%s", rep.eldsc),				|15
		sprintf$("%f", rep.oqnt),				|16
		sprintf$("%f", rep.oprc),				|17
		sprintf$("%f", rep.obsc),				|18
		sprintf$("%f", rep.oexc),				|19
		sprintf$("%f", rep.ocst),				|20
		sprintf$("%f", rep.ostx),				|21
		sprintf$("%f", rep.igst),				|21.A	|#ISGECGST002.sn
		sprintf$("%f", rep.cgst),				|21.B	
		sprintf$("%f", rep.sgst),				|21.C	|#ISGECGST002.en
		sprintf$("%f", rep.lcst),				|22
		sprintf$("%f", rep.ogrs),				|23
		sprintf$("%s", rep.rcno),				|24
		sprintf$("%d", rep.rcln),				|25
		sprintf$("%u(%02d/%02m/%04Y)", rep.rcdt),		|26
		sprintf$("%s", rep.isup),				|27
		sprintf$("%s", rep.grno),				|28
		sprintf$("%s", rep.grdt),				|29		|#ISGECDV001176.n
		sprintf$("%s", rep.trnm),				|30		|#ISGECDV001176.n
		sprintf$("%f", rep.rqnt),				|31
		sprintf$("%f", rep.rprc),				|32
		sprintf$("%f", rep.rbsc),				|33
		sprintf$("%f", rep.rexc),				|34
		sprintf$("%f", rep.rcst),				|35
		sprintf$("%f", rep.rstx),				|36
		sprintf$("%f", rep.rigst),				|36.A	|#ISGECGST002.sn
		sprintf$("%f", rep.rcgst),				|36.B	
		sprintf$("%f", rep.rsgst),				|36.C	|#ISGECGST002.en
		sprintf$("%f", rep.roch),				|37
		sprintf$("%f", rep.rgrs),				|38
		sprintf$("%f", rep.bbal),				|39			|#ISGECDV001192.n
		sprintf$("%f", rep.rbal),				|40			|#ISGEC001125.n
		sprintf$("%s", rep.rstt),				|41
		sprintf$("%s", rep.blad),				|42
		sprintf$("%f", rep.bval))				|43
	
		line3 = strip$(shiftl$(line3))
		seq.puts(line3, stat.fp)
}	

function Initialize_Variable()
{
	rep.orno = ""				|1
	rep.otbp = ""				|2				|#ISGECDV001027.n
	rep.nama = ""				|3				|#ISGECDV001027.n
	rep.buyr = ""				|4				|#ISGEC001069.n
	rep.bynm = ""				|5				|#ISGEC001069.n
	rep.pono = 0				|6
	rep.cwar = ""				|6.1				|GH431CR774.a
	rep.odat = 0				|7
	rep.ccur = ""				|7.1				|#ISGEC015087.n
	rep.hdst = ""				|8				|#ISGEC001087.n
	rep.ddta = 0				|9				|#ISGEC001069.n
	rep.item = ""				|10
	rep.itdsc = ""				|11
	rep.cprj = ""				|12	
	rep.pjdsc = ""				|13
	rep.cspa = ""				|14
	rep.eldsc = ""				|15
	rep.oqnt = 0				|16
	rep.oprc = 0				|17
	rep.obsc = 0				|18
	rep.oexc = 0				|19
	rep.ocst = 0				|20
	rep.ostx = 0				|21
	rep.lcst = 0				|22
	rep.ogrs = 0				|23
	rep.rcno = ""				|24
	rep.rcln = 0				|25
	rep.rcdt = 0				|26
	rep.isup = ""				|27
	rep.grno = ""				|28
	rep.grdt = ""				|29			|#ISGECDV001176.n
	rep.trnm = ""				|30			|#ISGECDV001176.n
	rep.rqnt = 0				|31
	rep.rprc = 0				|32
	rep.rbsc = 0				|33
	rep.rexc = 0				|34
	rep.rcst = 0				|35
	rep.rstx = 0				|36
	rep.roch = 0				|37
	rep.rgrs = 0				|38
	rep.bbal = 0				|39			|#ISGECDV001192.n
	rep.rbal = 0				|40			|#ISGEC001125.n
	rep.rstt = ""				|41
	rep.blad = ""				|42
	rep.bval = 0				|43
	rep.sgst = 0.00		|#ISGECGST002.sn
	rep.cgst = 0.00
	rep.igst = 0.00
	rep.rsgst = 0.00
	rep.rcgst = 0.00
	rep.rigst = 0.00	|#ISGECGST002.en
}

function Initialize_Variable_Multi_Receipt()
{
	rep.oqnt = 0				|16
	rep.oprc = 0				|17
	rep.obsc = 0				|18
	rep.oexc = 0				|19
	rep.ocst = 0				|20
	rep.ostx = 0				|21
	rep.lcst = 0				|22
	rep.ogrs = 0				|23
	rep.rcno = ""				|24
	rep.rcln = 0				|25
	rep.rcdt = 0				|26
	rep.isup = ""				|27
	rep.grno = ""				|28
	rep.grdt = ""				|29			|#ISGECDV001176.n
	rep.trnm = ""				|30			|#ISGECDV001176.n
	rep.rqnt = 0				|31
	rep.rprc = 0				|32
	rep.rbsc = 0				|33
	rep.rexc = 0				|34
	rep.rcst = 0				|35
	rep.rstx = 0				|36
	rep.roch = 0				|37
	rep.rgrs = 0				|38
	rep.bbal = 0				|39			|#ISGECDV001192.n
	rep.rbal = 0				|40			|#ISGEC001125.n
	rep.rstt = ""				|41
	rep.blad = ""				|42
	rep.bval = 0				|43
	
	rep.sgst = 0.00		|#ISGECGST002.sn
	rep.cgst = 0.00
	rep.igst = 0.00
	rep.rsgst = 0.00
	rep.rcgst = 0.00
	rep.rigst = 0.00	|#ISGECGST002.en
}

function get.date.utc(
		domain	tcdate	i.date,
		long		i.hh,
		long		i.mn,
		long		i.ss,
	ref	domain	tcdate	o.date
			)
{
	long	dd, mm, yy, hh, mn, ss
	
	utc.to.date(i.date, yy , mm, dd, hh, mn, ss)
	o.date = date.to.utc(yy, mm, dd, i.hh, i.mn, i.ss)
	if	o.date < 0	then
		o.date = 0
	endif
}	

function open.file()
{
	err.file = bse.tmp.dir$() & str$(utc.num()) & ".xls"
	
	file.name = "c:\temp\" & str$(utc.num())  & ".xls"
	
	stat.fp = seq.open(err.file, "w+")
	if stat.fp >0 then
 		seq.seek(0, 0, stat.fp)
	endif
}

function close.file()
{
	seq.close(stat.fp)
	ret2 = server2client(err.file,file.name,1)
	ret2 = app_start(file.name,"c:","","","")
}

function get.po.date()								|#ISGEC01072.sn
{
	select	tdmsl400.apdt
	from	tdmsl400
	where	tdmsl400._index1 = {:tdpur400.orno,0}
	selectdo
		rep.odat = tdmsl400.apdt
	selectempty
		tdmsl400.apdt = 0
	endselect	
}										|#ISGEC01072.en

function get.po.workstatus()							|GH588CR910.a.s
{
	select	tdmsl400.orno,
		tdmsl400.vrsn,
		tdmsl400.work					
	from	tdmsl400
	where	tdmsl400._index1 = {:tdpur400.orno}
	order by tdmsl400._index1
	selectdo
	selectempty
		tdmsl400.work = empty
	endselect	
}										|GH588CR910.a.e



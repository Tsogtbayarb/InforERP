|******************************************************************************
|* tdisg4213m000  0  VRC B61U a7 live
|* Update PO Activities
|* Tools                         
|* 2014-03-11
|******************************************************************************
|* Main table tdpur413 Purchase Order Activities, Form Type 4
|******************************************************************************
|* IDENT ISG001015, Manish Kumar , IT0303 , 11-03-2014 ,VRC B61U a7 live
|* Update Session For Inserting Activities in tdpur413
|****************************** declaration section ***************************
declaration:
#include <bic_dam>
	table   ttdpur413 | Purchase Order Activities
	table	ttdpur400
	table	ttdpur401
	table	ttdpur413
	table	ttdpur060

	extern	 domain		tccotp	cotp.f,cotp.t
|****************************** program section ********************************


|****************************** Field section **********************************
field.cotp.f:
when.field.changes:
	cotp.t = cotp.f
|****************************** Choice section *********************************
choice.update:
on.choice:
	if Insert.Activities() then
		message("Process Completed")
	endif
|****************************** group section **********************************

group.1:
init.group:
   get.screen.defaults()

|*************************** Functions Sections ********************************
functions:
function boolean Insert.Activities()
{
	select	tdpur060.cotp,tdpur060.acti,tdpur060.auto,
		tdpur060.prtr,tdpur060.acno
	from	tdpur060
	where	tdpur060._index1 inrange {:cotp.f} and {:cotp.t}
	selectdo
		select	tdpur400.orno,tdpur401.orno,tdpur401.pono,tdpur401.sqnb
		from	tdpur400,tdpur401
		where	tdpur400.cotp = {:tdpur060.cotp}
		and	tdpur400.orno refers to tdpur401
		and	tdpur401.sqnb <> 0
		selectdo
			if not Insert.tdpur413(
					tdpur400.orno,				|PO
					0,					|Position
					0,					|Sequence
					0,					|Receipt Sequence
					tdpur060.acti,				|Activity
					tcyesno.no,				|Manual
					tcyesno.yes,				|Applicable
					tdpur060.auto,				|Automatic
					tdpur060.acno,				|Execution Sequence
					tdxcst.not.applicable,			|Execution Status
					tdpur060.prtr,				|Device
					tdpur060.copy,				|No. Of Copies
					tcyesno.no				|Next Activity
					) then
				return(false)
			endif
			if not Insert.tdpur413(
					tdpur400.orno,				|PO
					tdpur401.pono,				|Position
					tdpur401.sqnb,				|Sequence
					0,					|Receipt Sequence
					tdpur060.acti,				|Activity
					tcyesno.no,				|Manual
					tcyesno.yes,				|Applicable
					tdpur060.auto,				|Automatic
					tdpur060.acno,				|Execution Sequence
					tdxcst.not.applicable,			|Execution Status
					tdpur060.prtr,				|Device
					tdpur060.copy,				|No Of Copies
					tcyesno.no				|Next Activity
					) then
			
				return(false)
			endif
		endselect
	endselect
	return(true)
}

function boolean Insert.tdpur413
	(
		domain	tcorno		i.orno,
		domain	tcpono		i.pono,
		domain	tcpono		i.sqnb,
		domain	tcpono		i.rsqn,
		domain	tcmcs.st20m	i.acti,
		domain	tcyesno		i.manu,
		domain	tcyesno		i.appl,
		domain	tcyesno		i.auto,
		domain	tcmcs.int2	i.xcsq,
		domain	tdxcst		i.xcst,
		domain	tcmcs.str15	i.prtr,
		domain	tccopy		i.copy,
		domain	tcyesno		i.nxct
	)
{
	long ret_val
	select	tdpur413.*
	from	tdpur413 for update
	where	tdpur413._index1 = {:i.orno,:i.pono,:i.sqnb,:i.rsqn,:i.acti}
	selectdo
	selectempty
		dal.new.object("tdpur413")
		
		dal.set.field("tdpur413.orno",i.orno)
	
		dal.set.field("tdpur413.pono",i.pono)
		
		dal.set.field("tdpur413.sqnb",i.sqnb)
		
		dal.set.field("tdpur413.rsqn",i.rsqn)
		
		dal.set.field("tdpur413.acti",i.acti)
		
		dal.set.field("tdpur413.manu",i.manu)
		
		dal.set.field("tdpur413.appl",i.appl)
		
		dal.set.field("tdpur413.auto",i.auto)
		
		dal.set.field("tdpur413.xcsq",i.xcsq)
		
		dal.set.field("tdpur413.xcst",i.xcst)
		
		dal.set.field("tdpur413.prtr",i.prtr)
		
		dal.set.field("tdpur413.copy",i.copy)
		
		dal.set.field("tdpur413.nxct",i.nxct)
		
		ret_val = dal.save.object("tdpur413")
		
		if ret_val = 0 then
			commit.transaction()
			return(true)
		else
			abort.transaction()
			show.dal.messages(MSG.ALL)
			return(false)
		endif
	endselect
	return(true)
}

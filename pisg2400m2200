|******************************************************************************
|* tfisg2400m220  0  VRC B61U a7 isg 
|* Customer wise Retention
|* Installation user             
|* 2014-11-26
|******************************************************************************
|* Form Type 4
|******************************************************************************
|* IDENT ISGEC0108,Mani sharma,Modification on report
|*
|* IDENT ISGEC015048, Gokul Chaurasia, 28-08-2015, VRC B61U a7 isg
|* Add Currecny & Amount In FC Fields In Report
|****************************** declaration section ***************************
declaration:

	table	ttfgld102
	table	ttfgld106
	table	ttccom100
	table   ttfgld005
	
	extern	domain	tccom.bpid	bpid.f,bpid.t
	extern	domain	tfgld.leac	leac.f
	extern	domain	tfgld.date	date.f
	
	extern	domain	tccom.bpid	rep.bpid
	extern	domain	tcnama		rep.nama
	extern	domain	tfgld.amnt	rep.amth
	extern	domain	tfgld.dbcr	rep.dbcr
	
	extern	domain	tfgld.ttyp	rep.ttyp
	extern	domain	tfgld.docn	rep.docn
	extern	domain	tfgld.lino	rep.lino
	extern	domain	tfgld.btno	rep.btno
	
	extern domain  tfgld.year      fyer				|ISGEC0108.sn
	extern domain  tfgld.prod      fprd.f,fprd.t			|ISGEC0108.en
	
	extern	domain	tfgld.amnt	rep.amnt			|#ISGEC015048.sn
	extern	domain	tcccur		rep.ccur			|#ISGEC015048.en
|****************************** program section ********************************


|****************************** group section **********************************

group.1:
init.group:
   get.screen.defaults()

|****************************** choice section ********************************

choice.cont.process:
on.choice:
   execute(print.data)

choice.print.data:
on.choice:
   if rprt_open() then
       read.main.table()
       rprt_close()
   else
       choice.again()
   endif


|****************************** field section *********************************

field.bpid.f:
when.field.changes:
	bpid.t = bpid.f
	
									|ISGEC0108.sn
	
field.fprd.f:
when.field.changes:
	fprd.t = fprd.f 

before.zoom:
	tfgld005.year = fyer						|ISGEC0108.en
	

|****************************** function section ******************************

functions:

function read.main.table()
{
	select 	tfgld102.amth,
		tfgld102.bpid,
		tfgld102.dbcr,
		tccom100.nama,
		tfgld102.fprd,
		tfgld102.fyer,
		tfgld102.ttyp:rep.ttyp,
		tfgld102.docn:rep.docn,
		tfgld102.lino:rep.lino,
		tfgld102.btno:rep.btno,
		tfgld102.amnt,						|#ISGEC015048.sn
		tfgld102.ccur						|#ISGEC015048.en
	from	tfgld102,tccom100
	where	tfgld102.leac = :leac.f
	and	tfgld102.bpid inrange :bpid.f and :bpid.t
| 	and     tfgld102.fyer = :fyer					|ISGEC0108.sn
	and     tfgld102.fyer <= :fyer					|ISGEC0108.sn
	and     tfgld102.fprd inrange 1 and 13				|ISGEC0108.en
| 	and	tfgld102.dcdt <= :date.f				|ISGEC0108.o
	and	tfgld102.bpid refers to tccom100
	selectdo
		if tfgld102.fyer = fyer and tfgld102.fprd > fprd.f then
			continue
		endif
			
		rep.amnt = 0
		rep.ccur = ""
		
		rep.bpid = tfgld102.bpid
		rep.nama = tccom100.nama
		rep.dbcr = tfgld102.dbcr
		rep.amth = tfgld102.amth(1)
		if tfgld102.ccur <> "INR" then
			rep.amnt = tfgld102.amnt				|#ISGEC015048.sn
			rep.ccur = tfgld102.ccur				|#ISGEC015048.en
		endif
		
		rprt_send()

	endselect
	

	select 	tfgld106.amth,
		tfgld106.dbcr,
		tfgld106.bpid,
		tccom100.nama,
		tfgld106.fyer,
		tfgld106.fprd,
		tfgld106.otyp:rep.ttyp,
		tfgld106.odoc:rep.docn,
		tfgld106.olin:rep.lino,
		tfgld106.obat:rep.btno,
		tfgld106.amnt,						|#ISGEC015048.sn
		tfgld106.ccur						|#ISGEC015048.en
	from	tfgld106,tccom100
	where	tfgld106._index3 = {:leac.f}
| 	and	tfgld106.dcdt <= :date.f				|ISGEC0108.o
	and     tfgld106.fyer <= :fyer					|ISGEC0108.sn
| 	and     tfgld106.fprd inrange :fprd.f and :fprd.t		|ISGEC0108.en
	and     tfgld106.fprd inrange 1 and 13		|ISGEC0108.en
	and	tfgld106.bpid inrange :bpid.f and :bpid.t
	and	tfgld106.bpid refers to tccom100
	selectdo
		if tfgld106.fyer = fyer and tfgld106.fprd > fprd.f then
			continue
		endif
		
| 		rep.amnt = 0
| 		rep.ccur = ""
		rep.bpid = tfgld106.bpid
		rep.nama = tccom100.nama
		rep.dbcr = tfgld106.dbcr
		rep.amth = tfgld106.amth(1)
		if tfgld106.ccur <> "INR" then
			rep.amnt = tfgld106.amnt				|#ISGEC015048.sn
			rep.ccur = tfgld106.ccur				|#ISGEC015048.en
		endif
		rprt_send()
	endselect
	
}

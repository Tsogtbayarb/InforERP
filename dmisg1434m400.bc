|******************************************************************************
|* dmisg1434m400  0  VRC B61U a7 isg 
|* Pre Order Receipt History
|* merino1                       
|* 2017-12-29
|******************************************************************************
|* Main table dmisg134 Receive Vendor Document, Form Type 4
|******************************************************************************								
|* 
|* GH611CR903, RAvi Kumar, 22-07-2021
|* CR903-Additional disciplines to be added in ERP IDMS for Pre-order and post-order functionality
|****************************** declaration section ***************************
declaration:

	table	tdmisg134 | Receive Vendor Document
	table	tdmisg121
	table	tdmisg131
	table	tdmisg135
	table	tdmisg136
	table	ttcibd001
	table	ttccom100
	table	ttcisg005
	table	ttdpur200
	table	ttdpur400

	extern	domain	tfgld.date	date.f, date.t
	extern	domain	tccprj		cprj.f, cprj.t
	extern	domain	whinh.shpm	rcno.f, rcno.t
		domain	dmdoc.docid	specification.ID
		domain	tcmcs.long	receipt.cycle.time
		domain	tcyesno		sent.for.eval(10)
		domain	tcmcs.str30	technically.cleared.time
		domain	tcmcs.str100m	specification.desc
		domain	tcdate		cmp.date.f, cmp.date.t, specification.date, evaluation.date(10)	
		domain	tcdsca		lot.item.desc
		domain	tcmcs.str5	evaluation.cycle.time(10), evaluated(10), technical.clearance(10)
		domain	dmisg.eng	engg.dept
		domain	tcnama		o.vendor.name, evaluator.name(10)
		domain	tcrqno		indent.no
		domain	tcdate		status.date, indent.date
		domain	tcnama		status.user.name
		domain	tfgld.user	status.user
		domain	tfgld.date	date.A, date.B
			long		dept.loop
			
			string	err.file(100), file.name(100), line(2500)
			long  	stat.fp, ret2
	
| 	#define NO_OF_DEPTS 7		|GH611CR903.old
	#define NO_OF_DEPTS 9	
	#pragma used dll ottdllbw 
	
	

|****************************** program section ********************************
before.program:

|****************************** group section **********************************

group.1:
init.group:
	get.screen.defaults()

|****************************** choice section ********************************

choice.print.report:	
on.choice:
	generate.file()
	
		

|****************************** field section *********************************	

field.cprj.f:
when.field.changes:
	cprj.t = cprj.f

field.date.f:
when.field.changes:
	date.t = date.f

field.rcno.f:
when.field.changes:
	rcno.t = rcno.f


|****************************** function section ******************************

functions:

function generate.file()
{
	err.file = "c:\temp\" & str$(utc.num()) & ".xls"
	
	file.name = "c:\temp\" & str$(utc.num())  & ".xls"
	
	stat.fp = seq.open(err.file, "w+")
	
	generate.header()
	
	read.main.table()
	
	message("Please refer to file %s in C:", file.name)
	
	seq.close(stat.fp)
	ret2 = server2client(err.file,file.name,1)
	ret2 = app_start(file.name,"c:","","","")
}

|* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

function generate.header()
{
	if stat.fp >0 then
		
		line =concat$("	", 
			sprintf$("Project ID"),			|1
			sprintf$("Vendor Name"),			|2
			sprintf$("Receipt No."),			|3
			sprintf$("Revision"),				|4
			sprintf$("Receipt creation Date"),		|5
			sprintf$("Indent to Offer receipt cycle time"),|6
			sprintf$("Offer receipt to Evaluation cycle time"),|7
			sprintf$("Offer receipt to Technicaly cleared cycle time"),|8
			sprintf$("Mechanical evaluation cycle time"),	|9
			sprintf$("Process evaluation cycle time"),	|10
			sprintf$("Structure evaluation cycle time"),	|11
			sprintf$("Piping evaluation cycle time"),	|12
			sprintf$("Quality evaluation cycle time"),	|13
			sprintf$("C&I evaluation cycle time"),		|14
			sprintf$("Electrical evaluation cycle time"),	|15		
			sprintf$("WWS evaluation cycle time"),		|15.1		|GH611CR903.a
			sprintf$("MHE evaluation cycle time"),		|15.2		|GH611CR903.a
			sprintf$("Linked Specification ID"),		|16
			sprintf$("Linked Specification Description"),	|17
			sprintf$("Specification Released Date"),	|18
			sprintf$("Linked Lot Item"),			|19
			sprintf$("Lot item description"),		|20
			sprintf$("Indent no. For the Linked lot Item"),|21
			sprintf$("Indent Date"),			|22
			sprintf$("Receipt Distribution Date ( Sent for Evaluation)"),|23
			sprintf$("Cycle time for distribution"),	|24
			sprintf$("Receipt Status ( Current)"),		|25
			sprintf$("Receipt Status updated on"),		|26
			sprintf$("Receipt Status Maintained by (ID)"),	|27
			sprintf$("Receipt Status Maintained by (Name)"),|28
			sprintf$("Mechanical Response Received"),	|29
			sprintf$("Mechanical Technical Clearance"),	|30
			sprintf$("Evaluator submission Date (Mech.)"),	|31
			sprintf$("Evaluator Name (Mech.)"),		|32
			sprintf$("Structure Response Received"),	|33
			sprintf$("Structure Technical Clearance"),	|34
			sprintf$("Evaluator submission Date (structure.)"),|35
			sprintf$("Evaluator Name(structure.)"),	|36
			sprintf$("Piping Response Received"),		|37
			sprintf$("Piping Technical Clearance"),	|38
			sprintf$("Evaluator submission Date (structure.)"),|39
			sprintf$("Evaluator Name(structure.)"),	|40
			sprintf$("Process Response Received"),		|41
			sprintf$("Process Technical Clearance"),	|42
			sprintf$("Evaluator submission Date (Process)"),|43
			sprintf$("Evaluator Name(Process.)"),		|44
			sprintf$("CI Response Received"),		|45
			sprintf$("CI Technical Clearance"),		|46
			sprintf$("Evaluator submission Date (C & I)"),|47
			sprintf$("Evaluator Name(C&I.)"),		|48
			sprintf$("Electrical Response Received"),	|49
			sprintf$("Electrical Technical Clearance"),	|50
			sprintf$("Evaluator submission Date (Electrical)"),|51
			sprintf$("Evaluator Name(Electrical.)"),	|52
			sprintf$("Quality Response Received"),		|53
			sprintf$("Quality Technical Clearance"),	|54
			sprintf$("Evaluator submission Date (Quality)"),|55
			sprintf$("Evaluator Name(Quality.)"),		|56
			sprintf$("WWS Response Received"),		|57		|GH611CR903.a.s
			sprintf$("WWS Technical Clearance"),		|58
			sprintf$("Evaluator submission Date (WWS)"),	|59
			sprintf$("Evaluator Name(WWS.)"),		|60
			sprintf$("MHE Response Received"),		|61
			sprintf$("MHE Technical Clearance"),		|62
			sprintf$("Evaluator submission Date (MHE)"),	|63
			sprintf$("Evaluator Name(MHE.)"),		|64		|GH611CR903.a.e
			sprintf$("POS requested On"),			|65
			sprintf$("POS issued on"),			|66
			sprintf$(""))					|Last		
	
		line = strip$(shiftl$(line))
		seq.puts(line, stat.fp)
	endif
}

|* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

function read.main.table()
{	
	o.vendor.name = ""
	convert.dates(date.f, 0, 0, 0, cmp.date.f)
	convert.dates(date.t, 0, 0, 0, cmp.date.t)
	
	select	dmisg134.*, dmisg134.sent:sent.for.eval,
		tccom100.nama:o.vendor.name, tcibd001.dsca:lot.item.desc
	from	dmisg134, tccom100, tcibd001
	where	dmisg134._index2 inrange {:cprj.f, :rcno.f} and {:cprj.t, :rcno.t}
	and	dmisg134.date inrange :cmp.date.f and :cmp.date.t
	and	dmisg134.bpid refers to tccom100 UNREF CLEAR
	and	dmisg134.item refers to tcibd001 UNREF CLEAR
	and	dmisg134.orno = ""
	and	dmisg134.pono = 0
	selectdo
		if isspace(o.vendor.name) then
			o.vendor.name = dmisg134.nama
		endif
		
		get.indent.and.PO.data()
		get.linked.specification.data()
		
		select	tdpur400.odat
		from	tdpur400
		where	tdpur400._index1 = {:dmisg134.orno}
		selectdo
		selectempty
			tdpur400.odat = 0
		endselect
		
		convert.dates.to.non.utc(dmisg134.date, date.A)
		convert.dates.to.non.utc(tdpur400.odat, date.B)
		
		receipt.cycle.time = (date.A - date.B)
		
		select	dmisg135.*
		from	dmisg135
		where	dmisg135._index1 = {:dmisg134.rcno, :dmisg134.revn}
		selectdo
			initialize.values.in.arrays()
			dept.loop = 0
			
			for dept.loop = 1 to NO_OF_DEPTS 
				evaluated(1, dept.loop) = enum.descr$("tcyesno", dmisg136.cler, "2")
	
				if sent.for.eval(dept.loop) = tcyesno.yes then
| 					engg.dept = ltoe(dept.loop)			|GH611CR903.a.s
					if dept.loop = 8 or dept.loop = 9 then
						engg.dept = ltoe(dept.loop + 1)
					else
						engg.dept = ltoe(dept.loop)
					endif						|GH611CR903.a.e
					
					select	dmisg136.*
					from	dmisg136
					where	dmisg136._index1 = {
								:dmisg134.rcno, 
								:dmisg134.revn,
								:engg.dept
								}
					selectdo
						assign.the.data(dept.loop)
					endselect
				else
					evaluation.cycle.time(1, dept.loop) = "N.A"	
					evaluated(1, dept.loop) = "N.A"			
					technical.clearance(1, dept.loop) = "N.A"	
					evaluator.name(1, dept.loop) = "N.A"		
					evaluation.date(dept.loop) = 0			
				endif
			endfor
			print.detail()
		endselect	
	endselect
}

|* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

function print.detail()
{	
	
	line = concat$("	",
		sprintf$("%s", dmisg134.cprj),				|1			
		sprintf$("%s", o.vendor.name),				|2
		sprintf$("%s", trim$(dmisg134.rcno)),			|3
		sprintf$("%s", trim$(dmisg134.revn)),			|4
		sprintf$("%u(%02d/%02m/%04Y)", dmisg134.date),		|5
		sprintf$("%d", receipt.cycle.time),			|6
		sprintf$("%s", ""),					|7
		sprintf$("%s", technically.cleared.time),		|8
		sprintf$("%s", evaluation.cycle.time(1, 1)),		|9
		sprintf$("%s", evaluation.cycle.time(1, 2)),		|10
		sprintf$("%s", evaluation.cycle.time(1, 3)),		|11
		sprintf$("%s", evaluation.cycle.time(1, 4)),		|12
		sprintf$("%s", evaluation.cycle.time(1, 5)),		|13
		sprintf$("%s", evaluation.cycle.time(1, 6)),		|14
		sprintf$("%s", evaluation.cycle.time(1, 7)),		|15
		sprintf$("%s", evaluation.cycle.time(1, 8)),		|15.1		|GH611CR903.a
		sprintf$("%s", evaluation.cycle.time(1, 9)),		|15.2		|GH611CR903.a
		sprintf$("%s", specification.ID),			|16
		sprintf$("%s", specification.desc),			|17
		sprintf$("%u(%02d/%02m/%04Y)", specification.date),	|18
		sprintf$("%s", dmisg134.item),				|19
		sprintf$("%s", lot.item.desc),				|20
		sprintf$("%s", indent.no),				|21
		sprintf$("%u(%02d/%02m/%04Y)", indent.date),		|22
		sprintf$("%u(%02d/%02m/%04Y)", dmisg134.sdat),		|23
		sprintf$("%s", ""),					|24
		sprintf$("%s", enum.descr$("dmisg.receipt", dmisg134.stat, "2")),|25
		sprintf$("%u(%02d/%02m/%04Y)", status.date),		|26
		sprintf$("%s", status.user),				|27
		sprintf$("%s", status.user.name),			|28
		sprintf$("%s", evaluated(1, 1)),			|29
		sprintf$("%s", technical.clearance(1, 1)),		|20
		sprintf$("%u(%02d/%02m/%04Y)", (evaluation.date(1))),	|31
		sprintf$("%s", evaluator.name(1, 1)),			|32
		sprintf$("%s", evaluated(1, 2)),			|33
		sprintf$("%s", technical.clearance(1, 2)),		|34
		sprintf$("%u(%02d/%02m/%04Y)", evaluation.date(2)),	|35
		sprintf$("%s", evaluator.name(1, 2)),			|36
		sprintf$("%s", evaluated(1, 3)),			|37
		sprintf$("%s", technical.clearance(1, 3)),		|38
		sprintf$("%u(%02d/%02m/%04Y)", evaluation.date(3)),	|39
		sprintf$("%s", evaluator.name(1, 3)),			|40
		sprintf$("%s", evaluated(1, 4)),			|41
		sprintf$("%s", technical.clearance(1, 4)),		|42
		sprintf$("%u(%02d/%02m/%04Y)", evaluation.date(4)),	|53
		sprintf$("%s", evaluator.name(1, 4)),			|44
		sprintf$("%s", evaluated(1, 5)),			|45
		sprintf$("%s", technical.clearance(1, 5)),		|46
		sprintf$("%u(%02d/%02m/%04Y)", evaluation.date(5)),	|47
		sprintf$("%s", evaluator.name(1, 5)),			|48
		sprintf$("%s", evaluated(1, 6)),			|49
		sprintf$("%s", technical.clearance(1, 6)),		|50
		sprintf$("%u(%02d/%02m/%04Y)", evaluation.date(6)),	|51
		sprintf$("%s", evaluator.name(1, 6)),			|52
		sprintf$("%s", evaluated(1, 7)),			|53
		sprintf$("%s", technical.clearance(1, 7)),		|54
		sprintf$("%u(%02d/%02m/%04Y)", evaluation.date(7)),	|55
		sprintf$("%s", evaluator.name(1, 7)),			|56
		sprintf$("%s", evaluated(1, 8)),			|57		|GH611CR903.a.s
		sprintf$("%s", technical.clearance(1, 8)),		|58
		sprintf$("%u(%02d/%02m/%04Y)", evaluation.date(8)),	|59
		sprintf$("%s", evaluator.name(1, 8)),			|60
		sprintf$("%s", evaluated(1, 9)),			|61
		sprintf$("%s", technical.clearance(1, 9)),		|62
		sprintf$("%u(%02d/%02m/%04Y)", evaluation.date(9)),	|63
		sprintf$("%s", evaluator.name(1, 9)),			|64		|GH611CR903.a.e
		sprintf$("%s", ""),					|65
		sprintf$("%s", ""),					|66
		sprintf$(""))						|Last

		line = strip$(shiftl$(line))
		
		seq.puts(line, stat.fp)
}

function initialize.values.in.arrays()
{
	long k
	
	for k = 1 to 10 
		evaluation.cycle.time(1, k) = ""
		evaluated(1, k) = ""
		technical.clearance(1, k) = ""
	endfor
}


function assign.the.data(long i.dept.loop)
{
	domain	tcdate	rev.0.date
	
	get.receipt.status.data()
	technical.clearance(1, i.dept.loop) = enum.descr$("tcyesno", dmisg136.cler, "2")
	evaluation.date(i.dept.loop) = dmisg136.sudt
	tcisgdll0100.get.user.name(dmisg136.suby, evaluator.name(1, i.dept.loop))
	
	if dmisg134.stat = dmisg.receipt.cleared then
		select	dmisg134.date:rev.0.date
		from	dmisg134
		where	dmisg134._index1 = {:dmisg134.rcno, "00"}
		as set with 1 rows
		selectdo
		selectempty
			rev.0.date = 0
		endselect
		
		convert.dates.to.non.utc(dmisg134.adat, date.A)
		convert.dates.to.non.utc(rev.0.date, date.B)
		
		technically.cleared.time = str$(date.A - date.B) 
		
		if dmisg136.cler = tcyesno.yes then
			convert.dates.to.non.utc(dmisg136.date, date.A)
			convert.dates.to.non.utc(dmisg134.date, date.B)
			evaluation.cycle.time(1, i.dept.loop) = str$(date.A - date.B)
		else
			evaluation.cycle.time(1, i.dept.loop) = "NTC"
		endif
	else
		technically.cleared.time = "Pending for Clearance"
		evaluation.cycle.time(1, i.dept.loop) = "NTC"
	endif	
}


function get.indent.and.PO.data()
{		
	indent.no = dmisg134.rqno
	
	select	tdpur200.rdat:indent.date
	from	tdpur200
	where	tdpur200._index1 = {:indent.no}
	selectdo
	selectempty
		indent.date = 0
	endselect
}

function get.linked.specification.data()
{
	select	dmisg005.docn:specification.ID
	from	dmisg005
	where	dmisg005._index1 = {:dmisg134.cprj}
	and	dmisg005.loti = :dmisg134.item
	selectdo
	selectempty
		specification.ID = ""
	endselect
	
	select	dmisg121.dsca:specification.desc,
		dmisg121.acdt:specification.date
	from	dmisg121
	where	dmisg121._index1 = {:specification.ID, "00"}
	selectdo
	selectempty
		specification.desc = ""
		specification.date = 0
	endselect
}


function get.receipt.status.data()
{	
	status.date = 0
	status.user = ""
	
	on case dmisg134.stat
	case dmisg.receipt.submitted:
		status.date = dmisg134.date
		status.user = dmisg134.user
		break
		
	case dmisg.receipt.evaluation:
		status.date = dmisg134.sdat
		status.user = dmisg134.suer
		break
		
	case dmisg.receipt.cleared:
	case dmisg.receipt.comment:
		status.date = dmisg134.adat
		status.user = dmisg134.appr
		break
		
	case dmisg.receipt.issued:
		select	dmisg131.isdt:status.date,
		 	dmisg131.isby:status.user
		from	dmisg131
		where	dmisg131._index1 = {:dmisg134.trno}
		selectdo
		selectempty
			status.date = 0
			status.user = ""
		endselect
	endcase
	
	tcisgdll0100.get.user.name(status.user, status.user.name)
}

function convert.dates
	(
		domain	tfgld.date	i.date,
		domain	tcmcs.long 	i.hh,
		domain	tcmcs.long 	i.mm,
		domain	tcmcs.long 	i.ss,
	ref	domain 	tcdate		o.date
	)
{
	domain	tcmcs.long	yr, mnth, day
	
	yr = 0
	mnth = 0
	day = 0
	
	num.to.date(i.date, yr, mnth, day)
	o.date = date.to.utc(yr, mnth, day, i.hh , i.mm, i.ss)
}


function convert.dates.to.non.utc
			(
				domain	tcdate		i.date,
			ref	domain	tfgld.date	o.ref.date
			)
{
	long yy, mm, dd, hh, mn, ss
	
	o.ref.date = 0
	
	yy = 0	|* Year
	mm = 0	|* Month
	dd = 0	|* Day
	hh = 0	|* Hous
	mn = 0	|* Minutes
	ss = 0	|* Seconds
	
	utc.to.date(i.date, yy, mm, dd, hh, mn, ss)
	o.ref.date = date.to.num(yy, mm, dd)
}


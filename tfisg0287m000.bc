|******************************************************************************
|* tfisg0113m001  0  VRC B61U a7 isg 
|* Upload Cashflow Data
|* Ravi Kumar                    
|* 2019-12-02
|******************************************************************************
|* Main table tfisg013 Cashflow Project Description, Form Type 4
|******************************************************************************
                                                                                
|****************************** declaration section ***************************
declaration:

  table   ttpisg087 | Cashflow Project Description
  table	ttpisg088
  table	ttppdm600

   extern  domain  tccprj           cprj    fixed
   extern	domain	tcmcs.str20	cacp
   extern	domain	tcmcs.str60	dsca
   extern	domain	tpisg.domex	area
   extern	long	area.num, ptyp
   extern	domain	tcmcs.str100	file
   
	long			server_file_ptr
	string			tmp.record(2048)
	extern domain	tcmcs.str50	server.tmp.file, local.filename, error.message
	long			ret.num
	extern	domain	tcorno	ccod
	extern	domain	tpisg.ptyp	ptyp.enum
	extern	domain	tcemm.grid	eunt
	
   
   #pragma used dll ottdllbw
#include <bic_dam> 
#pragma used dll ottstpapihand

|****************************** program section ********************************


|****************************** group section **********************************
| choice.print.data:
| on.choice:
|    if rprt_open() then
|        print.error.report()
|        rprt_close()
|    else
|        choice.again()
|    endif

group.1:
init.group:
   get.screen.defaults()


functions:
function extern search()
{
	mess("tigenstring", 0, "Select The File . . .")
	if seq.open.dialog.local( false, "", "","Text Files(*.csv)|*.csv|All File(*.*)|*.*", file ) > 0 then
		mess("tigenstring", 0, "Success . . .")
	else
		mess("tigenstring", 0, "Cancelled . . .")
	endif
	display.all()
}

function extern process()
{
	display.all()
| 	
	if isspace(file) then
		mess("tigenstring", 1, "Please Specify a File")
		return
	else
		start.upload.file()
	endif
}

function extern start.upload.file()
{
| 	local.filename = _get.local.filename()
| 	server.tmp.file = dte$()&"_"&local.filename
	server.tmp.file = creat.tmp.file$( bse.tmp.dir$() )
	
	if client2server(file, server.tmp.file, false, false, true) = 0 then
		mess("tigenstring", 0, "File Copy to server:tmp successful !")
		server_file_ptr = seq.open(server.tmp.file, "r")
		
		if server_file_ptr then
			mess("tigenstring", 0, "Update in Progress . . .")
			update.receipt.data()
			seq.close(server_file_ptr)
			mess("tigenstring", 0, "Update Complete !!")
		else
			mess("tigenstring", 1, "ERROR : File Cannot be Opened")
		endif
	else
		mess("tigenstring", 1, "ERROR : Copy Failed")
	endif
}


function init.all.variables()
{

	cprj = ""
	ccod = ""
	ptyp = 0
	tmp.record = ""
	ptyp.enum = empty
}

function update.receipt.data()
{
	while(not seq.eof(server_file_ptr))
		mess("tigenstring", 0, "Update in Progress . . .")		
		init.all.variables()
		ret.num = seq.gets(tmp.record, 2048, server_file_ptr)
		if ret.num = 0 then
		| field sequence                            1, 2, 3, 4, 5
			ret.num = string.scan(tmp.record, "%s,%s,%d", cprj, ccod, ptyp)
			if ret.num <> 0 then
				insert.header()
				mess("tigenstring", 0, "Update in Progress . . .")
			endif
		endif
	endwhile
}

function insert.header()
{
	domain	tccom.bpid	ofbp
	ofbp = ""
	
	select	tpisg087.*
	from	tpisg087
	where	tpisg087._index1 = {:ccod}
	as set with 1 rows
	selectdo
		insert.line()
	selectempty
		select	tppdm740.ofbp:ofbp
		from	tppdm740
		where	tppdm740._index1 = {:cprj}
		as set with 1 rows
		selectdo
		selectempty
			ofbp = ""
		endselect
		
		select	tppdm600.ncmp
		from	tppdm600
		where	tppdm600._index1 = {:cprj}
		as set with 1 rows
		selectdo
			select	tcemm030.eunt:eunt
			from	tcemm030
			where	tcemm030.fcmp = {:tppdm600.ncmp}
			as set with 1 rows
			selectdo
			selectempty
				eunt = ""
			endselect
		selectempty
			eunt = ""
		endselect
		
		if dal.new.object("tpisg087") = 0 then
			dal.set.field("tpisg087.ccod", ccod)
			dal.set.field("tpisg087.cust", ofbp)
			dal.set.field("tpisg087.lddn", eunt)
			dal.set.field("tpisg087.lddl", "")
			if dal.save.object("tpisg087") = 0 then
				commit.transaction()
				insert.line()
			else
				abort.transaction()
				dal.get.error.message(error.message)
				message(error.message)
			endif
		endif
	endselect		
}


function insert.line()
{	
	ptyp.enum = ltoe(ptyp)
	
	select	tpisg088.*
	from	tpisg088
	where	tpisg088._index1 = {:ccod, :ptyp.enum, :cprj}
	as set with 1 rows
	selectdo
	selectempty
		if dal.new.object("tpisg088") = 0 then
			dal.set.field("tpisg088.ccod", ccod)
			dal.set.field("tpisg088.ptyp", ptyp.enum)
			dal.set.field("tpisg088.cprj", cprj)
			if dal.save.object("tpisg088") = 0 then
				commit.transaction()
			else
				abort.transaction()
				dal.get.error.message(error.message)
				message(error.message)
			endif
		endif
	endselect
}


|******************************************************************************
|* tdisg4400m200  0  VRC B61U a7 live
|* Ordering Report for PO and saving
|* Installation user             
|* 2014-11-03
|******************************************************************************
|* Main table tdpur400 Purchase Orders, Form Type 4
|******************************************************************************
|* ID: ISGECDV001136, IT0327, Ritu Shrivastava, Dt. 03-11-2014, VRC B61U a7 live
|* New Report
|*
|* ISGECDV001177, IT0205, Arjit Gupta, VRC B61U a7 live, Dt. 24-11-2014
|* Add filter Purchase Office, Add column PO Basic Amnt, PO Type
|* 
|* ISGEC001172, IT0205, Arjit Gupta, VRC B61U a7 isg, Dt. 29-11-2014
|* PO must be picked of Rev. 0 and PR Value to be initialize PO wise.
|
|* ISGEC015087, IT0302, Ankit Sharma, Dt. 25-02-2016, VRC B61U a7 live
|* Added currency column in field
|*
|* ID:ISGEC001230,Manav Singh,14-12-2018,VRC B61U a7 live
|* Ordering report for PO and Saving" session tdisg4400m200 is 
|* taking Rejected PO also as Approved PO
|*
|* ID:ISGEC01151,Anadi shankar,04-01-2019,VRC B61U a7 live
|* Modifications
|* 
|* ID: ISGEC01167,Bharti Garg, 22-03-2019
|* Added a field in selection
|****************************** declaration section ***************************
declaration:

  table   ttdpur400 | Purchase Orders
  table   ttdpur401
  table   ttdpur402
  table   ttdpur201
  table   ttdmsl400
  table   ttccom120
  table   ttccom100
  table   ttccom001
  table   ttppdm600
  table   ttcmcs052
  table	   ttctax941
  table	   ttcmcs036
  table   ttclct200
  table   ttcemm124
  table	   ttcmcs041				|#ISGEC01151.n
  
	table	ttdpur451			|#ISGECDV001177.n

|*************** Form Variables **********************
   extern  domain  tcorno           orno.f    fixed
   extern  domain  tcorno           orno.t    fixed
   extern  domain  tcemno	     buyr.f
   extern  domain  tcemno	     buyr.t
   extern  domain  tccprj           cprj.f
   extern  domain  tccprj           cprj.t
   extern  domain  tccom.bpid       otbp.f
   extern  domain  tccom.bpid       otbp.t
   extern  domain  tcdate           apdt.f
   extern  domain  tcdate           apdt.t
   extern  domain  tcyesno  txta.txt
   extern  domain  tcyesno  txtb.txt
	extern	domain	tccwoc		cwoc.f			|#ISGECDV001177.n
	extern	domain	tccwoc		cwoc.t			|#ISGECDV001177.n

|*************** File Handling Variables *******************
	string		err.file(100), file.name(100), line1(2000), line2(2000), line3(2000), line4(2000)
	long		ret2, stat.fp

|************** File Utilization Variables *************************
	extern	domain	tcorno		rep.orno	
	extern	domain	tcdate		rep.odat	
	extern	domain	tcdate		rep.apdt	
	extern	domain	tcrefa		rep.refa	
	extern	domain	tccom.bpid	rep.otbp
	extern	domain	tcnama		rep.nama
	extern domain  tccprj          rep.cprj
	extern domain  tcdsca          rep.dsca
	extern domain  tcdate          rep.ddta
	extern	domain	tcamnt		rep.rqmt	
	extern	domain	tcamnt		rep.rqvl
	extern	domain	tcamnt		rep.pbmt			|#ISGECDV001177.n	
	extern	domain	tcamnt		rep.pbvl			|#ISGECDV001177.n	
	extern	domain	tcamnt		rep.pomt	
	extern	domain	tcamnt		rep.povl	
	extern	domain	tcamnt		rep.samt	
	extern	domain	tcamnt		rep.sval	
	extern	domain	tcamnt		rep.sper
	extern	domain	tcemno		rep.buyr		
	extern	domain	tcnama		rep.bynm
	extern domain  tcmcs.str15     rep.hdst
	extern	domain	tcpric		rep.oprc
	extern domain  tcamnt          rep.oexc
	extern domain  tcamnt          rep.ocst
	extern domain  tcamnt          rep.lcst
	extern	domain	tcamnt		rep.obsc
	extern domain  tcamnt          rep.ostx
	extern domain  tcemm.grid      rep.grid
	extern	domain	tcdsca		rep.potp			|#ISGECDV001177.n
	extern	domain	tcmcs.str10	rep.ipdt			|#ISGECDV001177.n
|************** Script Variables ******************
		domain	tcdate		v.apdt.f
		domain	tcdate		v.apdt.t
		domain	tcamnt		scr.bval	
		domain	tcqiv1		scr.oqnt		
		domain	tcamnt		scr.oexc		
		domain	tcamnt		scr.ocst		
		domain	tcamnt		scr.ostx		
		domain	tcamnt		scr.lcst		
		domain	tcamnt	        ind.exc
		domain	tcamnt	        ind.stx
		domain	tcamnt	        ind.oth1
		domain	tcamnt	        ind.oth2
		domain	tcamnt	        ind.bas
		domain  tcorno         prev.orno
		domain  tcorno         prev.orno1
		domain	tcamnt		scr.pbmt
		domain	tcamnt		scr.pbvl
		extern	domain	tcccur		rep.ccur		|#ISGEC015087.n
|*************** Include **************
#include<bic_desktop>
|****************************** program section ********************************


|****************************** group section **********************************

group.1:
init.group:
   get.screen.defaults()

|****************************** choice section ********************************

choice.cont.process:
on.choice:
	generate.file()


|****************************** field section *********************************

field.orno.f:
when.field.changes:
   orno.t = orno.f

field.buyr.f:
when.field.changes:
   buyr.t = buyr.f
   
field.otbp.f:
when.field.changes:
   otbp.t = otbp.f
   
field.cprj.f:
when.field.changes:
   cprj.t = cprj.f
   
field.apdt.f:
when.field.changes:
   apdt.t = apdt.f

						|#ISGECDV001177.sn
field.cwoc.f:
when.field.changes:
	cwoc.t = cwoc.f
						|#ISGECDV001177.en
|****************************** function section ******************************
functions:

function generate.file()
{
	open.file()
	generate.header()
	read.main.table()
	close.file()
}

function open.file()
{
	err.file = bse.tmp.dir$() & str$(utc.num()) & ".xls"
	
	file.name = "c:\temp\" & str$(utc.num())  & ".xls"
	
	stat.fp = seq.open(err.file, "w+")
	if stat.fp >0 then
 		seq.seek(0, 0, stat.fp)
	endif
}

function generate.header()
{
	line1 = concat$("	",
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$("PO No. From"),
		sprintf$("%s", orno.f),
		sprintf$("To"),
		sprintf$("%s", orno.t))
	line1 = strip$(shiftl$(line1))		
	seq.puts(line1, stat.fp)

	line1 = concat$("	",
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$("Buyer From"),
		sprintf$("%s", buyr.f),
		sprintf$("To"),
		sprintf$("%s", buyr.t))
	line1 = strip$(shiftl$(line1))		
	seq.puts(line1, stat.fp)
	
	line1 = concat$("	",
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$("Project From"),
		sprintf$("%s", cprj.f),
		sprintf$("To"),
		sprintf$("%s", cprj.t))
	line1 = strip$(shiftl$(line1))		
	seq.puts(line1, stat.fp)
		
	line1 = concat$("	",
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$("Supplier From"),
		sprintf$("%s", otbp.f),
		sprintf$("To"),
		sprintf$("%s", otbp.t))
	line1 = strip$(shiftl$(line1))		
	seq.puts(line1, stat.fp)
	
	line1 = concat$("	",
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$("Period From"),
		sprintf$("%u(%2d-%2m-%4Y)", apdt.f),
		sprintf$("To"),
		sprintf$("%u(%2d-%2m-%4Y)", apdt.t))
	line1 = strip$(shiftl$(line1))		
	seq.puts(line1, stat.fp)
	
							|#ISGECDV001177.sn
	line1 = concat$("	",
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$(" "),
		sprintf$("Purchase Office From"),
		sprintf$("%s", cwoc.f),
		sprintf$("To"),
		sprintf$("%s", cwoc.t))
	line1 = strip$(shiftl$(line1))		
	seq.puts(line1, stat.fp)
							|#ISGECDV001177.en
	
	line2 =	concat$("	",
		sprintf$("Division"),					|1		
		sprintf$("PO No"),					|2		
		sprintf$("PO Creation Date"),				|3
		sprintf$("Currency"),					|3.1		|#ISGEC015087.n		
		sprintf$("PO Approval Date"),				|4		
		sprintf$("Item Description"),				|5		
		sprintf$("Supplier Code"),				|6
		sprintf$("Supplier Name"),				|7		
		sprintf$("Project Code"),				|8		
		sprintf$("Project Name"),				|9
		sprintf$("Planned Receipt Date"),			|10	
		sprintf$("Requisition Amount"),			|11
		sprintf$("Requisition Value In Rs. Lacs"),		|12
		sprintf$("PO Basic Amount"),				|13		|#ISGECDV001177.n
		sprintf$("PO Basic Amount in Lacs(Rs.)"),		|14		|#ISGECDV001177.n
		sprintf$("PO Gross Amount(Rs.)"),			|15
		sprintf$("PO Gross Amount in Lacs(Rs.)"),		|16
		sprintf$("Saving(Rs.)"),				|17
		sprintf$("Saving Value in Rs. Lacs"),			|18
		sprintf$("Saving %"),					|19
		sprintf$("Buyer Code"),				|20
		sprintf$("Buyer Name"),				|21
		sprintf$("PO Header Status"),				|22
		sprintf$("PO Type"),					|23		|#ISGECDV001177.n
		sprintf$("PO send for Approval Date"),			|24		|#ISGECDV001177.n
		sprintf$("Delivery Terms"))				|25		|#ISGEC01151.n
	line2 = strip$(shiftl$(line2))		
	seq.puts(line2, stat.fp)	
}

function read.main.table()
{
	get.date.utc(apdt.f, 00, 00, 00, v.apdt.f)
	get.date.utc(apdt.t, 23, 59, 59, v.apdt.t)
	
	select tdpur400.orno
	from   tdpur400, tdpur401, tdmsl400
	where  tdpur400._index1 inrange {:orno.f} and {:orno.t}
	and    tdpur400._index2 inrange {:otbp.f} and {:otbp.t}
	and    tdpur400._index4 inrange {:buyr.f} and {:buyr.t}
	and	tdpur400._index5 inrange {:cwoc.f} and	{:cwoc.t}		|#ISGECDV001177.n
	and    tdpur401.orno = tdpur400.orno
	and    tdpur401._index5 inrange {:cprj.f} and {:cprj.t}
	and    tdmsl400.orno = tdpur400.orno
	and    tdmsl400.vrsn = 0
	and    tdmsl400.stat = tcyesno.yes					|#ISGEC001230.n
	and    tdmsl400.work = tdmsl.work.completed				|#ISGEC001230.n
	and    tdmsl400.apdt inrange {:v.apdt.f} and {:v.apdt.t}
	and    tdpur401.oltp in (1,4)
	group by tdpur400.orno
	order by tdpur400.orno
	selectdo
		initialize.variables()
		scr.oexc = 0
		scr.ocst = 0
		scr.ostx = 0
		scr.lcst = 0
		scr.oqnt = 0
		ind.bas = 0				|#ISGEC001172.n
		
		select tdpur400.*,
			tdpur401.*,
			tcmcs041.dsca,							|#ISGEC01151.n	
			tdmsl400.apdt							|#ISGEC01167.n
		from   tdpur400, tdpur401, tdmsl400,tcmcs041				|#ISGEC01151.n
		where  tdpur400._index1 = {:tdpur400.orno}
		and    tdpur400._index2 inrange {:otbp.f} and {:otbp.t}
		and    tdpur400._index4 inrange {:buyr.f} and {:buyr.t}
		and	tdpur400._index5 inrange {:cwoc.f} and	{:cwoc.t}		|#ISGECDV001177.n
		and    tdpur401.orno = tdpur400.orno
		and    tdpur401._index5 inrange {:cprj.f} and {:cprj.t}
		and    tdmsl400.orno = tdpur400.orno
		and    tdmsl400.vrsn = 0
		and    tdmsl400.stat = tcyesno.yes					|#ISGEC001230.n
		and    tdmsl400.work = tdmsl.work.completed				|#ISGEC001230.n
		and    tdmsl400.apdt inrange {:v.apdt.f} and {:v.apdt.t}
		and    tdpur401.oltp in (1,4)
		and tdpur400.cdec refers to tcmcs041					|#ISGEC01151.n
		order by tdpur400._index1
		selectdo
			
			scr.oexc = 0
			scr.ocst = 0
			scr.ostx = 0
			scr.lcst = 0
			scr.oqnt = 0
			scr.pbmt = 0
			scr.pbvl = 0
			
			rep.orno = tdpur400.orno
			rep.otbp = tdpur400.otbp
			rep.refa = tdpur400.refa
			rep.odat = tdpur400.odat
			rep.ddta = tdpur400.ddat
			rep.buyr = tdpur400.ccon
			rep.apdt = tdmsl400.apdt
			rep.ccur = tdpur400.ccur		|#ISGEC015087.n
			rep.hdst = enum.descr$("tdpur.hdst", tdpur400.hdst)
			
									|#ISGEC001172.sn
			if	Get_Max_Revision(tdpur400.orno) <> tdmsl400.vrsn	then
				if	not Get_PO_History_501()	then
					continue
				endif	
				scr.oqnt = tdpur401.qoor + get.additional.qty.from.501(tdpur401.orno, tdpur401.pono)
			else	
									|#ISGEC001172.en
				scr.oqnt = tdpur401.qoor + get.additional.qty.from.401(tdpur401.orno, tdpur401.pono)
			endif							|#ISGEC001172.n
			
			if tdpur401.qoor <> 0	then
				rep.oprc = (tdpur401.oamt / tdpur401.qoor)
			else
				rep.oprc = 0
			endif	
			
			rep.obsc = rep.obsc + (scr.oqnt * rep.oprc)
			populate.tax.amounts()
			Get_Landed_Cost(tdpur401.orno, tdpur401.pono)
			rep.oexc = rep.oexc + scr.oexc
			rep.ocst = rep.ocst + scr.ocst
			rep.ostx = rep.ostx + scr.ostx
			rep.lcst = rep.lcst + scr.lcst
			rep.pomt = rep.obsc + rep.oexc + rep.ocst + rep.ostx + rep.lcst				|#ISGEC015087.o	
			rep.pomt = (rep.obsc + rep.oexc + rep.ocst + rep.ostx + rep.lcst) * tdpur400.ratp(1)	|#ISGEC015087.n				
			
			
									|#ISGECDV001177.sn
			select	pt_tdpur094.dsca:rep.potp
			from	tdpur094 pt_tdpur094
			where	pt_tdpur094._index1 = {:tdpur400.cotp}
			selectdo
			selectempty
				rep.potp = ""
			endselect
			
			if	Get_Max_Revision(tdpur400.orno) = tdmsl400.vrsn	then
| 				rep.pbmt = rep.pbmt + tdpur401.oamt				|#ISGEC015087.so
| 				rep.pbvl = rep.pbvl + tdpur401.oamt / 100000			|#ISGEC015087.eo
				rep.pbmt = rep.pbmt + (tdpur401.oamt * tdpur400.ratp(1))		|#ISGEC015087.sn
				rep.pbvl = rep.pbvl + (tdpur401.oamt * tdpur400.ratp(1)) / 100000	|#ISGEC015087.en
			else
				Get_PO_Basic_Amount(tdpur401.orno, 0 , tdpur401.pono, tdpur401.sqnb, scr.pbmt, scr.pbvl)
| 				rep.pbmt = rep.pbmt + scr.pbmt					|#ISGEC015087.so				
| 				rep.pbvl = rep.pbvl + scr.pbvl					|#ISGEC015087.eo
				rep.pbmt = rep.pbmt + (scr.pbmt * tdpur400.ratp(1))		|#ISGEC015087.sn
				rep.pbvl = rep.pbvl + (scr.pbvl * tdpur400.ratp(1))		|#ISGEC015087.sn
			endif	
			Get_Send_For_Approval_Date(tdpur401.orno, tdpur401.pono, tdpur401.sqnb, rep.ipdt)
									|#ISGECDV001177.en
			select tccom100.nama:rep.nama
			from   tccom100
			where  tccom100._index1 = {:tdpur400.otbp}
			selectdo
			endselect
			
			select tccom001.nama:rep.bynm
			from   tccom001
			where  tccom001._index1 = {:tdpur400.ccon}
			selectdo
			endselect
			
			select tdpur402.rqno,tdpur402.rqpo,tdpur201.*
			from   tdpur402,tdpur201
			where  tdpur402._index1 = {:tdpur401.orno,:tdpur401.pono,:tdpur401.sqnb}
			and    tdpur402.cmbe refers to tdpur201
			as set with 1 rows
			selectdo
				ind.exc = 0
				ind.stx = 0
				ind.oth1 = 0
				ind.oth2 = 0
				ind.bas = ind.bas + tdpur201.oamt			
			endselect
			
			rep.rqmt = ind.bas + ind.exc + ind.stx + ind.oth1 + ind.oth2
			rep.rqvl = rep.rqmt/100000
| 			rep.samt = rep.rqmt - rep.pomt			|#ISGECDV001177.o
			rep.samt = rep.rqmt - rep.pbmt			|#ISGECDV001177.nMM		|#ISGEC015087.o
| 			rep.samt = (rep.rqmt * tdpur400.ratp(1)) - rep.pbmt				|#ISGEC015087.nMM
			rep.sval = rep.samt/100000
			rep.povl = rep.pomt/100000
			if rep.rqmt <> 0 then
				rep.sper = (rep.samt/(rep.rqmt) * 100)					|#ISGEC015087.oMM
| 				rep.sper = (rep.samt/(rep.rqmt *tdpur400.ratp(1)) ) * 100 		|#ISGEC015087.nMM
			else
				rep.sper = 0
			endif
			
			select tcemm124.grid:rep.grid
			from   tcemm124
			where  tcemm124._index2 = {:tdpur400.cofc}
			selectdo
			endselect
			
			select tdpur401.cprj
			from   tdpur401
			where  tdpur401._index1 = {:tdpur400.orno}
			and    tdpur401.pono = 1
			selectdo
				rep.cprj = tdpur401.cprj
				select tcmcs052.dsca:rep.dsca
				from   tcmcs052
				where  tcmcs052._index1 = {:tdpur401.cprj}
				selectdo
				endselect
			endselect
		selectempty
			initialize.variables()
		endselect
		
		write.data()

	endselect
}

function write.data()
{	
	line3 =	concat$("	",
		sprintf$("%s", rep.grid),				|1
		sprintf$("%s", rep.orno),				|2				
		sprintf$("%u(%02d/%02m/%04Y)", rep.odat),		|3
		sprintf$("%s", rep.ccur),				|3.1				|# ISGEC015087.n		
		sprintf$("%u(%02d/%02m/%04Y)", rep.apdt),		|4				
		sprintf$("%s", rep.refa),				|5				
		sprintf$("%s", rep.otbp),				|6				
		sprintf$("%s", rep.nama),				|7				
		sprintf$("%s", rep.cprj),				|8				
		sprintf$("%s", rep.dsca),				|9				
		sprintf$("%u(%02d/%02m/%04Y)", rep.ddta),		|10				
		sprintf$("%f", rep.rqmt),				|11
		sprintf$("%f", rep.rqvl),				|12
		sprintf$("%f", rep.pbmt),				|13			|#ISGECDV001177.n
		sprintf$("%f", rep.pbvl),				|14			|#ISGECDV001177.n
		sprintf$("%f", rep.pomt),				|15
		sprintf$("%f", rep.povl),				|16
		sprintf$("%f", rep.samt),				|17
		sprintf$("%f", rep.sval),				|18
		sprintf$("%f", rep.sper),				|19
		sprintf$("%s", rep.buyr),				|20
		sprintf$("%s", rep.bynm),				|21
		sprintf$("%s", rep.hdst),				|22
		sprintf$("%s", rep.potp),				|23			|#ISGECDV001177.n
		sprintf$("%s", rep.ipdt),				|24			|#ISGECDV001177.n
		sprintf$("%s", tcmcs041.dsca))				|25			|#ISGEC01151.n
	line3 = strip$(shiftl$(line3))
		seq.puts(line3, stat.fp)
}	

function close.file()
{
	seq.close(stat.fp)
	ret2 = server2client(err.file,file.name,1)
	ret2 = app_start(file.name,"c:","","","")
}

function initialize.variables()
{
	rep.orno = ""
	rep.otbp = ""
	rep.nama = ""
	rep.apdt = 0
	rep.ddta = 0
	rep.odat = 0
	rep.refa = ""
	rep.cprj = ""
	rep.dsca = ""
	rep.rqmt = 0
	rep.rqvl = 0
	rep.pbmt = 0				|#ISGECDV001177.n
	rep.pbvl = 0				|#ISGECDV001177.n
	rep.pomt = 0
	rep.povl = 0
	rep.samt = 0
	rep.sval = 0
	rep.sper = 0
	rep.obsc = 0
	rep.oprc = 0
	rep.oexc = 0
	rep.ocst = 0
	rep.ostx = 0
	rep.lcst = 0
	rep.buyr = ""
	rep.bynm = ""
	rep.hdst = ""
	rep.potp = ""					|#ISGECDV001177.n
	rep.ipdt = ""					|#ISGECDV001177.n
	rep.ccur = ""				|7.1				|#ISGEC015087.n
}

function domain tcqiv1	 get.additional.qty.from.401(
				domain	tcorno	in.orno,
				domain	tcpono	in.pono
					)
{
	domain	tcqiv1 ord.qty, app.qty, diff.qty, bac.qty
	ord.qty = 0
	app.qty = 0
	bac.qty = 0
	diff.qty = 0
	select	q_tdpur401.qoor:ord.qty,
		q_tdpur401.qiap:app.qty,
		q_tdpur401.qibo:bac.qty
	from	tdpur401 q_tdpur401
	where	q_tdpur401._index1 = {:in.orno, :in.pono}
	and	q_tdpur401.oltp = tdgen.oltp.total
	selectdo
		diff.qty = diff.qty + bac.qty - (ord.qty - app.qty)
	endselect
	return(diff.qty)
}

							|#ISGEC001172.sn
function domain tcqiv1	 get.additional.qty.from.501(
				domain	tcorno	in.orno,
				domain	tcpono	in.pono
					)
{
	domain	tcqiv1 ord.qty, app.qty, diff.qty, bac.qty
	ord.qty = 0
	app.qty = 0
	bac.qty = 0
	diff.qty = 0
	select	q_tdmsl501.qoor:ord.qty,
		q_tdmsl501.qiap:app.qty,
		q_tdmsl501.qibo:bac.qty
	from	tdmsl501 q_tdmsl501
	where	q_tdmsl501._index1 = {:in.orno, 0, :in.pono}
	and	q_tdmsl501.oltp = tdgen.oltp.total
	selectdo
		diff.qty = diff.qty + bac.qty - (ord.qty - app.qty)
	endselect
	return(diff.qty)
}	
							|#ISGEC001172.en

function populate.tax.amounts()
{
	domain	tcamnt		line.tax.amount.array(1, 1)	based
	domain	tcamnt		line.base.amount.array(1, 1)	based
	domain	tccvat		line.tax.code.array(1, 1)	based
	domain	tctax.indt.l	o.line.indirect.tax.array(1, 1)	based
	domain	tctax.indt.l	i.duty.type.array(1, 1)		based
	domain	tcyesno		expensed.tax(1, 1)		based
	domain	tcmcs.s250m	o.comb.output.set(1)		based
	domain	tcpvat		o.tax.rates(1,1)		based
	domain	tcamnt		i.duties.array(1,1)		based
	domain tctax.seqn 	o.maximum.sequence.number
	domain tcamnt 		o.claimable.tax.amount
	domain tcamnt 		o.non.claimable.tax.amount
	domain tcamnt 		o.total.tax.amount
	domain tcamnt 		o.sales.tax
	domain tcmcs.s250m 	o.error.msg mb
	
	
	tctax.dll9141.determine.aggregate.tax.amounts.per.line( 
						tdpur401.ccty,				| domain tcccty i.tax.country, 
						tdpur401.cvat,				| domain tccvat i.aggregate.tax.code, 
						tdpur401.odat,				| domain tcdate i.tax.date, 
						tdpur401.oamt,				| domain tcamnt i.order.line.price, 
						tdpur401.cuva,				| domain tcamnt i.customs.value, 
						tdpur401.mrpi.l,			| domain tcamnt i.market.retail.price, 
						0.00,					| domain tcamnt i.retail.sales.price, 
						0.00,					| domain tcamnt i.tariff.price, 
						tdpur400.ccur,				| domain tcccur i.price.currency, 
						tdpur400.ratd,				| domain tcdate i.currency.rate.date, 
						tdpur400.ratt,				| domain tcrtyp i.currency.exchange.rate.type, 
						tdpur401.tase.l,			| domain tcpric i.asv.excise, 
						tdpur401.tasv.l,			| domain tcpric i.asv.vat, 
						tdpur401.tass.l,			| domain tcpric i.asv.service.tax, 
						tdpur401.tasg.l,			| domain tcpric i.asv.gst, 
						tdpur401.ddta,				| domain tcdate i.ship.or.receive.date, 
						tcyesno.no,				| domain tcyesno i.as.is.sales, 
						tcyesno.no,				| domain tcyesno i.yesno.flag, 
						i.duty.type.array,			| ref domain tctax.indt.l i.duty.type.array(,), 
						i.duties.array,				| ref domain tcamnt i.duties.array(,), 
						o.tax.rates,				| ref domain tcpvat o.tax.rates(,), 
						o.maximum.sequence.number,		| ref domain tctax.seqn o.maximum.sequence.number, 
						line.base.amount.array,			| ref domain tcamnt o.line.base.amount.array(,), 
						line.tax.amount.array,			| ref domain tcamnt o.line.tax.amount.array(,), 
						line.tax.code.array,			| ref domain tccvat o.line.tax.code.array(,) fixed, 
						o.line.indirect.tax.array,		| ref domain tctax.indt.l o.line.indirect.tax.array(,), 
						expensed.tax,				| ref domain tcyesno o.expensed.tax(,), 
						o.claimable.tax.amount,			| ref domain tcamnt o.claimable.tax.amount, 
						o.non.claimable.tax.amount,		| ref domain tcamnt o.non.claimable.tax.amount, 
						o.total.tax.amount,			| ref domain tcamnt o.total.tax.amount, 
						o.sales.tax,				| ref domain tcamnt o.sales.tax, 
						o.comb.output.set,			| ref domain tcmcs.s250m o.comb.output.set() fixed mb, 
						o.error.msg)				| ref domain tcmcs.s250m o.error.msg mb )
	select	tctax941.*,tcmcs036.*
	from	tctax941,tcmcs036
	where	tctax941._index1 = {:tdpur401.ccty, :tdpur401.cvat}
	and	tctax941.type = 20		|tctax.type.l.tax
	and	tctax941.cmbb refers to tcmcs036
	selectdo
		on case tcmcs036.indt.l
			case	tctax.indt.l.bed:
			case	tctax.indt.l.service:			
			case	tctax.indt.l.aed:	
			case	tctax.indt.l.sed:	
			case	tctax.indt.l.e.cess.excise:	
			case	tctax.indt.l.e.cess.service:			
			case	tctax.indt.l.hse.cess.excise:
			case	tctax.indt.l.hse.cess.servic:		
				scr.oexc = scr.oexc + line.tax.amount.array(tctax941.layr,tctax941.seqn)
				break
			case	tctax.indt.l.lst:	
			case	tctax.indt.l.cst:	
			case	tctax.indt.l.vat:	
			case	tctax.indt.l.n.a:
				scr.ocst = scr.ocst + line.tax.amount.array(tctax941.layr,tctax941.seqn)
				break	
			default:
				scr.ostx = scr.ostx + line.tax.amount.array(tctax941.layr,tctax941.seqn)
		endcase
	endselect
}


function Get_Landed_Cost(domain tcorno		im.orno,
			domain	tcpono		im.pono)
{
	domain	tcpono	tmp.sqnb
	string	tmp.borf(5)
	
	select	a_tdpur401.sqnb:tmp.sqnb 
	from	tdpur401 a_tdpur401
	where	a_tdpur401._index1 = {:im.orno, :im.pono}
	and	a_tdpur401.oltp in (2,3,4)
	selectdo
		tmp.borf = str$(im.pono)&"/"&str$(tmp.sqnb)
		select	tclct200.* from tclct200
		where	tclct200._index1 = {2}
		and	tclct200.bobj = :im.orno
		and	tclct200.borf = :tmp.borf
		order by tclct200._index1
		selectdo
			scr.lcst = scr.lcst + tclct200.lcam
		endselect
	endselect	
}

function get.date.utc(
		domain	tcdate	i.date,
		long		i.hh,
		long		i.mn,
		long		i.ss,
	ref	domain	tcdate	o.date
			)
{
	long	dd, mm, yy, hh, mn, ss
	
	utc.to.date(i.date, yy , mm, dd, hh, mn, ss)
	o.date = date.to.utc(yy, mm, dd, i.hh, i.mn, i.ss)
	if	o.date < 0	then
		o.date = 0
	endif
}	

					|#ISGECDV001177.sn
function domain tcpono Get_Max_Revision
				(
				domain	tcorno	in.orno
				)
{
	domain	tcpono	max.vrsn
	
	max.vrsn = 0
	select	max(ap_tdmsl400.vrsn):max.vrsn
	from	tdmsl400 ap_tdmsl400
	where	ap_tdmsl400._index1 = {:in.orno}
	selectdo
	endselect
	
	return(max.vrsn)
}

function Get_PO_Basic_Amount
			(
			domain	tcorno	in.orno,
			domain	tcpono	in.vrsn,
			domain	tcpono	in.pono,
			domain	tcpono	in.sqnb,
		ref	domain	tcamnt	ot.amnt,
		ref	domain	tcamnt	ot.lamt
			)
{
	ot.amnt = 0
	ot.lamt = 0
	
	select	od_tdmsl501.oamt:ot.amnt
	from	tdmsl501 od_tdmsl501
	where	od_tdmsl501._index1 = {:in.orno, :in.vrsn, :in.pono}
	and	od_tdmsl501.oltp in (tdgen.oltp.total, tdgen.oltp.orderline)
	selectdo
	endselect
	
	ot.lamt = ot.amnt / 100000
}

function Get_Send_For_Approval_Date(
				domain	tcorno		in.orno,
				domain	tcpono		in.pono,
				domain	tcpono		in.sqnb,
			ref	domain	tcmcs.str10	ot.ipdt
				)
{
	ot.ipdt = ""
	
	select	tdpur451.trdt
	from	tdpur451
	where	tdpur451._index1 = {:in.orno, :in.pono, :in.sqnb}
	and	tdpur451.appr = tcyesno.yes
	as set with 1 rows
	selectdo
	selectempty
		tdpur451.trdt = 0
	endselect
	if	tdpur451.trdt <> 0	then
		ot.ipdt = sprintf$("%u(%02m/%02d/%04Y)", tdpur451.trdt)
	endif
}
					|#ISGECDV001177.en
					
					|#ISGEC001172.sn
function boolean Get_PO_History_501()
{
	select	tdmsl501.qoor:tdpur401.qoor,
		tdmsl501.oamt:tdpur401.oamt,
		tdmsl501.ccty:tdpur401.ccty,
		tdmsl501.cvat:tdpur401.cvat,
		tdmsl501.odat:tdpur401.odat,
		tdmsl501.cuva:tdpur401.cuva,
		tdmsl501.mrpi.l:tdpur401.mrpi.l,
		tdmsl501.tase.l:tdpur401.tase.l,
		tdmsl501.tasv.l:tdpur401.tasv.l,
		tdmsl501.tass.l:tdpur401.tass.l,
		tdmsl501.ddta:tdpur401.ddta
	from	tdmsl501
	where	tdmsl501._index1 = {:tdpur401.orno, 0, :tdpur401.pono}
	as set with 1 rows
	selectdo
		return(true)
	endselect
	
	return(False)
}
					|#ISGEC001172.en

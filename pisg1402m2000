|******************************************************************************
|* tfisg1402m200  0  VRC B61U a7 isg 
|* Finalized Transactions By Ledger Compressed
|* Installation user             
|* 2014-10-08
|******************************************************************************
|* Form Type 4
|******************************************************************************
|* ID ISGEC001078,	Sujeet Kumar,		2014-10-08
|* Functional: Mr. Deepak Rawat
|****************************** declaration section ***************************
declaration:
	table	ttfgld106
	table	ttfgld011
	table	ttfgld008
	table	ttfgld018

|****************************** Form Field Variables ***************************

	extern	domain	tfgld.year	fyer.f
	extern	domain	tfgld.prod	prod.f
	extern	domain	tfgld.prod	prod.t
	extern	domain	tfgld.leac	leac.f
	extern	domain	tfgld.leac	leac.t
	extern	domain	tfgld.btno	btno.f
	extern	domain	tfgld.btno	btno.t
	extern	domain	tfgld.ttyp	ttyp.f
	extern	domain	tfgld.ttyp	ttyp.t
	extern	domain	tfgld.docn	docn.f
	extern	domain	tfgld.docn	docn.t
|********** Report Variables ***************************************************	
	extern 	domain 	tcncmp		rep.company
	extern 	domain 	tcncmp		rep.target.comp
	extern	domain	tfgld.year	rep.fiscal.year
	extern 	domain	tfgld.prod	rep.fiscal.prod
	extern	domain	tfgld.year	rep.tax.year
	extern	domain	tfgld.prod	rep.tax.prod
	extern 	domain	tfgld.btno	rep.batch
	extern	domain	tfgld.ttyp	rep.ttyp
	extern	domain	tfgld.docn	rep.docn
	extern	domain	tfgld.date	rep.dcdt
	extern	domain	tfgld.leac	rep.ledger
	extern	domain	tfgld.desc	rep.ledger.dsca
	extern	domain	tfgld.dimx	rep.dimension1
	extern	domain	tfgld.desc	rep.dim1.dsca
	extern	domain	tfgld.dimx	rep.dimension2
	extern	domain	tfgld.desc	rep.dim2.dsca
	extern	domain	tfgld.dimx	rep.dimension3
	extern	domain	tfgld.desc	rep.dim3.dsca
	extern	domain	tfgld.dimx	rep.dimension4
	extern	domain	tfgld.desc	rep.dim4.dsca
	extern	domain	tfgld.dimx	rep.dimension5
	extern	domain	tfgld.desc	rep.dim5.dsca
	extern	domain	tfgld.dbcr	rep.dbcr
	extern	domain	tfgld.amnt	rep.amount
	extern	domain	tccom.bpid	rep.bpid
	extern	domain	tcnama		rep.bp.nama
	extern 	domain	tcnama		rep.user.name
	extern 	domain	tcnama		rep.fina.name
	extern	domain	tfgld.btno	rep.btno
	extern	domain	tfgld.amnt	rep.amth
	extern	domain	tfgld.amnt	rep.forgn.amnt
	extern	domain	tfcmg.cheq	rep.cheque
	extern	domain	tfgld.ttyp	rep.sales.ityp
	extern	domain	tfgld.docn	rep.sales.idoc
	extern	domain	tcmcs.str30	rep.tran.type
	extern	domain	tcdsca		rep.comp.dsca
	extern	domain	tccprj		rep.cprj
	extern	domain	tcorno		rep.orno
	extern	domain	tcccur		rep.ccur
	extern	domain	tcmcs.str8	rep.table
	extern	domain	tfgld.refc	rep.refr

	extern	domain	tfgld.desc	sort.field1
	extern	domain	tcmcs.str70	sort.field2
	extern	domain	tfgld.leac	sort.field3
	
	extern	domain	tfgld.amnt		opening.balance
	extern	long 				rep.print
	
	#include "itfisgdll0000"

|****************************** program section ********************************


|****************************** group section **********************************

group.1:
init.group:
   get.screen.defaults()

|****************************** choice section ********************************

choice.cont.process:
on.choice:
   execute(print.data)

choice.print.data:
on.choice:
   if rprt_open() then
       read.main.table()
       rprt_close()
   else
       choice.again()
   endif


|****************************** field section *********************************

field.prod.f:
when.field.changes:
	prod.t = prod.f

field.leac.f:
when.field.changes:
	leac.t = leac.f
	
field.btno.f:
when.field.changes:
	btno.t = btno.f

field.ttyp.f:
when.field.changes:
	ttyp.t = ttyp.f

field.docn.f:
when.field.changes:
	docn.t = docn.f


|****************************** function section ******************************

functions:

function read.main.table()
{
	finalized.transactions()
}

function finalized.transactions()
{
	domain	tfgld.user	o.user
	domain tcccur	o.local.currency
	domain	tfgld.amnt	op.bal,op.bal1
	domain	tfgld.prod	o.prod.f,o.prod.t
	
	rep.tran.type = "Finalized Transactions"
	rep.table = "tfgld106"
	
	
	rep.print = 0
	select 	tfgld008.leac
	from	tfgld008
	where	tfgld008._index1 inrange {:leac.f} and {:leac.t}
	selectdo
		op.bal = 0
		op.bal1 = 0
		opening.balance = 0
		
		rep.company = get.compnr()
		rep.fiscal.year = fyer.f
		rep.ledger = tfgld008.leac
		
		op.bal = 0
		op.bal1 = 0
		opening.balance = 0
		
		op.bal = tfisgdll0000.Opening_Balances_Ledger_Currency(rep.company,rep.fiscal.year,rep.ledger)
		tfisgdll0000.calculate.period.range(	tfgld.ptyp.financial,		|Period
							rep.fiscal.year,		|Fiscal Year
							prod.f,				|Fiscal Period
							o.prod.f,			|Minimum Period
							o.prod.t)			|Max Period

		if prod.f <> 0 then
			op.bal1 = tfisgdll0000.Ledger_Account_total(rep.company,tfgld.ptyp.financial,rep.fiscal.year,o.prod.f,o.prod.t,rep.ledger)
		endif
		
		opening.balance = op.bal + op.bal1
		sort.field3 = strip$(rep.ledger)
		sort.field2 = strip$(rep.ledger)
		rep.ledger.dsca = get.ledger.description(rep.ledger)		
		rprt_send()
	endselect
	
	
	
	
	
	
	select 	tfgld106.otyp,
		tfgld106.odoc,
		tfgld106.ocmp,
		tfgld106.fyer,
		tfgld106.fprd,
		tfgld106.vyer,
		tfgld106.vprd,
		tfgld106.obat,
		tfgld106.dcdt,
		tfgld106.leac,
		tfgld106.dim1,
		tfgld106.dim2,
		tfgld106.dim3,
		tfgld106.dim4,
		tfgld106.dim5,
		tfgld106.dbcr,
		tfgld106.amth,
		tfgld106.bpid,
		tfgld106.oyer,
		tfgld106.amnt,
		tfgld106.ccur,
		tfgld106.trun,
		tfgld106.refr,
		tfgld011.catg,
		tfgld106.user
	from	tfgld106,tfgld011
	where	tfgld106._index3 inrange {:leac.f,:ttyp.f,:docn.f} and {:leac.t,:ttyp.t,:docn.t}
	and	tfgld106.obat inrange :btno.f and :btno.t	
	and	tfgld106.oyer = :fyer.f
	and	tfgld106.fprd inrange :prod.f and :prod.t
	and	tfgld106.otyp refers to tfgld011
	selectdo
		select 	tfgld018.date,
			tfgld018.time
		from	tfgld018
		where	tfgld018._index1 = {:tfgld106.otyp,:tfgld106.odoc}
		as set with 1 rows
		selectdo
		endselect
		
		
		rep.print = 1
		assign.report.variables(
				tfgld106.ocmp,		|Company
				tfgld106.ocmp,		|Target Company
				tfgld106.fyer,		|Fiscal Year
				tfgld106.fprd,		|Fiscal Period
				tfgld106.vyer,		|Tax Year
				tfgld106.vprd,		|Tax Period
				tfgld106.obat,		|Batch Number
				tfgld106.otyp,		|Transaction Type
				tfgld106.odoc,		|Document Number
				tfgld106.dcdt,		|Document Date
				tfgld106.leac,		|Ledger Account
				tfgld106.dim1,		|Dimension 1
				tfgld106.dim2,		|Dimension 2
				tfgld106.dim3,		|Dimension 3
				tfgld106.dim4,		|Dimension 4
				tfgld106.dim5,		|Dimension 5
				tfgld106.dbcr,		|Debit/Credt
				tfgld106.amth(1),	|Amount	
				tfgld106.bpid,		|Business Partner
				tfgld106.refr)		|Reference
		
		rprt_send()
	endselect
}


function initilized.report.variables()
{
	
	rep.company 	= 0
	rep.target.comp = 0 
	rep.fiscal.year = 0
	rep.fiscal.prod = 0
	rep.tax.year 	= 0
	rep.tax.prod 	= 0
	rep.batch	= 0
	rep.ttyp 	= ""
	rep.docn 	= 0
	rep.dcdt 	= 0
	rep.ledger 	= ""
	rep.ledger.dsca = ""
	rep.dimension1 	= ""
	rep.dim1.dsca 	= ""
	rep.dimension2 	= ""
	rep.dim2.dsca 	= ""
	rep.dimension3	= ""
	rep.dim3.dsca 	= ""
	rep.dimension4 	= ""
	rep.dim4.dsca 	= ""
	rep.dimension5 	= ""
	rep.dim5.dsca 	= ""
	rep.amount 	= 0.0
	rep.dbcr 	= empty
	sort.field1 	= ""
	sort.field2 	= ""
	sort.field3	= ""
	rep.user.name 	= ""
	rep.cheque	= ""
	rep.btno 	= 0
	rep.amth	= 0
	rep.sales.ityp 	= ""
	rep.sales.idoc 	= 0
	rep.fina.name 	= ""
	rep.forgn.amnt	= 0 
	rep.comp.dsca	= ""
	rep.refr	= ""
| 	rep.cprj	= ""
| 	rep.orno	= ""
}

function assign.report.variables
			(
				domain	tcncmp		i.cono,		|Company
				domain	tcncmp		i.tcom,		|Target Company
				domain	tfgld.year	i.fyer,		|Fiscal Year
				domain	tfgld.prod	i.fprd,		|Fiscal Period
				domain	tfgld.year	i.vyer,		|Tax Year
				domain	tfgld.prod	i.vprd,		|Tax Period
				domain	tfgld.btno	i.btno,		|Batch Number
				domain	tfgld.ttyp	i.ttyp,		|Transaction Type
				domain	tfgld.docn	i.docn,		|Document Number
				domain	tfgld.date	i.dcdt,		|Document Date
				domain	tfgld.leac	i.leac,		|Ledger Account
				domain	tfgld.dimx	i.dim1,		|Dimension 1
				domain	tfgld.dimx	i.dim2,		|Dimension 2
				domain	tfgld.dimx	i.dim3,		|Dimension 3
				domain	tfgld.dimx	i.dim4,		|Dimension 4
				domain	tfgld.dimx	i.dim5,		|Dimension 5
				domain	tfgld.dbcr	i.dbcr,		|Debit/Credit
				domain	tfgld.amnt	i.amth,		|Amount
				domain	tccom.bpid	i.bpid,		|Business Partner
				domain	tfgld.refc	i.refr		|Reference
			)
{
	initilized.report.variables()
		
	rep.company 	= i.cono
	rep.target.comp = i.tcom
	rep.fiscal.year = i.fyer
	rep.fiscal.prod = i.fprd
	rep.tax.year 	= i.vyer
	rep.tax.prod 	= i.vprd
	rep.batch	= i.btno
	rep.ttyp 	= i.ttyp
	rep.docn 	= i.docn
	rep.dcdt 	= i.dcdt
	rep.ledger 	= i.leac
	rep.ledger.dsca = get.ledger.description(i.leac)
	rep.dimension1 	= i.dim1
	rep.dim1.dsca 	= get.dimension.description(1,i.dim1)
	rep.dimension2 	= i.dim2
	rep.dim2.dsca 	= get.dimension.description(2,i.dim2)
	rep.dimension3	= i.dim3
	rep.dim3.dsca 	= get.dimension.description(3,i.dim3)
	rep.dimension4 	= i.dim4
	rep.dim4.dsca 	= get.dimension.description(4,i.dim4)
	rep.dimension5 	= i.dim5
	rep.dim5.dsca 	= get.dimension.description(5,i.dim5)
	
	rep.dbcr 	= i.dbcr
	rep.amount 	= i.amth
	
	rep.bpid 	= i.bpid
	rep.bp.nama 	= get.bp.name(i.bpid)
	
	rep.refr 	= i.refr
	
| 	rep.user.name	= get.user.code(rep.fiscal.year,rep.batch)
	
| 	rep.cheque	= get.cheque.number(rep.ttyp,rep.docn)
	
| 	payment.or.direct.debit.batch(rep.ttyp,rep.docn,rep.btno,rep.amth)
	
| 	get.sales.invoice.document(rep.ttyp,rep.docn,rep.sales.ityp,rep.sales.idoc)
	
	sort.field1 = rep.ttyp & "/" & str$(rep.docn)
	sort.field2 = strip$(rep.ledger) & " " & strip$(rep.dimension1) & " " & strip$(rep.dimension2)
^			& " " & strip$(rep.dimension3) & " " & strip$(rep.dimension4) 
^			& " " & strip$(rep.dimension5) & " " & strip$(rep.ttyp) & " " & str$(rep.docn)

	sort.field3 = strip$(rep.ledger)
	
}


function domain tfgld.desc get.ledger.description
			(
				domain	tfgld.leac	i.leac
			)
{
	domain	tfgld.desc	o.desc
	
	select	tfgld008.desc:o.desc
	from	tfgld008
	where	tfgld008._index1 = {:i.leac}
	as set with 1 rows
	selectdo
	selectempty
		o.desc = ""
	endselect
	
	return(o.desc)
}

function domain tfgld.desc get.dimension.description
			(
				domain	tfgld.dimn	i.dtyp,		|Dimension Type
				domain	tfgld.dimx	i.dimx		|Dimension
			)
{
	domain 	tfgld.desc	o.desc
	
	select	tfgld010.desc:o.desc
	from	tfgld010
	where	tfgld010._index1 = {:i.dtyp,:i.dimx}
	as set with 1 rows
	selectdo
	selectempty
		o.desc = ""
	endselect
	
	return(o.desc)
}


function domain tcnama	get.bp.name
			(
				domain	tccom.bpid	i.bpid
			)
{
	domain	tcnama	o.nama
	
	select	tccom100.nama:o.nama
	from	tccom100
	where	tccom100._index1 = {:i.bpid}
	as set with 1 rows
	selectdo
	selectempty
		o.nama = ""
	endselect
	
	return(o.nama)
}




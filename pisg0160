|******************************************************************************
|* tdisg016  0  VRC B61U a7 isg 
|* WayBill Book DAL
|* baan
|* 15-04-24 [15:28]
|******************************************************************************
|* Script Type: DAL
|******************************************************************************
|* IDENT PATCH002035, Mani sharma Dt . 27-04-2015 , VRC B61U a7 isg
|******************************************************************************                                                                                
#include <bic_dal2>
	table	ttdisg016
	table	ttdpur400
	table	ttppdm740
	
function extern long before.open.object.set()
{
	dal.field.depends.on("tdisg016.ofbp",HOOK_UPDATE,"tdisg016.cprj")
	dal.field.depends.on("tdisg016.otbp",HOOK_UPDATE,"tdisg016.orno")
	dal.field.depends.on("tdisg016.widt",HOOK_UPDATE,"tdisg016.wayb")
	dal.field.depends.on("tdisg016.isdt",HOOK_UPDATE,"tdisg016.orno")
	dal.field.depends.on("tdisg016.stat",HOOK_UPDATE,"tdisg016.orno")
	return(0)
}

function extern long before.save.object(long type)
{
	return(0)
}


function extern boolean tdisg016.cprj.is.valid(long mode)
{
	if not isspace(tdisg016.cprj) then
		select tppdm740.cprj
		from   tppdm740
		where  tppdm740._index1 = {:tdisg016.cprj}
		selectdo
		selectempty
			dal.set.error.message("@Project is not Valid")
			return(false)
		endselect
	endif
return(true)
}

function extern boolean tdisg016.orno.is.valid(long mode)
{
	if not isspace(tdisg016.orno) then
		select tdpur401.orno
		from   tdpur401
		where  tdpur401._index5 = {:tdisg016.cprj,:tdisg016.orno}
		as set with 1 rows
		selectdo
		selectempty
			dal.set.error.message("@Order is not Valid")
			return(false)
		endselect
	endif
return(true)
}

function extern tdisg016.ofbp.update(long mode)
{
	
	select tppdm740.ofbp
	from   tppdm740
	where  tppdm740._index1 = {:tdisg016.cprj}
	selectdo
		tdisg016.ofbp = tppdm740.ofbp
	selectempty
		tdisg016.ofbp = ""
	endselect	
}


function extern  tdisg016.otbp.update(long mode)
{
	
	select tdpur400.otbp
	from   tdpur400
	where  tdpur400._index1 = {:tdisg016.orno}
	selectdo
		tdisg016.otbp = tdpur400.otbp
	selectempty
		tdisg016.otbp = ""
	endselect	
}

function extern tdisg016.widt.update(long mode)
{
	if not isspace(tdisg016.wayb) then
		tdisg016.widt = date.num()
	endif	

}

function extern tdisg016.isdt.update(long mode)
{
	if not isspace (tdisg016.orno) then
		tdisg016.isdt = date.num()
	endif	
}

function extern tdisg016.stat.update(long mode)
{
	if not isspace(tdisg016.orno) then
		if tdisg016.stat = tdwaybill.received then 
			tdisg016.stat = tdwaybill.issued 
		endif
	else
		if tdisg016.stat = tdwaybill.issued then
			tdisg016.stat = tdwaybill.received 
		endif
	endif
	
}

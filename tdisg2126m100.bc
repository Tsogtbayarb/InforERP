|******************************************************************************
|* tdisg2126m000  0  VRC B61U a7 isg 
|* Request for Supplier Debit Entry
|* Ravi Kumar                    
|* 2021-01-05
|******************************************************************************
|* Main table tdisg226 Supplier Debit, Form Type 1
|******************************************************************************
                                                                                
|****************************** declaration section ***************************
declaration:

  table   ttdisg226 | Supplier Debit
  table   ttdisg227
  table   ttccom001
  table   ttdisg228
	extern	domain	tclogn	curr.user
	extern	domain	tcnama	bpid.nama
	extern	domain	tcdsca	cprj.dsca

|****************************** program section ********************************
before.program:
	curr.user = logname$
	select	tccom001.emno
	from	tccom001
	where	tccom001._index4 = {:curr.user}
	as set with 1 rows
	selectdo
	endselect
	
	select	tdisg228.*
	from	tdisg228
	where	tdisg228._index1 = {:tccom001.emno}
	selectdo
	selectempty
		tdisg228.emno = ""
		tdisg228.amnt = 0
	endselect
	
	query.extend.where("tdisg226.amnt < " & str$(tdisg228.amnt))
	
before.display.object:
	if tdisg226.stat <> tdisg.action.sendforapp then
		disable.commands("approve", "reject")
	else
		enable.commands("approve", "reject")
	endif

|****************************** group section **********************************

choice.approve:
on.choice:
	select	tdisg226.stat
	from	tdisg226 for update
	where	tdisg226._index1 = {:tdisg226.dbno}
	as set with 1 rows
	selectdo
		tdisg226.stat = tdisg.action.approved
		tdisg226.appr = logname$
		tdisg226.apdt = utc.num()
		db.update(ttdisg226, db.retry)
		commit.transaction()
	endselect
	insert.history()
	
choice.reject:
on.choice:
	select	tdisg226.stat
	from	tdisg226 for update
	where	tdisg226._index1 = {:tdisg226.dbno}
	as set with 1 rows
	selectdo
		tdisg226.stat = tdisg.action.reject
		tdisg226.reje = logname$
		tdisg226.redt = utc.num()
		db.update(ttdisg226, db.retry)
		commit.transaction()
	endselect
	insert.history()
	
field.bpid.nama:
before.display:
	select	tccom100.nama:bpid.nama
	from	tccom100
	where	tccom100._index1 = {:tdisg226.bpid}
	as set with 1 rows
	selectdo
	selectempty
		bpid.nama = ""
	endselect
	
field.cprj.dsca:
before.display:
	select	tcmcs052.dsca:cprj.dsca
	from	tcmcs052
	where	tcmcs052._index1 = {:tdisg226.cprj}
	as set with 1 rows
	selectdo
	selectempty
		cprj.dsca = ""
	endselect
	
functions:
function insert.history()
{
	tdisg227.dbno = tdisg226.dbno
	tdisg227.stat = tdisg226.stat
	tdisg227.rmks = tdisg226.rqrk
	tdisg227.actn = logname$
	tdisg227.acon = utc.num()
	db.insert(ttdisg227, db.retry)
	commit.transaction()
}

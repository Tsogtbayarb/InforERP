|******************************************************************************
|* tiisg941026  0  VRC B61U a7 live
|* Print Relased Drawings-Total Manhours Spent
|* Tools                         
|* 2014-05-16
|******************************************************************************
|* Main table tiisg910 Hours Transaction, Form Type 4
|******************************************************************************
|* IDENT ISG001022, Kartikeya Rao, IT0177, 16-05-2014 , VRC B61U a7 live 
|****************************** declaration section ***************************
declaration:
#ident "@(#)ISG001022, Kartikeya Rao, IT0177, 16-05-2014 , VRC B61U a7 live"


	table	ttiisg910	| Hours Transactions
	table	ttcmcs052	| Projects
	table	ttpptc100	| Elements
	table	ttiisg912	| Drawing groups
	table	ttppdm911


	extern	domain	tccprj    		cprj.f	fixed
	extern	domain	tccprj    		cprj.t	fixed
	extern	domain	tppdm.cspa    		cspa.f	fixed
	extern	domain	tppdm.cspa    		cspa.t	fixed
	extern	domain	tcemno				emno.f
	extern	domain	tcemno				emno.t
	extern	domain	tcmcs.str10			grcd.f
	extern	domain	tcmcs.str10			grcd.t
	extern	domain	tcdate				tdate.f
	extern	domain	tcdate				tdate.t
	
	extern	domain	tcleng				draftactual
	extern	domain	tcleng				draftbench
	extern	domain	tcleng				checkactual
	extern	domain	tcleng				checkbench
	extern	domain	tcleng				totalactual
	extern	domain	tcleng				totalbench
	extern	domain	tcmcs.str15			dstatus
	extern	domain	tcleng				threedmodhhrs
	extern	domain	tcleng				reworkhhrs

|****************************** form section **********************************

form.1:
init.form:
	get.screen.defaults()

|****************************** choice section ********************************

choice.cont.process:
on.choice:
	execute(print.data)

choice.print.data:
on.choice:
	if rprt_open() then
		read.main.table()
		rprt_close()
	else
		choice.again()
	endif


|****************************** field section *********************************

field.cprj.f:
when.field.changes:
	cprj.t = cprj.f

field.cspa.f:
when.field.changes:
	cspa.t = cspa.f
	
field.emno.f:
when.field.changes:
	emno.t = emno.f
	
field.grcd.f:
when.field.changes:
	grcd.t = grcd.f
	
field.tdate.f:
when.field.changes:
	tdate.t = tdate.f			


|****************************** function section ******************************

functions:

function read.main.table()
{
	select	tiisg910.cprj, tiisg910.dele, tiisg910.dcat, tiisg910.dsno, tiisg910.dsiz, tiisg910.dgid, tcmcs052.dsca,tiisg910.grcd,tiisg910.cdoc,tiisg910.emno,tccom001.nama
	from	tiisg910, tcmcs052,tccom001,tppdm911
	where	tiisg910._index3 inrange {:cprj.f, :cspa.f} and {:cprj.t, :cspa.t}
		and	tiisg910._index1 inrange {:emno.f} and {:emno.t}
		and	tiisg910.grcd between :grcd.f and :grcd.t 	
		and	tiisg910.acid in (1,2)
		and	tiisg910.cprj = tcmcs052.cprj
		and	tiisg910.emno = tccom001.emno	
		and	tiisg910.cdoc = tppdm911.cdoc
		and	tppdm911.cprj = tiisg910.cprj
		and	tppdm911.rdat between :tdate.f and :tdate.t
		and	tppdm911.cdoc <> "                              "
	group by tiisg910.cprj, tiisg910.dele, tiisg910.dcat, tiisg910.dsno, tiisg910.dsiz, tiisg910.dgid, tcmcs052.dsca,tiisg910.grcd,tiisg910.cdoc,tiisg910.emno,tccom001.nama
	order by tiisg910._index9
	selectdo
		draftactual = 0
		draftbench  = 0 
		checkactual = 0
		checkbench  = 0
		totalactual = 0
		totalbench  = 0
		dstatus     = ""
		threedmodhhrs= 0
		reworkhhrs = 0
		
		select sum(hhrs):draftactual from tiisg910 
		where tiisg910._index9 = {:tiisg910.cprj, :tiisg910.dele}
			and	tiisg910.dcat = :tiisg910.dcat
			and	tiisg910.dsno = :tiisg910.dsno
			and	tiisg910.dsiz = :tiisg910.dsiz 
			and	tiisg910.emno = :tiisg910.emno 
			and tiisg910.acid = 1 
		selectdo 
		endselect
		
		select sum(hhrs):checkactual from tiisg910 
		where tiisg910._index9 = {:tiisg910.cprj, :tiisg910.dele}
			and	tiisg910.dcat = :tiisg910.dcat
			and	tiisg910.dsno = :tiisg910.dsno
			and	tiisg910.dsiz = :tiisg910.dsiz 
			and	tiisg910.emno = :tiisg910.emno
			and tiisg910.acid = 2 
		selectdo 
		endselect
		
		totalactual = draftactual + checkactual		
		draftbench = get.standard.hrs(tiisg910.grcd, tiisg910.dgid, 1, tiisg910.dsiz)
		checkbench = get.standard.hrs(tiisg910.grcd, tiisg910.dgid, 2, tiisg910.dsiz)
		totalbench = draftbench + checkbench
		
		
		select tppdm911.cdoc,tppdm911.rdat
		from	tppdm911
		where	tppdm911.cdoc = :tiisg910.cdoc 
		selectdo
			dstatus = sprintf$("%D715",tppdm911.rdat)
		selectempty
			dstatus = "Under Process"
		endselect
		
		select sum(hhrs):threedmodhhrs
		from	tiisg910 
		where	tiisg910._index9 = {:tiisg910.cprj, :tiisg910.dele}
			and	tiisg910.dcat = :tiisg910.dcat
			and	tiisg910.dsno = :tiisg910.dsno
			and	tiisg910.dsiz = :tiisg910.dsiz
			and	tiisg910.emno = :tiisg910.emno 
			and	tiisg910.acid = 56
		selectdo
		endselect
		
		
		select sum(hhrs):reworkhhrs
		from	tiisg910 
		where	tiisg910._index9 = {:tiisg910.cprj, :tiisg910.dele}
			and	tiisg910.dcat = :tiisg910.dcat
			and	tiisg910.dsno = :tiisg910.dsno
			and	tiisg910.dsiz = :tiisg910.dsiz
			and	tiisg910.emno = :tiisg910.emno 
			and	tiisg910.acid in(3,74)
		selectdo
		endselect
		
		rprt_send()
	endselect
}




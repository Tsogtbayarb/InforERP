|******************************************************************************
|* tpisg2200  0  VRC B61U a7 dev 
|* Process Sanction Control
|* sujeet                        
|* 2014-02-17
|******************************************************************************
|* Main table tpest200 Estimate Lines, Form Type 4
|******************************************************************************
|*
|* GH471CR000, RAvi Kumar, 08-04-2021
|* Estimate line launch sanction control should not allow again if sanction already transfer  
|*
|* GH622CR000, RAvi Kumar, 05-08-2021
|* All Elements of estimate should be present in Standard WBS              
|****************************** declaration section ***************************
declaration:

table	ttpest200	|Estimate Lines
table	ttpisg001	|Sanction Control
table	ttpisg012	|Sanction Control

extern	domain	tccprj		cprj.f	fixed
extern	domain	tpest.vers	vers.f	fixed
extern	domain	tpest.sern	sern.f
extern	domain	tpest.sern	sern.t
extern	domain	tcmcs.str13	v.sess
long				ret

extern	domain	tcccty		o.reas			|#3823.n

#include<bic_dam>
|****************************** program section ********************************
before.program:
	import("prog.name$",v.sess)
	if v.sess = "tpest2100m000" then
		import("tpest200.cprj",cprj.f)
		import("tpest200.vers",vers.f)
	endif
	
	select	tpisg011.cprj						|GH471CR000.a.s
	from	tpisg011
	where	tpisg011._index1 = {:cprj.f}
	as set with 1 rows
	selectdo
		message("Project already Lanched")	
		execute(end.program)
	endselect							|GH471CR000.a.e
	
|****************************** group section **********************************

group.1:
init.group:
	get.screen.defaults()
	
	if v.sess = "tpest2100m000" then
		import("tpest200.cprj",cprj.f)
		import("tpest200.vers",vers.f)
	endif

|******************************* Form Command Section **************************	

choice.lunch:
before.choice:
								|#3823.sn
	if tpisgdll0012.Sanction_Approval_Matrix(tpisg.sanction.launch,o.reas) then		
		if not tpisgdll0012.Employee_Responsible_By_Project(cprj.f,tpisg.sanction.launch) then
			message("Sanction Approval Role Matrix Not Defined!!!")
			choice.again()
		endif
		
	else
		message("Approval Matrix is Not Defined !!!")
		choice.again()
	endif
	
	if check.not.standard.element.not.present() then			|GH622CR000.a.s
		message("Element " & trim$(tpest200.psel) & " not present in standard WBS. Sanction cannot be launched")
		choice.again()
	endif									|GH622CR000.a.e
								|#3823.en	
on.choice:
| 	dal.start.business.method("tpisg001","lunch",ret)
| 	dal.start.business.method("tpisg012","lunch",ret)			|#3823.o
	dal.start.business.method("tpisg012","lunch",ret,o.reas)		|#3823.n
	if ret = 0 then
		message("Successfully Launched")
	endif

|******************************* Field Section *********************************

field.sern.f:
when.field.changes:
	sern.t = sern.f

functions:						|GH622CR000.a.s
function boolean check.not.standard.element.not.present()
{
	select	tpest200.psel
	from	tpest200
	where	tpest200._index1 = {:cprj.f, :vers.f}
	selectdo
		select	tppdm090.cspa
		from	tppdm090
		where	tppdm090._index1 = {:tpest200.psel}
		as set with 1 rows
		selectdo
		selectempty
			return(true)
		endselect
	endselect
	return(false)
}										|GH622CR000.a.e


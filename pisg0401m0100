|******************************************************************************
|* tfisg0401m010  0  VRC B61U a7 live
|* Payment Details (IR Wise)
|* Tools                         
|* 2014-05-03
|******************************************************************************
|* Main table tfisg001 GR Details, Form Type 4
|*****************************************************************************
|* ID: ISGEC001118, IT0327, Ritu Shrivastava, Dt. 18-10-2014, VRC B61U a7 isg      

|*****************************************************************************
|* ID: ISGEC01023, IT0289, Shilpa Janardanan, Dt. 16-01-2015, VRC B61U a7 isg  
|* Change in Logic for  "Supplier Invoice No.", "Invoice Amount", & new total field added
|*****************************************************************************
|* ID:149, IDENT ISGEC01051 , Mani sharma, Dt.10-02-2015, VRC B61U a7 isg
|* Change on report labels
|****************************** declaration section ***************************
declaration:

  table   	ttfisg001 | GR Details
  table		ttfacp200
  table		ttccom100
  table		ttfcmg110
  table		ttfcmg112				|#ISGEC001118.sn
  table		ttfcmg113
  table		ttfcmg100
  table		ttfcmg103			
  table		ttfgld102
  table		ttfgld106				|#ISGEC001118.en
  table		ttfacp100				|#ISGEC01023.n
  table		ttdpur401				|#ISGEC01023.n
  table		twhinh312				|#ISGEC01023.n
  table		ttcmcs036
  table		ttcmcs032
  table		twhinh310
  table		ttclct200
  table		ttfacp935
  table		twhinh936
  table		ttdpur406
  table		ttctax941				|#ISGEC01023.en
  
  extern	domain	tccom.bpid	bpid.f
  extern	domain	tccom.bpid	bpid.t
  extern	domain	tfgld.docn	irno.f
  extern	domain	tfgld.docn	irno.t
  extern	domain	tccprj		cprj.f
  extern	domain	tccprj		cprj.t
  extern	domain	tcorno		orno.f
  extern	domain	tcorno		orno.t
							|#ISGEC001173.sn
  extern	domain	tcdate		irdt.f
  extern	domain	tcdate		irdt.t
							|#ISGEC001173.en
 extern 	domain	tcamnt		gross_amnt	|#ISGEC01023.sn
 extern	domain	tcamnt	r.eces.per, r.cess.per, r.vat.per
extern	domain	tcamnt	r.eces, r.cess, r.vat, add.chg,frt.amnt,add.chg.tax
extern	domain	tcamnt	total.vat,v.eces, v.cess, v.vat, v.add.chg,tax.amnt,tot.amnt
extern	domain	tcamnt	r.disc,cal.per
	extern	domain	tcamnt		rep.ot.amnt
	extern	domain	tcamnt		rep.exc.amnt
	extern	domain	tcamnt		rep.st.amnt
	extern	domain	tcamnt		tmp.rep.st.amnt
	extern	domain	tcamnt		tmp.rep.exc.amnt
	extern	domain	tcamnt		line.amnt
	extern	domain	tcamnt		gros.total
	extern	domain	tcamnt		line.total
	domain	tcbool		service.tax	
	domain	tcbool		fwhinh936.found
	extern  domain  tcpric           v.totl
							|#ISGEC01023.en
  
|****************************** program section ********************************


|****************************** group section **********************************

group.1:
init.group:
   get.screen.defaults()

|****************************** choice section ********************************

choice.cont.process:
on.choice:
   execute(print.data)

choice.print.data:
on.choice:
   if rprt_open() then
       read.main.table()
       rprt_close()
   else
       choice.again()
   endif


|****************************** field section *********************************

field.irno.f:
when.field.changes:
   irno.t = irno.f

field.bpid.f:
when.field.changes:
	bpid.t = bpid.f

field.orno.f:
when.field.changes:
	orno.t = orno.f

field.cprj.f:
when.field.changes:
	cprj.t = cprj.f


field.irdt.f:
when.field.changes:
	irdt.t = irdt.f
	
|****************************** function section ******************************

functions:

function read.main.table()
{

	extern	domain	tfgld.ttyp	link.ttyp,link.tdoc
	extern	domain	tfgld.docn	link.ninv,link.docn
	extern	domain	tfgld.amnt	link.amth,link.amth.1,assig.amth,ret.amnt
	

								|#ISGEC001173.sn
	long yearno
	long monthno
	long month_dayno
	long hours
	long minutes
	long seconds
	
	yearno = 0
	monthno = 0
	month_dayno = 0
	hours = 0
	minutes = 0
	seconds=  0
	
	utc.to.date  (irdt.f, yearno, monthno, month_dayno, hours, minutes, seconds)
	irdt.f = date.to.utc  (yearno, monthno, month_dayno, 0, 0, 1)
	
	utc.to.date  (irdt.t, yearno, monthno, month_dayno, hours, minutes, seconds)
	irdt.t = date.to.utc  (yearno, monthno, month_dayno, 23, 59, 59)
	
								|#ISGEC001173.en


	
	select 	tfisg001.*,
		tccom100.nama
	from	tfisg001,tccom100
	where	tfisg001._index1 inrange {:irno.f} and {:irno.t}
	and	tfisg001.irdt inrange :irdt.f and :irdt.t		|#ISGEC001173.n
| 	and	tfisg001.cust inrange :bpid.f and :bpid.t
	and	tfisg001.bpid inrange :bpid.f and :bpid.t
	and	tfisg001.cprj inrange :cprj.f and :cprj.t
	and 	tfisg001.orno inrange :orno.f and :orno.t
| 	and	tfisg001.cust refers to tccom100
	and	tfisg001.bpid refers to tccom100
	order by tfisg001.irdt
	selectdo
		
		link.ttyp = ""
		link.tdoc = ""
		link.ninv = 0
		link.docn = 0
		link.amth = 0
		assig.amth = 0
		tfcmg110.cheq = ""
		tfcmg100.rdat = 0
		tfacp200.isup = ""
		tfacp200.amth(1) = 0
		
		ret.amnt = 0 
		tfacp200.docd = 0
		
		get_gross_amount()		|ISGEC01023.n
		select	tfacp100.isup			|#ISGEC01023.sn
		from	tfacp100
		where	tfacp100._index1 = {:tfisg001.irno}
		selectdo
		selectempty
			tfacp100.isup = ""
		endselect				|#ISGEC01023.en
		|********
		|* When tfacp200.tpay = 1 or 4
		|********
		
		select tfacp200.ttyp,
			tfacp200.ninv,
			tfacp200.isup,
			tfacp200.docd,
			tfacp200.amth,
			tfacp200.dued
		from	tfacp200
		where	tfacp200._index1 = {:tfisg001.invt,:tfisg001.pinv}
| 		and	(tfacp200.tpay = 1 or tfacp200.tpay = 4)		|temp.o
		and	(tfacp200.tpay = 1)		|temp.n
		selectdo
			
			link.ttyp = ""
			link.tdoc = ""
			link.ninv = 0
			link.docn = 0
			link.amth = 0
			assig.amth = 0
			ret.amnt = 0
			
			select 	tfacp200.amth(1):link.amth.1,
				tfacp200.ttyp:link.ttyp,
				tfacp200.ninv:link.ninv,
				tfacp200.tdoc:link.tdoc,
				tfacp200.docn:link.docn,
				tfacp200.tpay, tfacp200.pdoc				|#ISGEC001118.n
			from	tfacp200
			where	tfacp200._index1 = {:tfacp200.ttyp,:tfacp200.ninv}
			and	(tfacp200.tpay = 2 or tfacp200.tpay = 8 or tfacp200.tpay = 4 or tfacp200.tpay = 14)	|#sujeet29112014
			selectdo
				
| 				on case etol(tfacp200.tpay)				|#ISGEC001118.so
| 				case 2:
| 					select tfcmg110.crea,
| 						tfcmg110.cheq				
| 					from	tfcmg110
| 					where	tfcmg110.tdoc = :tfacp200.ttyp 
| 					and	tfcmg110.pdoc = :tfacp200.ninv
| 					as set with 1 rows
| 					selectdo
| 					selectempty
| 						tfcmg110.cheq = ""			
| 						tfcmg110.crea = 0
| 					endselect
| 					

| 					break
| 				case 8:
| 					select tfcmg110.crea,
| 						tfcmg110.cheq			
| 					from	tfcmg110
| 					where	tfcmg110.tdoc = :tfacp200.tdoc
| 					and	tfcmg110.pdoc = :tfacp200.docn
| 					as set with 1 rows
| 					selectdo
| 					selectempty
| 						tfcmg110.cheq = ""			
| 						tfcmg110.crea = 0
| 					endselect
| 					
| 					break
| 				endcase							|#ISGEC001118.eo
					

				on case etol(tfacp200.tpay)
				case	2:
				case	8:
					link.amth = link.amth + link.amth.1			|#sujeet29112014
					break
				case	4:
				case	14:
					assig.amth = assig.amth + link.amth.1
					break
					
				endcase
				
											|#ISGEC001118.sn
				on case etol(tfacp200.tpay)				
				case 8:
					
					select	tfcmg100.cheq, tfcmg100.rdat,tfcmg100.chst
					from	tfcmg100
					where	tfcmg100.typo = :link.tdoc
					and	tfcmg100.doco = :link.docn
					selectdo
						if tfcmg100.chst = ltoe(5) then
							tfcmg110.cheq = "*" & strip$(shiftl$(tfcmg100.cheq))
						else
							tfcmg110.cheq = strip$(shiftl$(tfcmg100.cheq))
						endif	
						tfcmg113.rdat = tfcmg100.rdat
					selectempty
						Get_Cheque_Normal(link.tdoc, link.docn, link.ttyp, link.ninv, tfcmg.tcsh.cntr.ant, tfcmg110.cheq,tfcmg100.chst)
					endselect
					if tfcmg100.chst = ltoe(5) then
						tfcmg110.cheq = "*" & strip$(shiftl$(tfcmg100.cheq))
					else
						tfcmg110.cheq = strip$(shiftl$(tfcmg100.cheq))
					endif
					break
				case 2:
					Get_Cheque_Normal(link.tdoc, link.docn,link.ttyp, link.ninv, tfcmg.tcsh.cntr, tfacp200.pdoc,tfcmg100.chst)

				endcase
				
											|#ISGEC001118.en
| 				rprt_send()			|#sujeet29112014
			endselect
			
			ret.amnt = 0
			select 	sum(tfacp201.amnt):ret.amnt
			from	tfacp201
			where	tfacp201._index1 = {:tfacp200.ttyp,:tfacp200.ninv}
			and	tfacp201.cdf_bloc <> ""
			selectdo
			endselect
			
			rprt_send()				|#sujeet29112014
		selectempty
			rprt_send()
		endselect
		
	endselect
	
}


function Get_Cheque_Normal(								|#ISGEC001118.sn
			domain	tfgld.ttyp	i.tdoc,
			domain	tfgld.docn	i.docn,
			domain	tfgld.ttyp	i.ttyp,
			domain	tfgld.docn	i.ninv,
			domain	tfcmg.tcsh	i.tcsh,
		ref	domain	tfcmg.cheq	o.cheq,
		ref	domain	tfcmg.chst	o.chst
			)
{
	o.cheq = ""
	select	tfgld102.lino
	from	tfgld102
	where	tfgld102.ttyp = :i.tdoc
	and	tfgld102.docn = :i.docn
	and	tfgld102.tcsh = :i.tcsh
	and	tfgld102.ctyp = :i.ttyp
	and	tfgld102.cdoc = :i.ninv
	selectdo
		select	tfcmg112.bank, tfcmg112.tdoc, tfcmg112.pdoc
		from	tfcmg112
		where	tfcmg112._index2 = {:i.tdoc, :i.docn, :tfgld102.lino}
		selectdo
			select	tfcmg103.paym
			from	tfcmg103
			where	tfcmg103.ttyp = :tfcmg112.tdoc
			and	tfcmg103.docn = :tfcmg112.pdoc
			and	tfcmg103.bank = :tfcmg112.bank
			selectdo
				select	tfcmg100.cheq:o.cheq,
				        tfcmg100.rdat,tfcmg100.chst:o.chst
				from	tfcmg100
				where	tfcmg100._index1 = {:tfcmg112.bank, :tfcmg103.paym}
				and	tfcmg100.typo = :tfcmg112.tdoc
				and	tfcmg100.doco = :tfcmg112.pdoc
				selectdo
					tfcmg113.rdat = tfcmg100.rdat
				selectempty
					tfcmg113.rdat = 0
					o.cheq = ""
				endselect
			selectempty
				tfcmg113.rdat = 0
				o.cheq = ""
			endselect
		selectempty
			tfcmg113.rdat = 0
			o.cheq = ""
		endselect
	selectempty

		select	tfgld106.olin
		from	tfgld106
		where	tfgld106._index1 = {:i.tdoc, :i.docn}
		and	tfgld106._index9 = {:i.ttyp, :i.ninv}
		and	tfgld106.tcsh = :i.tcsh
		selectdo
			select	tfcmg112.bank, tfcmg112.tdoc, tfcmg112.pdoc
			from	tfcmg112
			where	tfcmg112._index2 = {:i.tdoc, :i.docn, :tfgld106.olin}
			selectdo
				select	tfcmg103.paym
				from	tfcmg103
				where	tfcmg103.ttyp = :tfcmg112.tdoc
				and	tfcmg103.docn = :tfcmg112.pdoc
				and	tfcmg103.bank = :tfcmg112.bank
				selectdo
					select	tfcmg100.cheq:o.cheq,tfcmg100.chst:o.chst
					from	tfcmg100
					where	tfcmg100._index1 = {:tfcmg112.bank, :tfcmg103.paym}
					and	tfcmg100.typo = :tfcmg112.tdoc
					and	tfcmg100.doco = :tfcmg112.pdoc
					selectdo
						tfcmg113.rdat = tfcmg100.rdat
					selectempty
						tfcmg113.rdat = 0
						o.cheq = ""
					endselect
				selectempty
					tfcmg113.rdat = 0
					o.cheq = ""
				endselect
			selectempty
				tfcmg113.rdat = 0
				o.cheq = ""
			endselect
		selectempty
			tfcmg113.rdat = 0
			o.cheq = ""
		endselect	
	endselect
	
}		
													|#ISGEC001118.en
function get_gross_amount()			|#ISGEC01023.sn
{
	gross_amnt = 0
	select	whinh312.*
	from	whinh312
	where	whinh312._index1 = {:tfisg001.rcno}
	selectdo
		select	whinh310.stat
		from	whinh310
		where	whinh310._index1 = {:whinh312.rcno}
		selectdo
		endselect
		
		select	tdpur401.*
		from	tdpur401
		where	tdpur401._index1 = {:whinh312.orno ,:whinh312.pono, :whinh312.seqn}
		selectdo
			get.discount.amount()
			v.totl = (tdpur401.pric - r.disc ) * whinh312.qrec
			rep.ot.amnt = 0
			rep.exc.amnt = 0
			rep.st.amnt = 0
			service.tax = false	
			calculate_freight()
			get.tax.rate(tdpur401.ccty, tdpur401.cvat)
			if	service.tax	then
				if	tdpur401.qoor <> 0	then
					line.amnt = (tdpur401.tass.l/tdpur401.qoor) * whinh312.qrcr
				else
					line.amnt = 0
				endif
			else
				if	tdpur401.qoor <> 0	then
					line.amnt = (tdpur401.tase.l/tdpur401.qoor) * whinh312.qrcr
				else
					line.amnt = 0
				endif
			endif
			
			v.eces =  ( (line.amnt * r.eces.per) / 100 )
			v.cess = (v.eces * r.cess.per) / 100 
			
			rep.exc.amnt = rep.exc.amnt + (v.eces + v.cess)
			r.eces = r.eces + v.eces
			r.cess = r.cess + v.cess
			if	tdpur401.qoor <> 0	then
				total.vat = (total.vat/tdpur401.qoor)*whinh312.qrcr
			else
				total.vat = 0
			endif	
			v.vat = ((total.vat + v.eces + v.cess  ) * r.vat.per) / 100
			rep.st.amnt = ((total.vat + rep.exc.amnt  ) * r.vat.per) / 100
			on case whinh310.stat
				case whinh.rhst.confirmed:
					get.tax.from.tfacp935(whinh312.rcno, whinh312.rcln, whinh312.orno, whinh312.pono,
								whinh312.seqn)
				break
				case whinh.rhst.open:
					tmp.rep.exc.amnt = rep.exc.amnt
					tmp.rep.st.amnt = rep.st.amnt
					fwhinh936.found = false		
					get.tax.from.whinh936()

					if	fwhinh936.found = false	then
						rep.st.amnt = tmp.rep.st.amnt
						rep.exc.amnt = tmp.rep.exc.amnt
					endif	
				break	
			ENDCASE	
			
			line.total = 0
			line.total = (rep.ot.amnt + rep.exc.amnt + rep.st.amnt) + (whinh312.qrcr * tdpur401.pric)
			gross_amnt = gross_amnt + line.total
| 			gross_amnt = gross_amnt +(whinh312.qrcr * tdpur401.pric)	
		selectempty
			gross_amnt = 0 
		endselect	
	selectempty
		get_gross_fromservice()
| 		gross_amnt = 0 
	
	endselect
}						|#ISGEC01023.en

function get.discount.amount()
{
	r.disc = 0
	
	if tdpur401.disc(1) <> 0 then 	| disc % 
		r.disc = tdpur401.pric * tdpur401.disc(1) / 100
	else
		if tdpur401.ldam(1) <> 0 then  | disc amnt	
			r.disc = tdpur401.ldam(1)
		endif
	endif
}


function get.tax.rate(domain tcccty i.ccty, 
		      domain tccvat i.cvat)
{
	r.eces.per = 0 
	r.cess.per = 0
	r.vat.per = 0
	
	select 	tctax941.*
	from 	tctax941
	where	tctax941._index1={:i.ccty,
				  :i.cvat}
	and	tctax941.type = {20}
	selectdo
		select 	tcmcs036.*
		from 	tcmcs036
		where	tcmcs036._index1={:tctax941.ccty,
					  :tctax941.cvat}
		selectdo
			if tcmcs036.indt.l = tctax.indt.l.bed or
				tcmcs036.indt.l = tctax.indt.l.service	then 
				select 	tcmcs032.*
				from 	tcmcs032
				where	tcmcs032._index1={:tcmcs036.ccty,
							  :tcmcs036.cvat}
				order by tcmcs032._index1 desc
				as set with 1 rows
				selectdo
					r.eces.per = tcmcs032.pvat
				endselect
				
				if	tcmcs036.indt.l = tctax.indt.l.service	then
					service.tax = true
				endif	
				
			endif
			
			if tcmcs036.indt.l = tctax.indt.l.e.cess.excise or
			   tcmcs036.indt.l = tctax.indt.l.hse.cess.excise or
			   tcmcs036.indt.l = tctax.indt.l.e.cess.service or		 
			   tcmcs036.indt.l = tctax.indt.l.hse.cess.servic 	then 
				select 	tcmcs032.*
				from 	tcmcs032
				where	tcmcs032._index1={:tcmcs036.ccty,
							  :tcmcs036.cvat}
				order by tcmcs032._index1 desc
				as set with 1 rows
				selectdo
					r.cess.per = r.cess.per + tcmcs032.pvat
				endselect
			endif
					
			if tcmcs036.indt.l = tctax.indt.l.vat  or
			tcmcs036.indt.l = tctax.indt.l.cst	or
			tcmcs036.indt.l = tctax.indt.l.lst	or
			tcmcs036.indt.l	= tctax.indt.l.n.a	then 
				select 	tcmcs032.*
				from 	tcmcs032
				where	tcmcs032._index1={:tcmcs036.ccty,
							  :tcmcs036.cvat}
				order by tcmcs032._index1 desc
				as set with 1 rows
				selectdo
					r.vat.per = r.vat.per + tcmcs032.pvat
				endselect
			endif
		endselect
	endselect
	
}

function calculate_freight()
{
	frt.amnt = 0
	add.chg = 0
	domain tcamnt	additional.amount
	select 	tclct200.lcos, sum(tclct200.lcam):additional.amount
	from	tclct200
	where	tclct200._index1 = {21, 100, :tfisg001.rcno, :1}
	
	group by tclct200.lcos
	wherebind(1, str$(whinh312.rcln))
	selectdo
		select	tclct115.dsca from tclct115
		where	tclct115._index1 = {:tclct200.lcos}
		selectdo
		endselect
		if tclct200.lcos = "FRT0001     " then
			frt.amnt = additional.amount
		else
			add.chg = add.chg + additional.amount
		endif
		rep.ot.amnt = rep.ot.amnt + additional.amount
	endselect
| 	if tdpur401.txin.l = tcyesno.no then
| 		g.totl = g.totl + frt.amnt + add.chg
| 	endif
	
}
function get.tax.from.tfacp935(domain	tcorno	rcno, domain	tcpono rcln, domain	tcorno	orno
				, domain	tcpono	pono
				, domain	tcpono	sqnb)
{
	domain	tcamnt	amount
	
	rep.exc.amnt = 0
	rep.st.amnt = 0
	
	select	tfacp935.ccty, tfacp935.cvat, sum(tfacp935.vamt):amount
	from	tfacp935
	where	tfacp935.rcno = :rcno
	and	tfacp935.rseq = :rcln
	and	tfacp935.orno = :orno			
	and	tfacp935.pono = :pono			
	and	tfacp935.sqnb = :sqnb			
	group by tfacp935.ccty, tfacp935.cvat
	selectdo
		select	tcmcs036.indt.l,tcmcs036.dsca
		from	tcmcs036
		where	tcmcs036._index1 = {:tfacp935.ccty, :tfacp935.cvat}
		selectdo
			on case etol(tcmcs036.indt.l)
			case 1:				|Service
			case 4:				|Basic Excise Duty
			case 9:				|Basic Custom Duty	
			case 10:			|Countervailing Duty
			case 14:			|ECess CVD
			case 15:			|ECess on Custom
			case 16:			|Ecess on Service
			case 17:			|Ecess on Excise
			case 19:			|SHE Cess on CVD
			case 20:			|SHE Cess on Excise
			case 21:			|SHE Cess on Service
			case 22:			|SHE Cess on Custom
				rep.exc.amnt = rep.exc.amnt + amount
				break
			case 2:				|Local Sales Tax	
			case 3:				|Central Sales Tax
			case 26:			|VAT
			case 30:			|Not Applicable
				rep.st.amnt = rep.st.amnt + amount
			endcase
		endselect
	endselect
}

function get.tax.from.whinh936()
{
	domain	tcamnt	amount
	
	rep.exc.amnt = 0
	rep.st.amnt = 0
	
	select   sum(whinh936.aamt):amount, whinh936.rcno, whinh936.taxc, whinh936.pvat
	from	 whinh936
	where	 whinh936._index1 = {:whinh312.rcno,:whinh312.rcln}
	group by whinh936.rcno,whinh936.taxc,whinh936.pvat
	selectdo
		fwhinh936.found = true		|#ISGECDV001020.n
		select	tcmcs036.indt.l,tcmcs036.dsca
		from	tcmcs036
		where	tcmcs036._index1 = {"IN", :whinh936.taxc}
		selectdo
			on case etol(tcmcs036.indt.l)
			case 1:				|Service
			case 4:				|Basic Excise Duty
			case 9:				|Basic Custom Duty	
			case 10:			|Countervailing Duty
			case 14:			|ECess CVD
			case 15:			|ECess on Custom
			case 16:			|Ecess on Service
			case 17:			|Ecess on Excise
			case 19:			|SHE Cess on CVD
			case 20:			|SHE Cess on Excise
			case 21:			|SHE Cess on Service
			case 22:			|SHE Cess on Custom
				rep.exc.amnt = rep.exc.amnt + amount
				break
			case 2:				|Local Sales Tax	
			case 3:				|Central Sales Tax
			case 26:			|VAT
			case 30:			|Not Applicable
				rep.st.amnt = rep.st.amnt + amount
			endcase
		endselect
	endselect
}


function get_gross_fromservice()
{
	v.eces = 0
	v.cess = 0
	select	tdpur406.*
	from	tdpur406
	where	tdpur406._index2 =  {:tfisg001.rcno}
	selectdo
		select	tdpur401.*
		from	tdpur401
		where	tdpur401._index1 =  {:tdpur406.orno, :tdpur406.pono, :tdpur406.sqnb}
		selectdo
			get.discount.amount()
			v.totl = (tdpur401.pric - r.disc ) * tdpur406.qiap
			rep.ot.amnt = 0
			rep.exc.amnt = 0
			rep.st.amnt = 0
			service.tax = false	
			calculate_freight()
			get.tax.rate(tdpur401.ccty, tdpur401.cvat)
			if	service.tax	then
				if	tdpur401.qoor <> 0	then
					line.amnt = (tdpur401.tass.l/tdpur401.qoor) * tdpur406.qiap
				else
					line.amnt = 0
				endif
			else
				if	tdpur401.qoor <> 0	then
					line.amnt = (tdpur401.tase.l/tdpur401.qoor) * tdpur406.qiap
				else
					line.amnt = 0
				endif
			endif
			
			v.eces =  ( (line.amnt * r.eces.per) / 100 )
			v.cess = (v.eces * r.cess.per) / 100 
			
			rep.exc.amnt = rep.exc.amnt + (v.eces + v.cess)
			r.eces = r.eces + v.eces
			r.cess = r.cess + v.cess
			if	tdpur401.qoor <> 0	then
				total.vat = (total.vat/tdpur401.qoor)*tdpur406.qiap
			else
				total.vat = 0
			endif	
			v.vat = ((total.vat + v.eces + v.cess  ) * r.vat.per) / 100
			rep.st.amnt = ((total.vat + rep.exc.amnt  ) * r.vat.per) / 100
			if	tdpur406.conf = tcyesno.yes	then
					get.tax.from.tfacp935(tdpur406.rcno, tdpur406.rseq, tdpur406.orno, tdpur406.pono,
						tdpur406.sqnb	)
			endif
			
			
			line.total = 0
			line.total = (rep.ot.amnt + rep.exc.amnt + rep.st.amnt) + (tdpur406.qiap * tdpur401.pric)
			gross_amnt = gross_amnt + line.total
		endselect	
	selectempty	
			gross_amnt = 0
	endselect
}

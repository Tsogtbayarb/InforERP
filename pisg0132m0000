|******************************************************************************
|* tpisg0132m000  0  VRC B61U a  stnd
|* Installment Wise Element Breakup
|* Installation user             
|* 2012-03-11
|******************************************************************************
|* Main table tpisg032 Installment Breakup, Form Type 1
|******************************************************************************
                                                                                
|****************************** declaration section ***************************
declaration:

  table		ttpisg032 | Installment Breakup
  table		ttpisg031
  table		ttppdm740
  table		ttpptc100
  

|****************************** Form Fields ************************************
extern 	domain	tppdm.aalc	contract.amnt		|Contract Amount
extern	domain	tppdm.aalc	tot.installment		|Total Of Installment
extern	domain	tppdm.aalc	tot.c.installment	|Total of Current Installment
extern	domain	tppdm.aalc	balance.entered		|Balance To Be Entered
|****************************** Field section ********************************

field.tpisg032.ofbp:
after.field:
	Get_ProjectBusinessPartner(tpisg032.cprj,tpisg032.ofbp,contract.amnt)
	display("contract.amnt")
	
	Get_Total_Of_Installment(tpisg032.cprj,tpisg032.ofbp,tot.installment)
	display("tot.installment")

	Get_Total_Of_Current_Installment(tpisg032.cprj,tpisg032.ofbp,tpisg032.nins,tot.c.installment)
	display("tot.c.installment")
	
	balance.entered = tot.installment - tot.c.installment
	display("balance.entered")
	
group.1:
init.group:
	export("tpisg032.cprj",tpisg032.cprj)
|****************************** Field section **********************************

field.tpisg032.cspa:
before.zoom:
	tpptc100.cprj = tpisg032.cprj
	
|****************************** Functions section **********************************
functions:

function Get_ProjectBusinessPartner
			(
				domain	tccprj		i.cprj,		|Project Code
				domain	tccom.bpid	i.ofbp,		|Business Partner
			ref 	domain	tppdm.aalc	o.contract.amnt	|Contract Amount	
			)
{
	select 	tppdm740.copr:o.contract.amnt
	from	tppdm740
	where	tppdm740._index1 = {:i.cprj,:i.ofbp}
	as set with 1 rows
	selectdo
	selectempty
		o.contract.amnt = 0
	endselect
	
}

function Get_Total_Of_Installment
			(
				domain	tccprj		i.cprj,	 	|Project Code
				domain	tccom.bpid	i.ofbp,		|Sold-to Business Partner
			ref 	domain	tppdm.aalc	o.insa		|Total of Installment 
			)
{

	o.insa = 0
	
	select 	sum(tpisg031.insa):o.insa
	from	tpisg031
	where	tpisg031._index1 = {:i.cprj,:i.ofbp}
	selectdo
	endselect
	
}

function Get_Total_Of_Current_Installment
			(
				domain	tccprj		i.cprj,		|Project Code
				domain	tccom.bpid	i.ofbp,		|Sold-to Business Partner
				domain	tppdm.nins	i.nins,		|Installment Number
			ref 	domain	tppdm.aalc	o.inia		|Last Assigned Amount		
			)
{
	o.inia = 0
	select tpisg032.inia:o.inia
	from	tpisg032
	where	tpisg032._index1 = {:i.cprj,:i.ofbp,:i.nins}
	order by tpisg032.inia desc
	as set with 1 rows
	selectdo
	endselect
	
}

|******************************************************************************
|* tpisg0474  0  VRC B61U a7 isg 
|* Site Action Report
|* user07                        
|* 2015-02-20
|******************************************************************************
|* Main table tpisg074 SAR Information, Form Type 4
|******************************************************************************
                                                                                
|****************************** declaration section ***************************
declaration:

  table   ttpisg074 | SAR Information
  table		ttccom100
  table		ttcmcs052  
  table		ttpisg076
  table		ttccom001
  table		tdmcom010

   extern  domain  tccprj           cprj.f    fixed
   extern  domain  tccprj           cprj.t    fixed
   extern  domain  tcmcs.long       sarn.f
   extern  domain  tcmcs.long       sarn.t
   extern	domain	tcmcs.str3	attach
   extern	domain	dmcom.shrtid	target.type
   extern	domain	dmcom.lnkd	target.object


|****************************** program section ********************************


|****************************** group section **********************************

group.1:
init.group:
   get.screen.defaults()

|****************************** choice section ********************************

choice.cont.process:
on.choice:
   execute(print.data)

choice.print.data:
on.choice:
   if rprt_open() then
       read.main.table()
       rprt_close()
   else
       choice.again()
   endif


|****************************** field section *********************************

field.cprj.f:
when.field.changes:
   cprj.t = cprj.f

field.sarn.f:
when.field.changes:
   sarn.t = sarn.f

before.zoom:
	query.extend.where.in.zoom("tpisg074.cprj = " & quoted.string(cprj.f))
	
field.sarn.t:
before.zoom:
	query.extend.where.in.zoom("tpisg074.cprj = " & quoted.string(cprj.t))
|****************************** function section ******************************

functions:

function read.main.table()
{
	select 	tpisg074.*
	from 	tpisg074
	where  	tpisg074._index1 inrange {:cprj.f, :sarn.f}
				    and {:cprj.t, :sarn.t}
	order by tpisg074._index1
	selectdo
		select tcmcs052.dsca
		from	tcmcs052
		where	tcmcs052._index1 = {:tpisg074.cprj}
		selectdo
		endselect
		
		select tccom100.nama
		from	tccom100
		where	tccom100._index1 = {:tpisg074.bpid}
		selectdo
		endselect
		
		select tpisg070.desc
		from	tpisg070
		where	tpisg070._index1 = {:tpisg074.natp}
		selectdo
		endselect
		
		select tpisg071.desc
		from	tpisg071
		where	tpisg071._index1 = {:tpisg074.cost}
		selectdo
		endselect
		
		select 	tccom001.nama
		from	tccom001
		where	tccom001._index1 = {:tpisg074.apby}
		selectdo
		endselect
		
		select 	tpisg076.desc
		from	tpisg076
		where	tpisg076._index1 = {:tpisg074.cwoc}
		selectdo
		endselect
		check_attachment()
		rprt_send()
	endselect
}

function check_attachment()
{
	attach = ""
	target.type = "SAR"
	target.object = "{" & chr$(34) & tpisg074.cprj & chr$(34) & "," & str$(tpisg074.sarn) & "}" 
	
	select	dmcom010.*
	from	dmcom010
	where	dmcom010._index4 = {:target.type, :target.object}
	selectdo
		attach = "Yes"
	selectempty
		attach = "No"
	endselect
}

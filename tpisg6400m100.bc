|******************************************************************************
|* tpisg6400m100  0  VRC B61U a7 live
|* Print Project Invoice
|* Installation user             
|* 2014-09-08
|******************************************************************************
|* Main table tppdm600 Project, Form Type 4
|******************************************************************************
|* ISGECDV001065, IT0205, Arjit Gupta, VRC B61U a7 live, Dt. 29-09-2014
|* Add logic of Advance and Rentention.
|******************************************************************************
|* ISGEC001079, IT0327, Ritu Shrivastava, VRC B61U a7 isg, Dt. 08-10-2014
|* Added calculation for miscellaneous claims
|* 
|* ISGEC001097, IT0205, Arjit Gupta, VRC B61U a7 isg, Dt. 14-10-2014
|* Composed Invoice logic added from temp. Table ciisg999.
|*
|* ISGEC001128, IT0205, Arjit Gupta, VRC B61U a7 isg, Dt. 27-10-2014
|* Composed Invoice Basic Amount Logic Changed.
|*
|* ISGEC001193, IT0205, Arjit Gupta, VRC B61U a7 isg, Dt. 23-12-2014
|* Case ID : 173, Label "Excise Duty Reimbursement" to be changed to 
|* "ED Reimbursement/SHE cess on Service Tax" and "S. Tax Reimbursement" to be
|* changed to "Sales Tax Reimbursement/Edu Cess on Service Tax" and both cess
|* on Service Tax will be bifurcated accordingly.

|*ISGEC01055, IT0289, SHilpa Janardanan, VRC B61U a7 isg, Dt 18-12/2015,
|* calculation for Advance remittance to be done only for "AAR" type
|*
|* ISGEC015062, Shilpa Janardanan, VRC B61U a7 isg, Dt 21-09-2015,
|* Logic correction to print domestic & export data
|*
|* ID ISGECFIN015, IT0386, Saurabh Dubey, 06 July 2016, VRC B61U a7 isg
|* Addition of Krishi Kalyan Cess Logic and Swacch Bharat Logic
|*
|* ID ISGEC016028, Manish Manchanda, 2016-10-13
|* Modification - customer field added in selection range and logic modified accordingly
|*
|* ID ISGEC01036,Shilpa Janardanan, 16/01/2017
|*correction for printing Basic amount if there are 2 BPs linked to One project
|*
|* ISGEC017017, Manish Manchanda, 2017-04-05
|* Modifications - packing and forwarding logic added in miscellaneous claims
|*
|* ISGECGST004, IT0386, Saurabh Dubey, 06 June 2017, VRC B61U a7 isg
|* GST Related Modifications
|* Isgec Onsite Deployment
|*
|* ID ISGEC001217, Priya Jindal, 8/2/2018, VRC B61U a7 isg
|* Changes in data read from parameter table 
|*
|*ID ISGEC001221, Manwendra singh, 14/03/2018, VRC B61U a7 isg
|*Modification, we found the message “no data within selection” while running the Co. 700
|*
|* ID: ISGEC01094, Bharti Garg, 07-08-2018, VRC B61U a7 isg
|* Added a logic for utgst amount
|*
|* ID: GH288CR000, RAvi Kumar, 13-03-2020,
|* Add logic in comp 651
|* 
|* GH434CR748, RAvi Kumar, 04-11-2020
|* Manual Invoice, Debit Note, Credit Note in Report Invoice Register
|****************************** declaration section ***************************
declaration:

	table	tcisli235
	table	ttcmcs052
	table	ttppin020
	table	tciisg000
	table	ttcemm170
	table	ttfacr200			|#ISGECDV001065.n
	table	tciisg999		|#ISGEC001097.n
	table	ttppdm740		|#ISGEC001097.n
							|#|#ISGEC001167.sn
	table	ttppdm600
	table	ttccom000
	table	ttccom130
							|#ISGEC001167.en
	table	tcisli936		|#ISGEC001193.n
	table	tciisg804		|GH434CR748.a
	table	tciisg805		|GH434CR748.a

	|***************** Form Variable ****************
	extern	domain	tccprj		cprj.f    fixed
	extern	domain	tccprj		cprj.t    fixed
	extern	domain	tcdate		indt.f
	extern	domain	tcdate		indt.t
	extern	domain	tcncmp		ncmp.f
	extern	domain	tcncmp		ncmp.t
	
	|**************** Script Variable ***************
	extern	domain	tcdate		v.indt.f
	extern	domain	tcdate		v.indt.t
	extern	domain	tcncmp		curr.comp,comp		|#ISGEC001221.n
	extern	domain	tfgld.date	v.date.f		|#ISGEC001097.n
	extern	domain	tfgld.date	v.date.t		|#ISGEC001097.n
	extern	domain	tcamnt		scr.samt		|#ISGEC001193.n
	extern	domain	tcamnt		scr.sces		|#ISGEC001193.n
	extern	domain	tcamnt		scr.shcs		|#ISGEC001193.n
	extern	domain	tcamnt		scr.kkcs		|#ISGECFIN015.n
	extern	domain	tcamnt		scr.sbcs		|#ISGECFIN015.n
	extern	domain	tcamnt		scr.cgst		|#ISGECGST004.sn
	extern	domain	tcamnt		scr.sgst
	extern	domain	tcamnt		scr.igst
	extern	domain	tcamnt		scr.cess		|#ISGECGST004.en
	|*************** Report Variable ****************
	extern	domain	tcamnt		rep.bamt	|Basic Amount
	extern	domain	tcamnt		rep.samt	|Sales Tax Amount
	extern	domain	tcamnt		rep.rem.edmt	|ED Reimbursement
	extern	domain	tcamnt		rep.rem.samt	|S. Tax Reimbursement
	extern	domain	tcamnt		rep.misc.claim	|Misc. Claims Reimbursement
	extern	domain	tcamnt		rep.gross.val	|Gross Invoice
	extern	domain	tcamnt		rep.advn	|Advance
	extern	domain	tcamnt		rep.retn	|Retention
	extern	domain	tcamnt		rep.nt.inmt	|Net Invoice Amount
	extern	domain	tcamnt		rep.cgst.amnt	|CGST	|#ISGECGST004.sn
	extern	domain	tcamnt		rep.sgst.amnt	|SGST	
	extern	domain	tcamnt		rep.igst.amnt	|IGST	
	extern	domain	tcamnt		rep.cess.amnt	|Cess	|#ISGECGST004.en	
	
	
	extern	domain	tdisg.type	type.f		|#ISGEC001167.n
	extern	domain	tccom.bpid	bpid.f, bpid.t			|#ISGEC016028.n
						|#SD06122017.sn
		domain	tcncmp		get_from_company
	extern	domain	tcdate		curr.date		|ISGEC001217.n	
	
			long	brp.id				|GH434CR748.a
	extern	domain	tfgld.date	date.f, date.t
		
		#define LOG.COMP1 200
		#define LOG.COMP2 700		|#SD06122017.en
		#define LOG.COMP3 651		|GH288CR000.a
|****************************** program section ********************************
before.program:
	curr.comp = get.compnr()
			|#SD06122017.sn
	curr.date = utc.num()			|ISGEC001217.n		
	if curr.comp = LOG.COMP2 then
		get_from_company = LOG.COMP2
	else
		if curr.comp = LOG.COMP3 then			|GH288CR000..a.logic
			get_from_company = LOG.COMP3
		else
			get_from_company = LOG.COMP1
		endif
	endif
			|#SD06122017.en

|****************************** group section **********************************
group.1:
init.group:
	get.screen.defaults()

|****************************** choice section ********************************
choice.cont.process:
on.choice:
	execute(print.data)

choice.print.data:
on.choice:
	if	rprt_open()	then
		if reportno = 4 then
			credit.debit.manual.data()
		else
			print.composed.invoice()		|#ISGEC001097.n
			read.main.table()
		endif
		rprt_close()
	else
		choice.again()
	endif
	
| choice.print.crdb:
| on.choice:
| 	credit.debit.manual.data()


|****************************** field section *********************************
field.cprj.f:
when.field.changes:
	cprj.t = cprj.f
									|#ISGEC016028.sn
before.zoom:
	project.zoom.filter()	

field.cprj.t:
before.zoom:
	project.zoom.filter()
									|#ISGEC016028.en
field.indt.f:
when.field.changes:
	indt.t = indt.f
	
field.ncmp.f:
when.field.changes:
	ncmp.t = ncmp.f	

field.ncmp.t:
check.input:
	if	ncmp.f > ncmp.t	then
		message("To Company cannot be greater than From Company")
		set.input.error("", e)
	endif	
									|#ISGEC016028.sn
field.bpid.f:								
when.field.changes:
	bpid.t	=	bpid.f						
before.zoom:
	customer.zoom.filter()
field.bpid.t:
before.zoom:
	customer.zoom.filter()
									|#ISGEC016028.en
	
|****************************** function section ******************************

functions:

function read.main.table()
{
	domain	tcyesno		tmp.remb
	domain	tcccty		tmp.rtyp
	domain	tcamnt		o.adv.amnt		|#ISGECDV001065.n
	domain	tcamnt		o.rtn.amnt		|#ISGECDV001065.n
	domain	tfgld.ttyp	prv.ttyp		|#ISGECDV001065.n
	domain	tfgld.docn	prv.docn		|#ISGECDV001065.n
	domain	tccom.bpid	o.ofbp		|#ISGEC01036.n
	
| 	tcmcs.dll0095.read.parm("ciisg000")			|ISGEC001217.o
| 	if curr.comp = 700 then				|ISGEC001217.sn		|GH288CR000.cng
	if curr.comp = 700 or curr.comp = 651 then				|GH288CR000.a
		tcmcs.dll0095.read.parm("ciisg000")
	else	
		select	ciisg000.*
		from	ciisg000
		where	ciisg000.indt <= :curr.date
		and	ciisg000._compnr = 200
		order by ciisg000.indt desc
		as set with 1 rows
		selectdo
		endselect
	endif							|ISGEC001217.en
	
	
	get.date.utc(indt.f, 00, 00, 00, v.indt.f)
	get.date.utc(indt.t, 23, 59, 59, v.indt.t)
	
	select	tcemm170.comp
	from	tcemm170
	where	tcemm170._index1 inrange {:ncmp.f}	and	{:ncmp.t}
	selectdo
		o.ofbp = ""
		switch.to.company(tcemm170.comp)			|#ISGEC001221.sn
		comp = tcemm170.comp
| 		if comp = 700 then					
		if comp = 700 or comp = 651 then			|GH288CR000.cng
			
		else
			comp = 200
		endif							|#ISGEC001221.en	
			
		select	cisli235.cprj
			, cisli235.ofbp:o.ofbp		|#ISGEC01036.n						|#ISGEC016028.n
| 		from	cisli235							|#ISGEC016028.o
		from	cisli235, tppdm740						|#ISGEC016028.n					
		where	cisli235.cprj	inrange :cprj.f		and	:cprj.t
		and	cisli235.codt	inrange :v.indt.f	and	:v.indt.t
		and	cisli235.stat	in	(cisli.stat.printed, cisli.stat.posted)
		and	tppdm740._index1	=	{cisli235.cprj}			|#ISGEC016028.n
		and	tppdm740.ofbp 		inrange	:bpid.f		and	:bpid.t	|#ISGEC016028.n	
		and	tppdm740._compnr 	=	:comp				|#ISGEC016028.n		|#ISGEC001221.n		
| 		group by cisli235.cprj							|#ISGEC016028.o
		group by cisli235.ofbp, cisli235.cprj					|#ISGEC016028.n
| 		order by cisli235.cprj							|#ISGEC016028.o
		order by cisli235.ofbp, cisli235.cprj					|#ISGEC016028.n
		selectdo
									|#ISGEC001167.sn
			if determine.project.type(cisli235.cprj) = type.f then
			else
				continue
			endif
									|#ISGEC001167.en
			initialize.variable()
			prv.ttyp = ""				|#ISGECDV001065.n
			prv.docn = 0				|#ISGECDV001065.n
			select	cisli235.ofbp, cisli235.nins, cisli235.amth, cisli235.txah,
				cisli235.cprj, cisli235.invt, cisli235.piln,			|#ISGEC001193.n
				cisli235.slcp, cisli235.ityp, cisli235.idoc			|#ISGECDV001065.n
			from	cisli235				
			where	cisli235.cprj = :cisli235.cprj
			and	cisli235.codt inrange :v.indt.f	and	:v.indt.t
			and	cisli235.stat	in	(cisli.stat.printed, cisli.stat.posted)
			and	cisli235.ofbp = :o.ofbp
			order by cisli235.ityp, cisli235.idoc				|#ISGECDV001065.n
			selectdo
				tmp.remb = empty
				tmp.rtyp = ""
| 				cisli235.ofbp	=	tppdm740.ofbp				|#ISGEC016028.n		|#ISGEC01036.o
				select	tcmcs052.dsca
				from	tcmcs052
				where	tcmcs052._index1 = {:cisli235.cprj}
				and	tcmcs052._compnr = :cisli235.slcp
				selectdo
				selectempty
					tcmcs052.dsca = ""
				endselect	
					
				select	tppin020.cdf_remb:tmp.remb,
					tppin020.cdf_rtyp:tmp.rtyp
				from	tppin020
				where	tppin020._index1 = {:cisli235.cprj, :cisli235.ofbp, :cisli235.nins}
				and	tppin020._compnr = :cisli235.slcp
				selectdo
				endselect
				if	tmp.remb = tcyesno.no	then
					rep.bamt = rep.bamt + cisli235.amth(1)
				else
					if	not isspace(tmp.rtyp)	then
						if	trim$(tmp.rtyp) = trim$(ciisg000.exii)	or
							trim$(tmp.rtyp) = trim$(ciisg000.hcex)	or
							trim$(tmp.rtyp) = trim$(ciisg000.scex)	then
							rep.rem.edmt = rep.rem.edmt + cisli235.amth(1)
						else
							if	trim$(tmp.rtyp) = trim$(ciisg000.csti)	then
								rep.rem.samt = rep.rem.samt + cisli235.amth(1)
							else
								if	trim$(tmp.rtyp) = trim$(ciisg000.vati)	or
									trim$(tmp.rtyp) = trim$(ciisg000.srvi)	or
									trim$(tmp.rtyp) = trim$(ciisg000.hcsv)	or
									trim$(tmp.rtyp) = trim$(ciisg000.scsv)	then		|#ISGEC001079.n
| 									trim$(tmp.rtyp) = trim$(ciisg000.scsv)	or		|#ISGEC001079.so
| 									trim$(tmp.rtyp) = trim$(ciisg000.frgt)	then		|#ISGEC001079.eo
									rep.rem.samt = rep.rem.samt + cisli235.amth(1)
								else									|#ISGEC001079.sn	
									if	trim$(tmp.rtyp) = trim$(ciisg000.frgt)	then			
										rep.misc.claim = rep.misc.claim + cisli235.amth(1)
									endif								|#ISGEC001079.en
								endif
							endif	
						endif
					endif
				endif
											|#ISGEC017017.sn
				if	trim$(tmp.rtyp)	=	trim$(ciisg000.pnfp)	then
					rep.misc.claim	=	rep.misc.claim	+	cisli235.amth(1)
				endif							
											|#ISGEC017017.en				
| 				rep.samt = rep.samt + cisli235.txah(1)		|#ISGEC001193.o
										|#ISGEC001193.sn
				scr.samt = 0
				scr.sces = 0
				scr.shcs = 0
				scr.kkcs = 0	|#ISGECFIN015.n
				scr.sbcs = 0	|#ISGECFIN015.n
				scr.cgst = 0.00	|#ISGECGST004.sn
				scr.sgst = 0.00	
				scr.igst = 0.00	
				scr.cess = 0.00	|#ISGECGST004.en
				Get_Service_Sales_Tax(cisli235.slcp, cisli235.cprj, cisli235.ofbp, cisli235.invt, cisli235.piln,
						scr.samt, scr.sces, scr.shcs, scr.sbcs, scr.kkcs,
						scr.cgst, scr.sgst, scr.igst, scr.cess)	|#ISGECGST004.n
				rep.rem.edmt = rep.rem.edmt + scr.shcs				|
				rep.rem.samt = rep.rem.samt + scr.sces				|#ISGEC016028.n
| 				rep.samt = rep.samt + scr.samt 			|#ISGECFIN015.o
				rep.samt = rep.samt + scr.samt + scr.kkcs + scr.sbcs		|#ISGECFIN015.n
				
				rep.cgst.amnt = rep.cgst.amnt + scr.cgst	|#ISGECGST004.sn
				rep.sgst.amnt = rep.sgst.amnt + scr.sgst
				rep.igst.amnt = rep.igst.amnt + scr.igst
				rep.cess.amnt = rep.cess.amnt + scr.cess	|#ISGECGST004.en
										
										|#ISGEC001193.en
										|#ISGECDV001065.sn
				o.adv.amnt = 0					
				o.rtn.amnt = 0					
				if	prv.ttyp <> cisli235.ityp	or prv.docn <> cisli235.idoc	then
					get.advance.retention(cisli235.ityp, cisli235.idoc, o.adv.amnt, o.rtn.amnt)
					rep.advn = rep.advn + o.adv.amnt
					rep.retn = rep.retn + o.rtn.amnt
					prv.ttyp = cisli235.ityp
					prv.docn = cisli235.idoc
				endif
										|#ISGECDV001065.en
			endselect
			rep.gross.val = rep.bamt + rep.samt + rep.rem.edmt + rep.rem.samt + rep.misc.claim +
					rep.cgst.amnt + rep.sgst.amnt + rep.igst.amnt+ rep.cess.amnt |#ISGECGST004.n
			rep.nt.inmt = rep.gross.val - (rep.advn + rep.retn)		|#ISGECDV001065.n
			rprt_send()
		endselect
		switch.to.company(curr.comp)
	endselect	
}

							|#ISGEC001193.sn
function Get_Service_Sales_Tax(
			domain	tcncmp		in.slcp,
			domain	tccprj		in.cprj,
			domain	tccom.bpid	in.ofbp,
			domain	tcinvt		in.invt,
			domain	cisli.piln	in.piln,
		ref	domain	tcamnt		ot.samt,
		ref	domain	tcamnt		ot.sces,
		ref	domain	tcamnt		ot.shcs,
		ref	domain	tcamnt		ot.sbcs,|#ISGECFIN015.n
		ref	domain	tcamnt		ot.kkcs,|#ISGECFIN015.n
		ref	domain	tcamnt		ot.cgst,|#ISGECGST004.sn
		ref	domain	tcamnt		ot.sgst,	
		ref	domain	tcamnt		ot.igst,	
		ref	domain	tcamnt		ot.cess	|#ISGECGST004.en	
				)
{
	select	cisli936.itax, cisli936.txah
	from	cisli936
	where	cisli936._index1 = {:in.slcp, :in.cprj, :in.ofbp, :in.invt, :in.piln}
	selectdo
		on case cisli936.itax
			case tctax.indt.l.service:		|Service Tax
			case tctax.indt.l.lst:			|Local Sales Tax
			case tctax.indt.l.cst:			|Central Sales Tax
			case tctax.indt.l.vat:			|VAT
			case tctax.indt.l.n.a:			|Not Applicable
				ot.samt = ot.samt + cisli936.txah(1)
				break
			case tctax.indt.l.e.cess.service:			|Service CESS 
				ot.sces = ot.sces + cisli936.txah(1)
				break
			case tctax.indt.l.hse.cess.servic:			|Service SHE CESS 	
				ot.shcs = ot.shcs + cisli936.txah(1)
				break
								|#ISGECFIN015.sn
			case tctax.indt.l.sb.cess:			|Swacch Bharat Cess	
				ot.sbcs = ot.sbcs + cisli936.txah(1)
				break		
			case tctax.indt.l.kk.cess:			|Krishi Kalyan Cess	
				ot.kkcs = ot.kkcs + cisli936.txah(1)
				break				|#ISGECFIN015.en
			case tctax.indt.l.cgst:			|CGST	|#ISGECGST004.sn	
				ot.cgst = ot.cgst + cisli936.txah(1)
				break	
			case tctax.indt.l.sgst:			|SGST		
				ot.sgst = ot.sgst + cisli936.txah(1)
				break
			case tctax.indt.l.utgst:			|UTGST		|#ISGEC01094.sn	
				ot.sgst = ot.sgst + cisli936.txah(1)
				break							|#ISGEC01094.en
			case tctax.indt.l.igst:			|IGST		
				ot.igst = ot.igst + cisli936.txah(1)
				break	
			case tctax.indt.l.cess:			|Cess		
				ot.cess = ot.cess + cisli936.txah(1)
				break				|#ISGECGST004.en
		ENDCASE
	endselect	
}
							|#ISGEC001193.en

function initialize.variable()
{
	rep.bamt = 0
	rep.samt = 0
	rep.rem.edmt = 0
	rep.misc.claim = 0
	rep.rem.samt = 0
	rep.gross.val = 0
	rep.advn = 0						|#ISGECDV001065.n
	rep.retn = 0						|#ISGECDV001065.n
	rep.nt.inmt = 0						|#ISGECDV001065.n
	rep.cgst.amnt = 0.00	|#ISGECGST004.sn
	rep.sgst.amnt = 0.00
	rep.igst.amnt = 0.00
	rep.cess.amnt = 0.00	|#ISGECGST004.en
}

function get.date.utc(
		domain	tcdate	i.date,
		long		i.hh,
		long		i.mn,
		long		i.ss,
	ref	domain	tcdate	o.date
			)
{
	long	dd, mm, yy, hh, mn, ss
	utc.to.date(i.date, yy , mm, dd, hh, mn, ss)
	o.date = date.to.utc(yy, mm, dd, i.hh, i.mn, i.ss)
	if	o.date < 0	then
		o.date = 0
	endif
}

								|#ISGECDV001065.sn
function get.advance.retention(
			domain	tfgld.ttyp	i.ttyp,
			domain	tfgld.docn	i.docn,
		ref	domain	tcamnt		o.advn,
		ref	domain	tcamnt		o.retn
				)
{
	o.advn = 0
	o.retn = 0
	select	tfacr200.amth, tfacr200.trec, 
		tfacr200.tdoc			|#ISGEC01156.n
		,tfacr200.docd						|#ISGEC016028.n
	from	tfacr200
	where	tfacr200._index1 = {:i.ttyp, :i.docn}
	and	tfacr200.docn <> 0
	and	tfacr200.trec in (tfacr.trec.assignment, tfacr.trec.credit)
	selectdo
		on case tfacr200.trec
		case tfacr.trec.assignment:
			if tfacr200.tdoc = "AAR" then	|#ISGEC01055.n
				if	tfacr200.docd	>	date.to.num(2016,05,31)	then	|#ISGEC016028.sn
					select	a_tfacr200.docd	
					from	tfacr200	a_tfacr200
					where	a_tfacr200._index1	=	{:i.ttyp,:i.docn}
					and	a_tfacr200.docn		=	0
					and	a_tfacr200.docd		=	:tfacr200.docd
					as	set with 1 rows
					selectdo
					selectempty
						tfacr200.amth(1)	=	0	
					endselect							
				endif												|#ISGEC016028.en				
				o.advn = o.advn + abs(tfacr200.amth(1))
			endif				|#ISGEC01055.n	
				break
			case tfacr.trec.credit:
				o.retn = o.retn + abs(tfacr200.amth(1))
				break
		EndCAsE
	endselect	
}				
								|#ISGECDV001065.en

							|#ISGEC001097.sn
function print.composed.invoice()
{
	get.utc.to.date(indt.f, v.date.f)
	get.utc.to.date(indt.t, v.date.t)
	select	tcemm170.comp
	from	tcemm170
	where	tcemm170._index1 inrange {:ncmp.f}	and	{:ncmp.t}
	selectdo
| 		switch.to.company(tcemm170.comp)
		switch.to.company(tcemm170.comp)			|#ISGEC001221.sn
		comp = tcemm170.comp
| 		if comp = 700 then
		if comp = 700 or comp = 651 then			|GH288CR000.cng
		else
			comp = 200
		endif							|#ISGEC001221.en	
		
		select	ciisg999.proj
			, ciisg999.ofbp							|#ISGEC016028.n
| 		from	ciisg999							|#ISGEC016028.o
		from	ciisg999, tppdm740						|#ISGEC016028.n							
		where	ciisg999._index3 inrange {:cprj.f}	and	{:cprj.t}
		and	ciisg999.date inrange :v.date.f	and	:v.date.t
		and	tppdm740._index1 	=	{ciisg999.proj}			|#ISGEC016028.n
		and	tppdm740.ofbp		inrange	:bpid.f	and	:bpid.t		|#ISGEC016028.n
		and	tppdm740._compnr 	=	:comp				|#ISGEC016028.n		|#ISGEC001221.n
| 		group by ciisg999.proj							|#ISGEC016028.o				
		group by ciisg999.ofbp, ciisg999.proj					|#ISGEC016028.n
| 		order by ciisg999.proj							|#ISGEC016028.o
		order by ciisg999.ofbp, ciisg999.proj					|#ISGEC016028.n
		selectdo
									|#ISGEC001167.sn
			if determine.project.type(ciisg999.proj) = type.f then
			else
				continue
			endif
									|#ISGEC001167.en
			
			initialize.variable()
			cisli235.ofbp	=	tppdm740.ofbp				|#ISGEC016028.n
			cisli235.cprj = ciisg999.proj
			select	ciisg999.date, ciisg999.cprc, ciisg999.stax, ciisg999.edre,
				ciisg999.stre, ciisg999.misc, ciisg999.advc, ciisg999.rete,
				ciisg999.pris, tcmcs052.dsca
			from	ciisg999, tcmcs052				
			where	ciisg999._index3 = {:ciisg999.proj}
			and	ciisg999.date inrange :v.date.f	and	:v.date.t
			and	ciisg999.proj refers to tcmcs052 unref Clear
			selectdo
				rep.bamt = rep.bamt + ciisg999.cprc			|#ISGEC001128.n
| 				rep.bamt = rep.bamt + ciisg999.pris			|#ISGEC001128.o
				rep.rem.edmt = rep.rem.edmt + ciisg999.edre
				rep.rem.samt = rep.rem.samt + ciisg999.stre
				rep.misc.claim = rep.misc.claim + ciisg999.misc
				rep.samt = rep.samt + ciisg999.stax
				rep.advn = rep.advn + ciisg999.advc
				rep.retn = rep.retn + ciisg999.rete						
			endselect
			rep.gross.val = rep.bamt + rep.samt + rep.rem.edmt + rep.rem.samt + rep.misc.claim
			rep.nt.inmt = rep.gross.val - (rep.advn + rep.retn)
			rprt_send()
		endselect
		switch.to.company(curr.comp)
	endselect	
}

function get.utc.to.date(
			domain	tcdate		i.utc,
		ref	domain	tfgld.date	o.dte	
			)
{
	long	dd, mm, yy, hh, mn, ss
	utc.to.date(i.utc, yy , mm, dd, hh, mn, ss)
	o.dte = date.to.num(yy, mm, dd)
	if	o.dte < 0	then
		o.dte = 0
	endif
}

										|#ISGEC001097.en




function domain tdisg.type determine.project.type
			(
				domain	tccprj		i.cprj
			)
{
	select 	tppdm740.ofbp,
		tppdm740.ccur,
		tccom130.ccty,
		tppdm600.ncmp	
		, tccom100.nama						|#ISGEC016028.n
	from	tppdm740,tccom100,tppdm600,tccom130
	where	tppdm740._index1 = {:i.cprj}
	and	tppdm740.ofbp refers to tccom100
	and	tppdm740.cprj refers to tppdm600
	and	tccom100.cadr refers to tccom130
	and	tppdm740._compnr = :comp			|#ISGEC015062.n  |#ISGEC001221.n
	as set with 1 rows
	selectdo
	endselect
	
	select	tcemm170.lcur
	from	tcemm170
	where	tcemm170._index1 = {:tppdm600.ncmp}
	and	tcemm170.lcur = :tppdm740.ccur
	as set with 1 rows
	selectdo
		select	tccom000.ccty
		from	tccom000
		where	tccom000._index1 = {0,:tppdm600.ncmp}
		selectdo
			if tccom000.ccty <> tccom130.ccty then
				return(tdisg.type.export)
			else	
				return(tdisg.type.domestic)
			endif	
		endselect
	selectempty
		return(tdisg.type.export)
	endselect
	
	return(tdisg.type.domestic)
}
											|#ISGEC016028.sn
function	customer.zoom.filter()
{
	on	case	type.f
	case	tdisg.type.export:
		query.extend.where.in.zoom("tccom100.ccur <> '"&"INR"&"'")
		break
	case	tdisg.type.domestic:
		query.extend.where.in.zoom("tccom100.ccur =  '"&"INR"&"'")
		break
	endcase	
}

function	project.zoom.filter()
{
	string	v.strn(300)
	
	v.strn	=	"tppdm600.cprj in(select tppdm740.cprj from tppdm740 where tppdm740._index2 inrange '"&bpid.f&"' and '"&bpid.t&"')"
	
	query.extend.where.in.zoom(v.strn)
}
											|#ISGEC016028.en
											
|----------------------------------------------------------GH434CR748 -----------------------------------------------------------------

function credit.debit.manual.data()
{	
| 	brp.id = brp.open("rtpisg640001105", "D", 1)
	long dd, mm, yy, hh, min1, ss, dd2, mm2, yy2, hh2, min2, ss2
	dd = 0
	mm = 0
	yy = 0
	hh = 0
	min1 = 0
	ss = 0
	
| 	get.date.utc(indt.f, 00, 00, 00, v.indt.f)
| 	get.date.utc(indt.t, 23, 59, 59, v.indt.t)
	utc.to.date(indt.f, yy, mm , dd, hh, min1, ss)
	utc.to.date(indt.t, yy2, mm2 , dd2, hh2, min2, ss2)
	
	date.f = date.to.num(yy, mm, dd)
	date.t = date.to.num(yy2, mm2, dd2)
	
	select	ciisg804.comp,
		ciisg804.tran,
		ciisg804.docn
	from	ciisg804
	where	ciisg804._index1 inrange {:ncmp.f} and {:ncmp.t}
	and	ciisg804.cprj inrange :cprj.f and :cprj.t
	group by ciisg804.comp, ciisg804.tran, ciisg804.docn
	selectdo
		select	tfacr200.docd,
			tfacr200.itbp,
			tccom100.nama
		from	tfacr200, tccom100
		where	tfacr200._index1 = {:ciisg804.tran, :ciisg804.docn}
| 		and	tfacr200.proj = :ciisg804.cprj
		and	tfacr200.docd inrange :date.f and :date.t
		and	tfacr200.itbp inrange :bpid.f and :bpid.t
		and	tfacr200.docn = 0
		and	tfacr200._compnr = :ciisg804.comp
		and	tfacr200.itbp refers to tccom100
		selectdo
			select	ciisg805.*
			from	ciisg805
			where	ciisg805._index1 = {:ciisg804.comp, :ciisg804.tran, :ciisg804.docn}
			selectdo
				initialize.variable()
								
				select	sum(ciisg804.amnt):rep.bamt,
					sum(ciisg804.cgsa):rep.cgst.amnt,
					sum(ciisg804.sgsa):rep.sgst.amnt,
					sum(ciisg804.igsa):rep.igst.amnt,
					sum(ciisg804.totl):rep.gross.val
| 					sum(ciisg804.lsad):rep.advn,
| 					sum(ciisg804.lsrt):rep.retn
				from	ciisg804
				where	ciisg804._index1 = {:ciisg805.comp, :ciisg805.tran, :ciisg805.docn, :ciisg805.dbcr}
				selectdo
					select	ciisg804.cprj
					from	ciisg804
					where	ciisg804._index1 = {:ciisg805.comp, :ciisg805.tran, :ciisg805.docn, :ciisg805.dbcr}
					as set with 1 rows
					selectdo
					endselect
					
					select	tcmcs052.dsca
					from	tcmcs052
					where	tcmcs052._index1 = {:ciisg804.cprj}
					as set with 1 rows
					selectdo
					selectempty
						tcmcs052.dsca = ""
					endselect
					
					get.advance.retention(ciisg805.tran, ciisg805.docn, rep.advn, rep.retn)
				selectempty
					rep.bamt = 0 
					rep.cgst.amnt = 0
					rep.sgst.amnt = 0
					rep.igst.amnt = 0
					rep.gross.val = 0
					rep.advn = 0
					rep.retn = 0
				endselect
				
				rep.nt.inmt = rep.gross.val - (rep.advn + rep.retn)
| 				brp.ready(brp.id)
				rprt_send()
			endselect
		endselect
	endselect
| 	brp.close(brp.id)
}






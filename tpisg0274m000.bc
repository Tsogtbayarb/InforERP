|******************************************************************************
|* tpisg0274m000  0  VRC B61U a7 isg 
|* Update Reviewer for SAR
|* Merino1                       
|* 2016-02-27
|******************************************************************************
|* Main table tpisg074 SAR Information, Form Type 4
|******************************************************************************
|* 
|* GH242CR505, Adarsh Pal, 17-01-2020, VRC B61U a7 isg
|* Modifications in logic of New Reviewer where Project Team only displayed in zoom
|*
|* ID: GH353CR658, RAvi Kumar, 29-06-2020
|* SAR Improvement points
|****************************** declaration section ***************************
declaration:

	table   ttpisg074 | SAR Information

	extern  domain  tccprj         cprj
	extern  domain  tcmcs.long	sarn
	extern  domain  tpisg.stat	stat
	extern	domain	tcemno		rper,rper.new
	extern	domain	tcnama		rper.nama, rper.new.nama
	string	query.string(200)
	

|****************************** program section ********************************
before.program:

|****************************** group section **********************************

group.1:
init.group:
   get.screen.defaults()
|****************************** choice section **********************************
choice.update:
before.choice:
	if isspace(cprj) then
		message("Input Project")
		choice.again()
	endif

	if sarn = 0 then
		message("Input SAR")
		choice.again()
	endif

	if isspace(rper.new) then
		message("Input New Reviewer")
		choice.again()
	endif
	
	select	tpisg074.stat
	from	tpisg074
	where	tpisg074._index1 = {:cprj, :sarn}
	selectdo
		if tpisg074.stat = tpisg.stat.created or tpisg074.stat = tpisg.stat.review then
		else
			message("Status not Under Review")
			choice.again()
		endif
	endselect
	
on.choice:
	select tpisg074.rper
	from	tpisg074 for update
	where	tpisg074._index1 = {:cprj,:sarn}
	selectdo
		tpisg074.rper = rper.new
		tpisg074.srrd = date.num()
		db.update(ttpisg074,db.retry,e)
		if not e then
			message("Reviewer Updated Successfully")
			commit.transaction()
		else
			abort.transaction()	
		endif		
	endselect	

|****************************** field section **********************************
field.cprj:
before.zoom:
	query.string ="tpisg074.stat = tpisg.stat.review"
	query.extend.where.in.zoom(query.string) 

field.sarn:
before.zoom:
	query.extend.where.in.zoom("tpisg074.cprj = " & quoted.string(cprj) & " and tpisg074.stat = tpisg.stat.review ")
	
	
field.stat:
before.display:
| 	stat = tpisg.stat.review					|RAvi.com.06082020
	stat = empty							|RAvi.a.s.06082020
	select	tpisg074.stat:stat
	from	tpisg074
	where	tpisg074._index1 = {:cprj, :sarn}
	as set with 1 rows
	selectdo
	selectempty
		stat = empty
	endselect							|RAvi.a.e.06082020
	
field.rper:
before.display:
	select	tpisg074.rper
	from	tpisg074
	where	tpisg074._index1 = {:cprj,:sarn}
	selectdo
		rper = tpisg074.rper
	selectempty
		rper = ""
	endselect		
									|#GH242CR505.sn
field.rper.new:
before.zoom:
| 	query.extend.where.in.zoom("tccom001.loco in(select dmisg133.logn from dmisg133 where dmisg133.cprj = " & quoted.string(cprj) & ")")
	query.extend.where.in.zoom("dmisg152.cprj = " & quoted.string(cprj))	|GH353CR658.cng
									|#GH242CR505.sn
									
check.input:
	select	dmisg152.ecod
	from	dmisg152
	where	dmisg152._index1 = {:cprj, :rper.new}
	selectdo
	selectempty
		message("User not allowed")
		set.input.error("",e)
	endselect
									
field.rper.nama:
before.display:
	select	tccom001.nama:rper.nama
	from	tccom001
	where	tccom001._index1 = {:rper}
	as set with 1 rows
	selectdo
	selectempty
		rper.nama = ""
	endselect

field.rper.new.nama:
before.display:
	select	tccom001.nama:rper.new.nama
	from	tccom001
	where	tccom001._index1 = {:rper.new}
	as set with 1 rows
	selectdo
	selectempty
		rper.new.nama = ""
	endselect

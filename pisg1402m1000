|******************************************************************************
|* tfisg1402m100  0  VRC B61U a7 live
|* Print Finalized / Non - Finalized Voucher
|* Installation user             
|* 2014-10-06
|******************************************************************************
|* Form Type 4
|******************************************************************************
|*
|* ID ISGECDV001079,	Sujeet Kumar,		2014-10-06
|* New Report Development.
|* Functional: Mr. Deepak Rawat
|*
|* ISGEC001093, IT0205, Arjit Gupta, VRC B61U a7 isg, Dt. 13-10-2014
|* Logic Added for Interim for category income tax
|* Add logic to get Bill of Lading
|****************************** declaration section ***************************
declaration:
	table	ttfgld102
	table	ttfgld106
	table	ttfgld107
	table	ttfgld018
	table	ttfgld100

|******** Form Field ***********************************************************

	extern	domain	tfgld.year		fyer.f
	extern	domain	tcncmp			ncmp.f
	extern	domain	tfgld.btno		btno.f
	extern	domain	tfgld.btno		btno.t
	extern	domain	tfgld.ttyp		ttyp.f
	extern	domain	tfgld.ttyp		ttyp.t
	extern	domain	tfgld.docn		docn.f
	extern	domain	tfgld.docn		docn.t
|********** Report Variables ***************************************************	
	extern 	domain 	tcncmp		rep.company
	extern 	domain 	tcncmp		rep.target.comp
	extern	domain	tfgld.year	rep.fiscal.year
	extern 	domain	tfgld.prod	rep.fiscal.prod
	extern	domain	tfgld.year	rep.tax.year
	extern	domain	tfgld.prod	rep.tax.prod
	extern 	domain	tfgld.btno	rep.batch
	extern	domain	tfgld.ttyp	rep.ttyp
	extern	domain	tfgld.docn	rep.docn
	extern	domain	tfgld.date	rep.dcdt
	extern	domain	tfgld.leac	rep.ledger
	extern	domain	tfgld.desc	rep.ledger.dsca
	extern	domain	tfgld.dimx	rep.dimension1
	extern	domain	tfgld.desc	rep.dim1.dsca
	extern	domain	tfgld.dimx	rep.dimension2
	extern	domain	tfgld.desc	rep.dim2.dsca
	extern	domain	tfgld.dimx	rep.dimension3
	extern	domain	tfgld.desc	rep.dim3.dsca
	extern	domain	tfgld.dimx	rep.dimension4
	extern	domain	tfgld.desc	rep.dim4.dsca
	extern	domain	tfgld.dimx	rep.dimension5
	extern	domain	tfgld.desc	rep.dim5.dsca
	extern	domain	tfgld.dbcr	rep.dbcr
	extern	domain	tfgld.amnt	rep.amount
	extern	domain	tccom.bpid	rep.bpid
	extern	domain	tcnama		rep.bp.nama
	extern 	domain	tcnama		rep.user.name
	extern 	domain	tcnama		rep.fina.name
	extern	domain	tfgld.btno	rep.btno
	extern	domain	tfgld.amnt	rep.amth
	extern	domain	tfgld.amnt	rep.forgn.amnt
	extern	domain	tfcmg.cheq	rep.cheque
	extern	domain	tfgld.ttyp	rep.sales.ityp
	extern	domain	tfgld.docn	rep.sales.idoc
	extern	domain	tcmcs.str30	rep.tran.type
	extern	domain	tcdsca		rep.comp.dsca
	extern	domain	tccprj		rep.cprj
	extern	domain	tcorno		rep.orno
	extern	domain	tcccur		rep.ccur
	extern	domain	tcmcs.str8	rep.table
	extern	domain	tcdsca		rep.biln			|#ISGEC001093.n

	extern	domain	tfgld.desc	sort.field1
	extern	domain	tcmcs.str60	sort.field2
	
|****************************** program section ********************************


|****************************** group section **********************************

group.1:
init.group:
   get.screen.defaults()

|****************************** choice section ********************************

choice.cont.process:
on.choice:
   execute(print.data)

choice.print.data:
on.choice:
	select 	tfgld001.ncmp
	from	tfgld001
	where	tfgld001._index1 = {:ncmp.f}
	as set with 1 rows
	selectdo
	selectempty
		message("Enter Valid Financial Company")
		choice.again()
	endselect
	switch.to.company(ncmp.f)
   if rprt_open() then
       read.main.table()
       rprt_close()
   else
       choice.again()
   endif


|****************************** field section *********************************
field.fyer.f:
before.display:
	fyer.f = get.fiscal.year(date.num())
field.ncmp.f:
before.display:
	ncmp.f = get.compnr()

field.btno.f:
when.field.changes:
	btno.t = btno.f
before.zoom:
	tfgld100.year = fyer.f

field.btno.t:
before.zoom:
	tfgld100.year = fyer.f

field.ttyp.f:
when.field.changes:
	ttyp.t = ttyp.f
	
field.docn.f:
when.field.changes:
	docn.t = docn.f
before.zoom:
	tfgld018.ttyp = ttyp.f

field.docn.t:
before.zoom:
	tfgld018.ttyp = ttyp.t
	


|****************************** function section ******************************

functions:

function domain tfgld.year get.fiscal.year
				(
					domain	tfgld.date	i.date
				)
{
	domain	tfgld.year	o.year
	domain	tfgld.date	o.stdt
	
	select tfgld005.year:o.year,
		tfgld005.stdt:o.stdt
	from	tfgld005
	where	tfgld005._index1 = {1}
	and	tfgld005.stdt <= :i.date
	order by tfgld005.stdt desc
	as set with 1 rows
	selectdo
	selectempty
		o.year = 0
	endselect
	
	return(o.year)
}
function read.main.table()
{
	non.finalized.transactions()
	finalized.transactions()
}

function non.finalized.transactions()
{
	domain tcccur	o.local.currency
	
	rep.tran.type = "Non-Finalized Transactions"
	rep.table = "tfgld102"
	select 	tfgld102.cono,
		tfgld102.tcom,
		tfgld102.fyer,
		tfgld102.fprd,
		tfgld102.vyer,
		tfgld102.vprd,
		tfgld102.btno,
		tfgld102.year,
		tfgld102.ttyp,
		tfgld102.docn,
		tfgld102.lino,
		tfgld102.serl,
		tfgld102.dcdt,
		tfgld102.leac,
		tfgld102.dim1,
		tfgld102.dim2,
		tfgld102.dim3,
		tfgld102.dim4,
		tfgld102.dim5,
		tfgld102.wtsc,
		tfgld102.dbcr,
		tfgld102.vamh,
		tfgld102.amth,
		tfgld102.amnt,
		tfgld102.ccur,
		tfgld102.bpid,
		tfgld102.catg
	from	tfgld102
	where	tfgld102._index1 inrange {:ncmp.f,:fyer.f,:btno.f,:ttyp.f,:docn.f}
				and	{:ncmp.f,:fyer.f,:btno.t,:ttyp.t,:docn.t}
	order by tfgld102.btno
	selectdo
		assign.report.variables(
				tfgld102.cono,		|Company
				tfgld102.tcom,		|Target Company
| 				tfgld102.fyer,		|Fiscal Year
				tfgld102.year,		|Fiscal Year
				tfgld102.fprd,		|Fiscal Period
				tfgld102.vyer,		|Tax Year
				tfgld102.vprd,		|Tax Period
				tfgld102.btno,		|Batch Number
				tfgld102.ttyp,		|Transaction Type
				tfgld102.docn,		|Document Number
				tfgld102.dcdt,		|Document Date
				tfgld102.leac,		|Ledger Account
				tfgld102.dim1,		|Dimension 1
				tfgld102.dim2,		|Dimension 2
				tfgld102.dim3,		|Dimension 3
				tfgld102.dim4,		|Dimension 4
				tfgld102.dim5,		|Dimension 5
				tfgld102.dbcr,		|Debit/Credit
				tfgld102.amth(1),	|Amount		
				tfgld102.bpid)		|Business Partner
		tcemm.dll0170.get.company.local.currency( tfgld102.cono,tcyesno.no, o.local.currency) 	
		tcemm.dll0170.get.company.description( tfgld102.cono, tcyesno.no, rep.comp.dsca )
		if tfgld102.ccur <> o.local.currency then
			rep.forgn.amnt = tfgld102.amnt
		endif
		
		rep.ccur = tfgld102.ccur
		
		on case  tfgld102.catg
		case	tfgld.catg.purchase.inv:
		case	tfgld.catg.purchase.cred:
			get.purchase.order(tfgld102.cono,tfgld102.ttyp,tfgld102.docn,rep.cprj,rep.orno)
			break
		endcase
		
		
		rprt_send()
		
		if tfgld102.wtsc = tcyesno.yes or tfgld102.vamh(1) <> 0 then
			interim.tax.table(tfgld102.cono,tfgld102.ttyp,tfgld102.docn,
					tfgld102.lino,tfgld102.serl,tfgld102.dbcr,
					tfgld102.fprd,tfgld102.vprd,tfgld102.dcdt,
					tfgld102.bpid)
		endif
		
	endselect		
}

function interim.tax.table
			(
				domain	tcncmp		i.cono,		|Company
				domain	tfgld.ttyp	i.ttyp,		|Transactin Type
				domain	tfgld.docn	i.docn,		|Document
				domain	tfgld.lino	i.lino,		|Line
				domain	tfgld.serl	i.serl,		|Sequence No.
				domain	tfgld.dbcr	i.dbcr,		|Debit/Credit
				domain	tfgld.prod	i.fprd,		|Fiscal Period
				domain	tfgld.prod	i.vprd,		|Tax Period
				domain	tfgld.date	i.dcdt,		|Document Date
				domain	tccom.bpid	i.bpid
			)
{
	domain	tfgld.dbcr	tmp.dbcr
	tmp.dbcr = i.dbcr
	
	select 	tfgld107.cono,
		tfgld107.tcom,
		tfgld107.year,
		tfgld107.btno,
		tfgld107.year,
		tfgld107.ttyp,
		tfgld107.docn,
		tfgld107.vlac,
		tfgld107.dim1,
		tfgld107.dim2,
		tfgld107.dim3,
		tfgld107.dim4,
		tfgld107.dim5,
		tfgld107.ktax,
		tfgld107.vlac,
		tfgld107.vamh
	from	tfgld107
	where	tfgld107._index1 = {:i.cono,:i.ttyp,:i.docn,:i.lino,:i.serl}
	selectdo
		
		i.dbcr = tmp.dbcr
		on case tfgld107.ktax
			case	tcktax.vat:	
			case	tcktax.stamp.tax:	
			case	tcktax.social.contrib:	
			case	tcktax.excl.of.withhld:	
			case	tcktax.tax.settlement:	
			case	tcktax.not.applicable:	
				
				if i.dbcr = tfgld.dbcr.credit then
				else
				endif
				
				break
			case	tcktax.income.tax:
				if tfgld107.vamh(1) > 0 then
					i.dbcr = tfgld.dbcr.credit		|#ISGEC001093.n
				else
					i.dbcr = tfgld.dbcr.debit		|#ISGEC001093.n
				endif
				
				break
			case	tcktax.social.cont.exp:
					if i.dbcr = tfgld.dbcr.credit then
						i.dbcr = tfgld.dbcr.debit
					else
						i.dbcr = tfgld.dbcr.credit
					endif
				break
		endcase
		
		assign.report.variables(
				tfgld107.cono,		|Company
				tfgld107.tcom,		|Target Company
				tfgld107.year,		|Fiscal Year
				i.fprd,			|Fiscal Period
				tfgld107.year,		|Tax Year
				i.vprd,			|Tax Period
				tfgld107.btno,		|Batch Number
				tfgld107.ttyp,		|Transaction Type
				tfgld107.docn,		|Document Number
				i.dcdt,			|Document Date
				tfgld107.vlac,		|Ledger Account
				tfgld107.dim1,		|Dimension 1
				tfgld107.dim2,		|Dimension 2
				tfgld107.dim3,		|Dimension 3
				tfgld107.dim4,		|Dimension 4
				tfgld107.dim5,		|Dimension 5
				i.dbcr,			|Debit/Credt
				tfgld107.vamh(1),	|Amount	
				i.bpid)			|Business Partner
		
		
		rprt_send()
	endselect
}

function get.purchase.order
			(
				domain	tcncmp		i.cono,		|Financial Comapny
				domain	tfgld.ttyp	i.ttyp,		|Transaction Type
				domain	tfgld.docn	i.docn,		|Document Number
			ref 	domain	tccprj		o.cprj,		|Project
			ref 	domain	tcorno		o.orno		|Purchase Order
			)
{
	domain	tcpono		o.pono
	domain	tcpono		o.sqnb
	
	
	select tfacp251.orno:o.orno,
		tfacp251.pono:o.pono,
		tfacp251.sqnb:o.sqnb
	from	tfacp251
	where	tfacp251._index1 = {:i.cono,:i.ttyp,:i.docn}
	and	tfacp251.otyp = 1
	as set with 1 rows
	selectdo
		select 	tdpur401.cprj:o.cprj
		from	tdpur401
		where	tdpur401._index1 = {:o.orno,:o.pono,:o.sqnb}
		and	tdpur401._compnr = 200				|#36096.n
		as set with 1 rows
		selectdo
		selectempty
			o.cprj = ""
		endselect
	selectempty
		o.orno = ""
		o.cprj = ""
	endselect
	
}


function finalized.transactions()
{
	domain	tfgld.user	o.user
	domain tcccur	o.local.currency
	
	rep.tran.type = "Finalized Transactions"
	rep.table = "tfgld106"
	
	
	select 	tfgld106.otyp,
		tfgld106.odoc,
		tfgld106.ocmp,
		tfgld106.fyer,
		tfgld106.fprd,
		tfgld106.vyer,
		tfgld106.vprd,
		tfgld106.obat,
		tfgld106.dcdt,
		tfgld106.leac,
		tfgld106.dim1,
		tfgld106.dim2,
		tfgld106.dim3,
		tfgld106.dim4,
		tfgld106.dim5,
		tfgld106.dbcr,
		tfgld106.amth,
		tfgld106.bpid,
		tfgld106.oyer,
		tfgld106.amnt,
		tfgld106.ccur,
		tfgld106.trun,
		tfgld106.catg
	from	tfgld106
	where	tfgld106._index1 inrange {:ttyp.f,:docn.f} and {:ttyp.t,:docn.t}
	and	tfgld106.obat inrange :btno.f and :btno.t
	and	tfgld106.oyer = :fyer.f
	and	tfgld106.ocmp = :ncmp.f
	and	tfgld106.amth(1) <> 0
	order by tfgld106.obat 
	selectdo
		assign.report.variables(
				tfgld106.ocmp,		|Company
				tfgld106.ocmp,		|Target Company
				tfgld106.fyer,		|Fiscal Year
				tfgld106.fprd,		|Fiscal Period
				tfgld106.vyer,		|Tax Year
				tfgld106.vprd,		|Tax Period
				tfgld106.obat,		|Batch Number
				tfgld106.otyp,		|Transaction Type
				tfgld106.odoc,		|Document Number
				tfgld106.dcdt,		|Document Date
				tfgld106.leac,		|Ledger Account
				tfgld106.dim1,		|Dimension 1
				tfgld106.dim2,		|Dimension 2
				tfgld106.dim3,		|Dimension 3
				tfgld106.dim4,		|Dimension 4
				tfgld106.dim5,		|Dimension 5
				tfgld106.dbcr,		|Debit/Credt
				tfgld106.amth(1),	|Amount	
				tfgld106.bpid)
		

		tcemm.dll0170.get.company.local.currency( tfgld106.ocmp,tcyesno.no, o.local.currency) 
		tcemm.dll0170.get.company.description( tfgld106.ocmp, tcyesno.no, rep.comp.dsca )
		if tfgld106.ccur <> o.local.currency then
			rep.forgn.amnt = tfgld106.amnt
		endif
		
		rep.ccur = tfgld106.ccur
		
		on case  tfgld106.catg
			case	tfgld.catg.purchase.inv:
			case	tfgld.catg.purchase.cred:
				get.purchase.order(tfgld106.ocmp,tfgld106.otyp,tfgld106.odoc,rep.cprj,rep.orno)
				break
		endcase
		
		select tfgld109.trus:o.user 
		from	tfgld109
		where	tfgld109._index1 = {:tfgld106.oyer,:tfgld106.trun}
		as set with 1 rows
		selectdo
		endselect
		
		rep.fina.name = get.user.name(o.user)	
		
		rprt_send()
	endselect
}


function initilized.report.variables()
{
	
	rep.company 	= 0
	rep.target.comp = 0 
	rep.fiscal.year = 0
	rep.fiscal.prod = 0
	rep.tax.year 	= 0
	rep.tax.prod 	= 0
	rep.batch	= 0
	rep.ttyp 	= ""
	rep.docn 	= 0
	rep.dcdt 	= 0
	rep.ledger 	= ""
	rep.ledger.dsca = ""
	rep.dimension1 	= ""
	rep.dim1.dsca 	= ""
	rep.dimension2 	= ""
	rep.dim2.dsca 	= ""
	rep.dimension3	= ""
	rep.dim3.dsca 	= ""
	rep.dimension4 	= ""
	rep.dim4.dsca 	= ""
	rep.dimension5 	= ""
	rep.dim5.dsca 	= ""
	rep.amount 	= 0.0
	rep.dbcr 	= empty
	sort.field1 	= ""
	sort.field2 	= ""
	rep.user.name 	= ""
	rep.cheque	= ""
	rep.btno 	= 0
	rep.amth	= 0
	rep.sales.ityp 	= ""
	rep.sales.idoc 	= 0
	rep.fina.name 	= ""
	rep.forgn.amnt	= 0 
	rep.comp.dsca	= ""
| 	rep.cprj	= ""
| 	rep.orno	= ""
}

function assign.report.variables
			(
				domain	tcncmp		i.cono,		|Company
				domain	tcncmp		i.tcom,		|Target Company
				domain	tfgld.year	i.fyer,		|Fiscal Year
				domain	tfgld.prod	i.fprd,		|Fiscal Period
				domain	tfgld.year	i.vyer,		|Tax Year
				domain	tfgld.prod	i.vprd,		|Tax Period
				domain	tfgld.btno	i.btno,		|Batch Number
				domain	tfgld.ttyp	i.ttyp,		|Transaction Type
				domain	tfgld.docn	i.docn,		|Document Number
				domain	tfgld.date	i.dcdt,		|Document Date
				domain	tfgld.leac	i.leac,		|Ledger Account
				domain	tfgld.dimx	i.dim1,		|Dimension 1
				domain	tfgld.dimx	i.dim2,		|Dimension 2
				domain	tfgld.dimx	i.dim3,		|Dimension 3
				domain	tfgld.dimx	i.dim4,		|Dimension 4
				domain	tfgld.dimx	i.dim5,		|Dimension 5
				domain	tfgld.dbcr	i.dbcr,		|Debit/Credit
				domain	tfgld.amnt	i.amth,		|Amount
				domain	tccom.bpid	i.bpid		|Business Partner
			)
{
	initilized.report.variables()
		
	rep.company 	= i.cono
	rep.target.comp = i.tcom
	rep.fiscal.year = i.fyer
	rep.fiscal.prod = i.fprd
	rep.tax.year 	= i.vyer
	rep.tax.prod 	= i.vprd
	rep.batch	= i.btno
	rep.ttyp 	= i.ttyp
	rep.docn 	= i.docn
	rep.dcdt 	= i.dcdt
	rep.ledger 	= i.leac
	rep.ledger.dsca = get.ledger.description(i.leac)
	rep.dimension1 	= i.dim1
	rep.dim1.dsca 	= get.dimension.description(1,i.dim1)
	rep.dimension2 	= i.dim2
	rep.dim2.dsca 	= get.dimension.description(2,i.dim2)
	rep.dimension3	= i.dim3
	rep.dim3.dsca 	= get.dimension.description(3,i.dim3)
	rep.dimension4 	= i.dim4
	rep.dim4.dsca 	= get.dimension.description(4,i.dim4)
	rep.dimension5 	= i.dim5
	rep.dim5.dsca 	= get.dimension.description(5,i.dim5)
	
	rep.dbcr 	= i.dbcr
	rep.amount 	= i.amth
	
	rep.bpid 	= i.bpid
	rep.bp.nama 	= get.bp.name(i.bpid)
	
	rep.user.name	= get.user.code(rep.fiscal.year,rep.batch)
	
	rep.cheque	= get.cheque.number(rep.ttyp,rep.docn)
	
	payment.or.direct.debit.batch(rep.ttyp,rep.docn,rep.btno,rep.amth)
	
| 	get.sales.invoice.document(rep.ttyp,rep.docn,rep.sales.ityp,rep.sales.idoc)		|#36084.o
| 	get.sales.invoice.document(rep.target.comp,rep.ttyp,rep.docn,rep.sales.ityp,rep.sales.idoc)		|#36084.n
								|#ISGEC001093.sn
	rep.biln = ""
	Get_Bill_of_Lading(i.ttyp, i.docn, rep.biln)
								|#ISGEC001093.en
	
	sort.field1 = rep.ttyp & "/" & str$(rep.docn)
	sort.field2 = strip$(rep.ledger) & " " & strip$(rep.dimension1) & " " & strip$(rep.dimension2)
^			& " " & strip$(rep.dimension3) & " " & strip$(rep.dimension4) 
^			& " " & strip$(rep.dimension5) 
	
}

function domain tfgld.desc get.ledger.description
			(
				domain	tfgld.leac	i.leac
			)
{
	domain	tfgld.desc	o.desc
	
	select	tfgld008.desc:o.desc
	from	tfgld008
	where	tfgld008._index1 = {:i.leac}
	as set with 1 rows
	selectdo
	selectempty
		o.desc = ""
	endselect
	
	return(o.desc)
}

function domain tfgld.desc get.dimension.description
			(
				domain	tfgld.dimn	i.dtyp,		|Dimension Type
				domain	tfgld.dimx	i.dimx		|Dimension
			)
{
	domain 	tfgld.desc	o.desc
	
	select	tfgld010.desc:o.desc
	from	tfgld010
	where	tfgld010._index1 = {:i.dtyp,:i.dimx}
	as set with 1 rows
	selectdo
	selectempty
		o.desc = ""
	endselect
	
	return(o.desc)
}


function domain tcnama	get.bp.name
			(
				domain	tccom.bpid	i.bpid
			)
{
	domain	tcnama	o.nama
	
	select	tccom100.nama:o.nama
	from	tccom100
	where	tccom100._index1 = {:i.bpid}
	as set with 1 rows
	selectdo
	selectempty
		o.nama = ""
	endselect
	
	return(o.nama)
}


function domain tcnama get.user.code
			(
				domain	tfgld.year	i.year,
				domain	tfgld.btno	i.btno
			)
{
	domain 	tfgld.user	o.user
	domain	tcnama		o.name
	
	select 	tfgld100.user:o.user
	from	tfgld100
	where	tfgld100._index1 = {:i.year,:i.btno}
	as set with 1 rows
	selectdo
		o.name = get.user.name(o.user)		
	selectempty
		o.name = ""
	endselect
	
	return(o.name)
}


function domain tcnama get.user.name
			(
				domain	tfgld.user	i.user
			)
{
	domain 	tcnama	o.name
	
	select 	ttaad200.name:o.name
	from	ttaad200
	where	ttaad200._index1 = {:i.user}
	and	ttaad200._compnr = 0
	as set with 1 rows
	selectdo
	selectempty
		o.name = ""
	endselect
	return(o.name)
}

function domain tfcmg.cheq get.cheque.number
			(
				domain	tfgld.ttyp	i.ttyp,
				domain	tfgld.docn	i.docn
			)
{
	domain	tfcmg.cheq	o.cheq
	
	select tfcmg100.cheq:o.cheq
	from	tfcmg100
	where 	tfcmg100.typo = :i.ttyp
	and	tfcmg100.doco = :i.docn
	selectdo
	selectempty
		select tfcmg100.cheq:o.cheq
		from	tfcmg100
		where 	tfcmg100.ttyp = :i.ttyp
		and	tfcmg100.docn = :i.docn
		selectdo
		selectempty
			o.cheq = ""
		endselect
	endselect
	
	return(o.cheq)
}


function payment.or.direct.debit.batch
			(
				domain	tfgld.ttyp	i.ttyp,
				domain	tfgld.docn	i.docn,
			ref	domain	tfgld.btno	o.btno,
			ref 	domain	tfgld.amnt	o.amth
			)
{
	
	select 	tfcmg103.btno:o.btno, 
		tfcmg103.amth(1):o.amth
	from	tfcmg103
	where	tfcmg103.ttyp = :i.ttyp
	and	tfcmg103.docn = :i.docn
	as set with 1 rows
	selectdo
	selectempty
		select 	tfcmg403.btno:o.btno, 
			tfcmg403.amth(1):o.amth
		from	tfcmg403
		where	tfcmg403.ttyp = :i.ttyp
		and	tfcmg403.docn = :i.docn
		as set with 1 rows
		selectdo
		selectempty
			o.btno = 0
			o.amth = 0
		endselect
	endselect
}


function get.sales.invoice.document
			(
				domain	tcncmp		i.ncmp,		|#36084.n
				domain	tfgld.ttyp	i.ttyp,
				domain	tfgld.docn	i.docn,
			ref 	domain	tfgld.ttyp	o.ityp,
			ref 	domain	tfgld.docn	o.idoc
			)
{
									|#36084.sn
	if tfisg.dll1402.get.sales.invoice.from.ir(i.ncmp,i.ttyp,i.docn,
		o.ityp,o.idoc) then
	else
	endif
									|#36084.en
	
					


| 						|#36084.so
| 	select 	tfisg001.ityp:o.ityp,
| 		tfisg001.idoc:o.idoc
| 	from	tfacp100,tfisg001
| 	where	tfacp100._index4 = {:i.ttyp,:i.docn}
| 	and 	tfacp100.ninv refers to tfisg001
| 	as set with 1 rows
| 	selectdo
| 	selectempty
| 		o.ityp = ""
| 		o.idoc = 0
| 	endselect
						|#36084.eo
}

						|#ISGEC001093.sn
function Get_Bill_of_Lading
			(
			domain	tfgld.ttyp	in.ttyp,
			domain	tfgld.docn	in.docn,
		ref	domain	tcdsca		ot.biln
			)
{
	domain	whinh.shpm	t_rcno
	domain	tcpono		t_rseq, t_pono
	domain	tcorno		t_orno, t_cinv, t_bold
	domain	tcitem		t_item
	
	t_rcno = ""
	t_orno = ""
	t_cinv = ""
	t_bold = ""
	t_item = ""
	t_rseq = 0
	t_pono = 0
	
	select	bl_tfacp251.rcno:t_rcno,
		bl_tfacp251.rseq:t_rseq,
		bl_tfacp251.orno:t_orno,
		bl_tfacp251.pono:t_pono
	from	tfacp251 bl_tfacp251
	where	bl_tfacp251.ityp = :in.ttyp
	and	bl_tfacp251.idoc = :in.docn
	order by bl_tfacp251.rcno desc, bl_tfacp251.rseq desc
	as set with 1 rows
	selectdo
		select	bl_tdpur401.item:t_item
		from	tdpur401 bl_tdpur401
		where	bl_tdpur401._index1 = {:t_orno, :t_pono}
		as set with 1 rows
		selectdo
		endselect
		
		select	bl_tdisg832.cinv:t_cinv
		from	tdisg832 bl_tdisg832
		where	bl_tdisg832._index4 = {:t_rcno, :t_rseq}
		and	bl_tdisg832.item = :t_item
		and	bl_tdisg832.cinv <> "         " 
		selectdo
		endselect
		
		select	bl_tdisg831.bold:t_bold
		from	tdisg831 bl_tdisg831
		where	bl_tdisg831._index3 = {:t_cinv}
		as set with 1 rows
		selectdo
		endselect
		
		select	bl_tdisg839.biln:ot.biln
		from	tdisg839 bl_tdisg839
		where	bl_tdisg839._index1 = {:t_bold}
		selectdo
		endselect
	endselect	
}			
									|#ISGEC001093.en
